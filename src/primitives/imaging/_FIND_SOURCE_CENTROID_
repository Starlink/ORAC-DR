#+
# Name:
#    _FIND_SOURCE_CENTROID_
#
# Purpose:
#    Finds the centroid of a point source within the current frame.
#
# Language:
#    Perl5
#
# Description:
#    This primitive finds the centroid of a point source within a box
#    either centred or displaced from the centre by the telescope
#    offsets and optional additional offsets.  The centroid is measured
#    in pixel co-ordinates.  The point source can be either positive or
#    negative with respect to the background level.
#
#    It defines an initial search box around the expected positions,
#    which is shifted to the maximum or minimum detected pixel within
#    the initial box, depending on the polarity of the source being
#    detected.  There is optional smoothing to remove small noise and
#    hot pixels before locating the extreme-value pixel.
#
# Arguments:
#    ANGLE = REAL (Given)
#       Rotation angle of the declination axis with respect to the
#       frame's y axis measured counter clockwise.
#    BOXSIZE = REAL (Given)
#       The size in arcseconds of the search box in which to find the
#       object's approximate position, using the brightest pixel, or
#       the minimum pixel when POSITIVE is false. [5]
#    CENTRE( 2 ) = REAL (Returned)
#       The centroid pixel co-ordinates.
#    DECOFF = REAL (Given)
#       The approximate declination offset of the centroid of the point
#       source to be measured with respect to the nominal position.  The
#       nominal position is the declination centre of the array plus
#       the Declination telescope offset.  [0.0]
#    FOUND = LOGICAL (Returned)
#       If true, a centroid was found.
#    INIT( 2 ) = REAL (Returned)
#       The initial pixel co-ordinates for the centroid search.
#    POSITIVE = LOGICAL (Given)
#       If 1 (true), the point source is positive, i.e. has values above
#       the sky level.  If 0 (false), the point source is negative. [1]
#    RAOFF = REAL (Given)
#       The approximate right-ascension offset of the centroid of the
#       point source to be measured with respect to the nominal position.
#       The nominal position is the R.A. centre of the array plus the
#       R.A. telescope offset.  [0.0]
#    SMOOTH = LOGICAL (Given)
#       If true (1), a 5-by-5-pixel block median smooth is applied before
#       searching for the extreme value.  This removes noise and hot
#       spots from mislocating the search window.  [1]
#
# Notes:
#    -  This primitive is suitable for infra-red imaging instruments.
#    -  Processing only occurs for object frames.
#    -  The reference pixel is derived from the frame centre and
#    displacements obtained from the referenceoffset calibration
#    (defaulting to null offsets).
#    -  Error messages are issued if the centroid position contains a
#    bad value or the returned status is not OK. 
#    -  Header ORAC_ROTATION---the angle of the chip's y axis with
#    respect to North (positive is anti-clockwise)---is used to convert
#    sky co-ordinate displacements into pixels using a simple rotation
#    matrix.  If this header is absent, no rotation is assumed.   
#    This formulation is satisfactory for the UKIRT's instrument scales
#    and sizes, and its declination range.  A more-sophisticated
#    transformation would be needed near the poles or for wide fields.
#    - Works with either KAPPA V0.13 or V0.14.  The WCS Frame is set to
#    PIXEL for the latter.
#    -  Should the extreme value lie within the outer 10% or 3 pixels of
#    the edge of the initial region, a new region of the same size but 
#    centred on the extreme value is used, and its extreme value
#    supplied as the initial centroid position.  This allows for the
#    source being slightly displaced from its nominal position, e.g.
#    the telescope offsets have shifted.
#    -  The centroid search box is set to twice the number of pixels in
#    0.5 arcseconds plus one, subject to the constraint of being between
#    9 and 51.
#
# Tasks:
#    KAPPA: BLOCK, CENTROID, STATS, WCSFRAME.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC, Starlink)
#
# Copyright:
#    Copyright (C) 1998-2004 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# This should only be performed on OBJECT frames.
    if ( $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "OBJECT" ) {

# Obtain arguments.
# =================

# Process as positive or negative source.
       my $positive = $_FIND_SOURCE_CENTROID_{POSITIVE};
       $positive = defined( $positive ) ? $positive : 1;

# Smooth before finding the extreme pixel value.
       my $smooth = $_FIND_SOURCE_CENTROID_{SMOOTH};
       $smooth = defined( $smooth ) ? $smooth : 1;

# Obtain the relative offsets of the point-source images.
       my $rraoff = $_FIND_SOURCE_CENTROID_{RAOFF};
       $rraoff = defined( $rraoff ) ? $rraoff : 0.0;
       my $rdecoff = $_FIND_SOURCE_CENTROID_{DECOFF};
       $rdecoff = defined( $rdecoff ) ? $rdecoff : 0.0;

# Obtain the rotation angle in degrees.  The numeric default was UFTI
# and time specific.  Now the ideal no-rotation is a compromise default.
       my $defrot = $Frm->uhdr( "ORAC_ROTATION" );
       $defrot = defined( $defrot ) ? $defrot : 0.0;
       my $rotangle = ( $_FIND_SOURCE_CENTROID_{ANGLE} || $defrot );

# Define the size of the box in arcseconds, and hence pixels. 
       my $boxsize = ( $_FIND_SOURCE_CENTROID_{BOXSIZE} || 5 );

# Find pixel limits of the search box.
# ====================================

# Obtain frame central pixel.
       _GET_FRAME_CENTRE_
       my $xmid = $_GET_FRAME_CENTRE_{XCENTRE};
       my $ymid = $_GET_FRAME_CENTRE_{YCENTRE};

# Obtain the referenceoffset from the calibration system using null
# offsets if it cannot extract the x and y displacements successfully.
# There must be two offset co-ordinates.
       my @disp = ( 0, 0 );
       my $offset = eval { $Cal->referenceoffset };
       if ( !$@ ) {
          if ( $#$offset == 1 ) {
             @disp = @$offset;
          }
       }

# Define some useful variables to apply the rotation matrix.
       my $pi = atan2( 1, 1 ) * 4;
       my $dtor = $pi / 180.0;
       my $cosrot = cos( $rotangle * $dtor );
       my $sinrot = sin( $rotangle * $dtor );
 
# Obtain the plate scale and pixel increments along the sky co-ordinate axes.
       _GET_PLATE_SCALE_
       my $ra_pixelscale = $_GET_PLATE_SCALE_{RASCALE};
       my $dec_pixelscale = $_GET_PLATE_SCALE_{DECSCALE};
       my $platescale = $_GET_PLATE_SCALE_{PLATESCALE};

# Obtain the approximate offsets with respect to the reference pixel.
       my $ra_off_as = $Frm->uhdr( "ORAC_RA_TELESCOPE_OFFSET" ) + $rraoff;
       my $dec_off_as = $Frm->uhdr( "ORAC_DEC_TELESCOPE_OFFSET" ) + $rdecoff;

# Convert to pixel offsets of the object in the Cartesian sense.
       my $ra_off_p = -1.0 * $ra_off_as / $ra_pixelscale;
       my $dec_off_p = -1.0 * $dec_off_as / $dec_pixelscale;

# Apply the rotation matrix to derive the Cartesian offsets.
       my $xoff = $ra_off_p * $cosrot - $dec_off_p * $sinrot;
       my $yoff = $ra_off_p * $sinrot + $dec_off_p * $cosrot;

# Derive the centre co-ordinates of the box.
       my $xcentre = int( $xmid + $disp[ 0 ] + $xoff );
       my $ycentre = int( $ymid + $disp[ 1 ] + $yoff );

# Tell the user where we're looking.
       orac_print "Looking at ($xcentre, $ycentre) for an object in " . $Frm->file . ".\n";

# Store the initial centre position for use by calling script.
       my @init = ( $xcentre, $ycentre );
       $_FIND_SOURCE_CENTROID_{INIT} = \@init;

# Find the boxsize in pixels.  Note use the average, positive plate
# scale.
       my $pixbox = nint( $boxsize / $platescale );
       my $halfbox = int( $pixbox / 2 );

# Define what is close to the edge of the box.
       my $border = max( int( $pixbox * 0.1 ), 3 );

# Set the co-ordinate system to pixels.
# =====================================
       my ( $frameno, $param1, $ORAC_STATUS );
       my $ndf = $Frm->file;
       if ( starversion_gt( "KAPPA", 'V0.13-6' ) ) {

# Record the current WCS domain number.
          $Mon{ "ndfpack_mon" }->obeyw( "ndftrace", "ndf=$ndf" );
          ( $ORAC_STATUS, $frameno ) = $Mon{ "ndfpack_mon" }->get( "ndftrace", "current" );

# Switch to pixel co-ordinates for the registration.
          $param1 = "ndf=$ndf frame=pixel";
          $Mon{ "ndfpack_mon" }->obeyw( "wcsframe", "$param1" );
       }

# Smooth the data to avoid locating noise spots.
# ==============================================
       my $tmp = new ORAC::TempFile;
       my $tmpfile = $ndf;
       if ( $smooth ) {

# Create temporary file.
          $tmpfile = $tmp->file;

# Smooth the image using a median 5x5-pixel neighbourhood, to remove
# noise spikes and hot pixels.  This file is only used in the
# statistics stage.
          $param1 = "in=$ndf out=$tmpfile box=5 estimator=median";
          $Mon{ "kappa_mon" }->obeyw( "block", "$param1" );
       }

# Locate the centroid.
# ====================

# Specify a section which should contain the source.
       my $ndfsect = "ndf=${tmpfile}(${xcentre}~${pixbox},${ycentre}~${pixbox})";

# Locate the main object within a central box.
       $Mon{ "kappa_mon" }->obeyw( "stats", "$ndfsect" );

# Define search box size depending on the platescale within limits.
# Assume a typical FWHM of 0.5 arcsec.
       my $search = min( 51, max( 2 * int( 0.5 / $platescale ) + 1, 9 ) );

       my ( $param2, @maxpos, @minpos, $centroid_status );
       my $badint = -0x80000000;
       if ( $positive ) {

# Extract the location of the maximum pixel from the section.
          ( $ORAC_STATUS, @maxpos ) = $Mon{ "kappa_mon" }->get( "stats", "maxpos" );

# Validate the position.
          if ( $maxpos[ 0 ] != $badint ) {

# Is the peak close to the edge of the box?
             if ( abs( abs( $maxpos[0] - $xcentre ) - $halfbox ) <= $border ||
                  abs( abs( $maxpos[1] - $ycentre ) - $halfbox ) <= $border ) {
            
# Shift the search box to be centred on the highest value within the 
# search box.
                $ndfsect = "ndf=${tmpfile}($maxpos[0]~$pixbox,$maxpos[1]~$pixbox)";

# Locate the main object within the displaced box.  The repeated
# statistics allows for a displaced source just outside the original
# box, and previous peak value was only the periphery of the source.
                $Mon{ "kappa_mon" }->obeyw( "stats", "$ndfsect" );

# Extract the location of the maximum pixel from the shifted section.
                ( $ORAC_STATUS, @maxpos ) = $Mon{ "kappa_mon" }->get( "stats", "maxpos" );
             }
          }

# Validate the position.
          if ( $maxpos[ 0 ] != $badint ) {

# Shift the centroid search box to be centred on the highest value within the 
# search box.
             $ndfsect = $Frm->file."($maxpos[0]~$pixbox,$maxpos[1]~$pixbox)";

# Specify the other parameters for the CENTROID stage.
             $param1 = "ndf=$ndfsect search=$search";
             if ( starversion_gt( "KAPPA", 'V0.13-6' ) ) {
                $param2 = "mode=interface init='$maxpos[0],$maxpos[1]'";
             } else {
                $param2 = "mode=interface init=[$maxpos[0],$maxpos[1]] cosys=w";
             }

# Find the centroid of the object.  For the moment start the search at
# the frame centre.  The standard position may be displaced from this,
# and should be written to the headers.
             $centroid_status = $Mon{ "kappa_mon" }->obeyw( "centroid", "$param1 $param2" );
          } else {
             orac_err "Unable to find the position of the maximum value for centroiding.  " .
                      "Data are probably all bad.\n";
          }

       } else {

# Extract the location of the minimum pixel of the section.
          ( $ORAC_STATUS, @minpos ) = $Mon{ "kappa_mon" }->get( "stats", "minpos" );

# Validate the position.
          if ( $minpos[ 0 ] != $badint ) {

# Is the peak close to the edge of the box?
             if ( abs( abs( $minpos[0] - $xcentre ) - $halfbox ) <= $border ||
                  abs( abs( $minpos[1] - $ycentre ) - $halfbox ) <= $border ) {
            
# Shift the search box to be centred on the lowest value within the 
# search box.
                $ndfsect = "ndf=${tmpfile}($minpos[0]~$pixbox,$minpos[1]~$pixbox)";

# Locate the main object within the displaced box.  The repeated
# statistics allows for a displaced source just outside the original
# box, and previous peak value was only the periphery of the source.
                $Mon{ "kappa_mon" }->obeyw( "stats", "$ndfsect" );

# Extract the location of the minimum pixel from the shifted section.
                ( $ORAC_STATUS, @minpos ) = $Mon{ "kappa_mon" }->get( "stats", "minpos" );
             }
          }

# Validate the position.
          if ( $minpos[ 0 ] != $badint ) {

# Shift the search box to be centred on the lowest value within the 
# search box.
             $ndfsect = $Frm->file."($minpos[0]~$pixbox,$minpos[1]~$pixbox)";

# Specify the other parameters for the CENTROID stage.
             $param1 = "ndf=$ndfsect nopositive search=$search";
             if ( starversion_gt( "KAPPA", 'V0.13-6' ) ) {
                $param2 = "mode=interface init='$minpos[0],$minpos[1]'";
             } else {
                $param2 = "mode=interface init=[$minpos[0],$minpos[1]] cosys=w";
             }

# Find the centroid of the object.  For the moment start the search at
# the frame centre.  The standard position may be displaced from this,
# and should be written to the headers.
             $centroid_status = $Mon{ "kappa_mon" }->obeyw( "centroid", "$param1 $param2" );
          } else {
             orac_err "Unable to find the position of the maximum value for centroiding.  " .
                      "Data are probably all bad.\n";
          }
       }

# Watch for possible error conditions.
# ====================================

# Check that a centroid has been found.
       my $found = 1;

       if ( !defined( $centroid_status ) || $centroid_status != ORAC__OK ) {
          orac_err "Unable to find a centroid within a box of $boxsize arcseconds centred at ($xcentre,$ycentre).\n";
          $found = 0;

       } else {

# Store the located centroid within a text file of the format PSF expects.
# (There should be some rationalisation in KAPPA and CCDPACK to use
# Clive Davenhall's small tables.  In that case this would be altered.)
# From KAPPA V0.14 the returned value pair is a space-separated string,
# not an array of numbers.
          my @centre;
          if ( starversion_gt( "KAPPA", 'V0.13-6' ) ) {
             ( $ORAC_STATUS, my $centres ) = $Mon{ "kappa_mon" }->get( "centroid", "centre" );
             @centre = split( /\s+/, $centres );

# For V0.13 and earlier, an array of double-precision numbers are returned.
          } else {
             ( $ORAC_STATUS, @centre ) = $Mon{ "kappa_mon" }->get( "centroid", "centre" );

          }

# Check that the centre is not bad.
          if ( $centre[ 0 ] < -1.0E38 || $centre[ 1 ] < -1.0E38 ) {
             $found = 0;
             orac_err "Unable to find a centroid within a box of $boxsize arcseconds centred at ($xcentre,$ycentre).\n";
          }

          # Return the central position as integers.
          @centre = map { int( $_ ) } @centre;

# Store the centre information for use by calling script.
          $_FIND_SOURCE_CENTROID_{CENTRE} = \@centre;
       }

# Return whether or not a centroid was found.
       $_FIND_SOURCE_CENTROID_{FOUND} = \$found;

# Restore the former WCS domain.
# ==============================
       if ( starversion_gt( "KAPPA", 'V0.13-6' ) ) {
          $param1 = "ndf=$ndf frame=$frameno";
          $Mon{ "ndfpack_mon" }->obeyw( "wcsframe", "$param1" );
       }
    }

# Podule
# ======

=head1 NAME

_FIND_SOURCE_CENTROID_ -- Finds the centroid of a point source within the current frame.

=head1 DESCRIPTION

This primitive finds the centroid of a point source within a box
either centred or displaced from the centre by the telescope
offsets and optional additional offsets.  The centroid is measured
in pixel co-ordinates.  The point source can be either positive or
negative with respect to the background level.

It defines an initial search box around the expected positions,
which is shifted to the maximum or minimum detected pixel within
the initial box, depending on the polarity of the source being
detected.  There is optional smoothing to remove small noise and
hot pixels before locating the extreme-value pixel.

=head1 ARGUMENTS

=over 4

=item ANGLE = REAL (Given)

Rotation angle of the declination axis with respect to the
frame's y axis measured counter clockwise.

=item BOXSIZE = REAL (Given)

The size in arcseconds of the search box in which to find the
object's approximate position, using the brightest pixel, or
the minimum pixel when POSITIVE is false. [5]

=item CENTRE( 2 ) = REAL (Returned)

The centroid pixel co-ordinates.

=item DECOFF = REAL (Given)

The approximate declination offset of the centroid of the point source
to be measured with respect to the nominal position.  The nominal
position is the Declination centre of the array plus the declination
telescope offset.  [0.0]

=item FOUND = LOGICAL (Returned)

If true, a centroid was found.

=item INIT( 2 ) = REAL (Returned)

The initial pixel co-ordinates for the centroid search.

=item POSITIVE = LOGICAL (Given)

If 1 (true), the point source is positive, i.e. has values above
the sky level.  If 0 (false), the point source is negative. [1]

=item RAOFF = REAL (Given)

The approximate right-ascension offset of the centroid of the point
source to be measured with respect to the nominal position.
The nominal position is the R.A. centre of the array plus the
R.A. telescope offset.  [0.0]

=item SMOOTH = LOGICAL (Given)

If true (1), a 5-by-5-pixel block median smooth is applied before
searching for the extreme value.  This removes noise and hot
spots from mislocating the search window.  [1]

=back

=head1 NOTES

=over 4

=item *

This primitive is suitable for infra-red imaging instruments.

=item *

Processing only occurs for object frames.

=item *

The reference pixel is derived from the frame centre and displacements
obtained from the referenceoffset calibration (defaulting to null
offsets).

=item *

Error messages are issued if the centroid position contains a
bad value or the returned status is not OK. 

=item *

Header ORAC_ROTATION---the angle of the chip's y axis with respect
to North (positive is anti-clockwise)---is used to convert sky
co-ordinate displacements into pixels using a simple rotation
matrix.  If this header is absent, no rotation is assumed.   
This formulation is satisfactory for the UKIRT's instrument scales
and sizes, and its declination range.  A more-sophisticated
transformation would be needed near the poles or for wide fields.

=item *

Works with either KAPPA V0.13 or V0.14.  The WCS Frame is set to
PIXEL for the latter.

=item *

Should the extreme value lie within the outer 10% or 3 pixels of
the edge of the initial region, a new region of the same size but 
centred on the extreme value is used, and its extreme value
supplied as the initial centroid position.  This allows for the
source being slightly displaced from its nominal position, e.g.
the telescope offsets have shifted.

=item *

The centroid search box is set to twice the number of pixels in
0.5 arcseconds plus one, subject to the constraint of being between
9 and 51.

=back

=head1 TASKS

KAPPA: BLOCK, CENTROID, STATS, WCSFRAME.

=head1 AUTHORS

Malcolm J. Currie (mjc@star.rl.ac.uk)

=head1 COPYRIGHT

Copyright (C) 1998-2004 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
