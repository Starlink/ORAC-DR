#+
# Name:
#    _LAMP_FLAT_STEER_
#
# Purpose:
#    Steers processing for the LAMP_FLAT recipe.
#
# Language:
#    Perl5
#
# Description:
#    This primitive control processing for the LAMP_FLAT recipe through
#    steering headers listed below.
#
# Steering Headers:
#    CYCLE_NUMBER = INTEGER
#       Number of the cycle, a cycle being a set of frames to complete a
#       pass through the recipe.  The first cycle is 0.
#    DIFFERENCE_PAIR = LOGICAL
#       Whether or not to subtract pairs.  It is true every second frame.
#    MAKE_FLAT = LOGICAL
#       Whether or not to make the flat.  The flat is made once all the
#       jittered target frames in a cycle are available.
#    PAIR_ORDER = LOGICAL
#       Pair subtraction order, true means take second from the first,
#       and false means take the first from the second.  The value is
#       true.
#    POL_CYCLE_NUMBER = INTEGER
#       Number of the polarimetry cycle, a cycle being a set of frames to
#       complete a pass through the recipe for all waveplate angles.  The
#       first cycle is 0.
#    USE_VARIANCE = LOGICAL
#       Whether or not variance processing is to occur.  This equates to
#       argument USEVAR.
#    WAVEPLATE_FLAT = LOGICAL
#       See the argument of the same name.  This header merely propagates
#       the value of the argument.
#
# Arguments:
#    NUMBER = INTEGER (Given)
#       The number of frames in the group.  If absent, the number of
#       offsets, as given by internal header ORAC_NUMBER_OF_OFFSETS, minus
#       one is used.  If neither is available, 6 is used.  An error state
#       arises if the number of jittered frames is fewer than 6 or is
#       odd numbered.  []
#    USEVAR = LOGICAL (Given)
#       Whether or not to create and propagate variance arrays.  [0]
#    WAVEPLATE_FLAT  = LOGICAL (Given)
#       Whether or not to make a flat for each polarimeter waveplate angle.
#       For non-polarimetric data, the value ofd the argument is immaterial.
#       For polarimetric data, false (0) means combine all waveplate angles
#       to make the flat (there should be an equal number of each angle).  [0]
#
# Notes:
#    -  This primitive is suitable for ESO infra-red cameras with a
#    calibration lamp.
#    -  Processing only occurs for calibration-lamp frames.
#    -  The data are deemed to be polarimetry if the frame internal
#    header ORAC_FILTER contains the string "pol".
#    -  A list of the distinct filters within the group is stored in an
#    array stored by refereence in the group user header FILTER_LIST.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC, Starlink)
#
# Copyright:
#    Copyright (C) 1998-2003 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# This should only be performed on calibration-lamp frames.
    if ( $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "LAMP" ) {

# Form list of the filters.
# =========================

# Obtain the filter name from the frame header.
       my $filter = $Frm->uhdr( "ORAC_FILTER" );

# Create a new list of filters used within the group.
       my $filterlist;
       if ( ! defined $Grp->uhdr( "FILTER_LIST" ) ) {
          my @filters = ( $filter );
          $filterlist = \@filters;

# Obtain the list of filters already found.  Augment the list
# if the current filter is not in the list.  Uses a hash to store
# each new filter.
       } else {
          $filterlist = $Grp->uhdr( "FILTER_LIST" );
          push( @$filterlist, $filter );
          my %seen = ();
          my @distinct = ();
          foreach my $f ( @$filterlist ) {
             push( @distinct, $f ) unless $seen{ $f }++;
          }
          @$filterlist = @distinct;
       }

# Store the list in the group user header.
       $Grp->uhdr( "FILTER_LIST", $filterlist );

# Polarimetry flats
# =================

# Decide whether polarimetric data require a flat for each waveplate angle,
# or a combined flat.  The value is always zero for non-polarimetric data.
# Default to zero, as combining all angles seems to be the normal procedure.
       my $waveplate_flat = 0;
       if ( $Frm->uhdr( "ORAC_FILTER" ) =~ /pol/ ) {
          $waveplate_flat = $_LAMP_FLAT_STEER_{WAVEPLATE_FLAT};
          $waveplate_flat = defined( $waveplate_flat ) ? $waveplate_flat: 0;
       }

# Obtain the number of frames in the group.
# =========================================

# First see if a hardwired value has been supplied in the recipe.  This
# deals with old recipes with fixed numbers and cases where a frame or
# more is missing.
       my $number = $_LAMP_FLAT_STEER_{NUMBER};

# Otherwise use the ORAC_NUMBER_OF_OFFSETS user header.  In full ORAC,
# this stores the total number of offsets used, but ignores repeat
# iterators.  By convention there is always an offset to the origin at
# the end, hence the recipe requires ORAC_NUMBER_OF_OFFSETS-1.
       if ( ! defined ( $number ) ) {
          $number = $Frm->uhdr( "ORAC_NUMBER_OF_OFFSETS" );
          if ( defined( $number ) ) {
             $number--;

# Finally default to the original 6.
          } else {
             $number = 6;
          }
       }

# Report invalid number of frames.
       if ( $number < 6 ) {
          orac_throw "The number of frames ($number) is fewer than 6.\n";
       }

# The number of frames must be even, as they come in on and off pairs.
       if ( $number % 2 != 0 ) {
          orac_throw "The number of jittered frames ($number) is not a " .
                     "positive multiple of 2.\n";
       }

# Set steering headers.
# =====================

# Obtain the USEVAR argument.  Default to false.
       my $usevar = $_LAMP_FLAT_STEER_{USEVAR};
       $usevar = defined( $usevar ) ? $usevar : 0;
       $Frm->uhdr( "USE_VARIANCE", $usevar );

# Specify during processing of which frames should the pairs be subtracted.
       if ( ( $Grp->num + 1 ) % 2 == 0 ) {
          $Frm->uhdr( "DIFFERENCE_PAIR", 1 );

# A difference is to be made, but in what order.  PAIR_ORDER = 1 means
# take the second from the first of the pair.
          $Frm->uhdr( "PAIR_ORDER", 1 );

       } else {
          $Frm->uhdr( "DIFFERENCE_PAIR", 0 );
       }


# Specify during processing of which frames should the flat be created.
       if ( ( $Grp->num + 1 ) % $number == 0 && $Grp->num > 0 ) {
          $Frm->uhdr( "MAKE_FLAT", 1 );

       } else {
          $Frm->uhdr( "MAKE_FLAT", 0 );
       }

# Insert the cycle number of the set of NUMBER frames, unless it is
# polarimetry flats at each angle.  A complete cycle is four times
# the number of jitter positions.  Ideally, one would like to
# inquire the number of angles, but at the time of writing it is
# always four.
       my $cycleno = int( $Grp->num / $number );
       $Frm->uhdr( "CYCLE_NUMBER", $cycleno );
       if ( $waveplate_flat ) {
          $Frm->uhdr( "POL_CYCLE_NUMBER", int( $Grp->num / ( $number * 4 ) ) );
       } else {
          $Frm->uhdr( "POL_CYCLE_NUMBER", $cycleno );
       }

# Insert whether or not to make separate waveplate flats.
       $Frm->uhdr( "WAVEPLATE_FLAT", $waveplate_flat );
    }

# Podule
# ======

=head1 NAME

_LAMP_FLAT_STEER_ -- Steers processing for the LAMP_FLAT recipe.

=head1 DESCRIPTION

This primitive control processing for the LAMP_FLAT recipe through
steering headers listed below.

=head1 STEERING HEADERS

=over 4

=item CYCLE_NUMBER = INTEGER

Number of the cycle, a cycle being a set of frames to complete a
pass through the recipe.  The first cycle is 0.

=item DIFFERENCE_PAIR = LOGICAL

Whether or not to subtract pairs.  It is true every second frame.

=item MAKE_FLAT = LOGICAL

Whether or not to make the flat.  The flat is made once all the
jittered target frames in a cycle are available.

=item PAIR_ORDER = LOGICAL

Pair subtraction order, true means take second from the first,
and false means take the first from the second.  The value is
true.

=item POL_CYCLE_NUMBER = INTEGER

Number of the polarimetry cycle, a cycle being a set of frames to
complete a pass through the recipe for all waveplate angles.  The
first cycle is 0.

=item USE_VARIANCE = LOGICAL

Whether or not variance processing is to occur.  This equates to
argument USEVAR.

=item WAVEPLATE_FLAT = LOGICAL

See the argument of the same name.  This header merely propagates
the value of the argument.

=back

=head1 ARGUMENTS

=over 4

=item NUMBER = INTEGER (Given)

The number of frames in the group.  If absent, the number of
offsets, as given by internal header ORAC_NUMBER_OF_OFFSETS, minus
one is used.  If neither is available, 6 is used.  An error state
arises if the number of jittered frames is fewer than 6 or is
odd numbered.  []

=item USEVAR = LOGICAL (Given)

Whether or not to create and propagate variance arrays.  [0]

=item WAVEPLATE_FLAT  = LOGICAL (Given)

Whether or not to make a flat for each polarimeter waveplate angle.
For non-polarimetric data, the value ofd the argument is immaterial.
For polarimetric data, false (0) means combine all waveplate angles
to make the flat (there should be an equal number of each angle).  [0]

=back

=head1 NOTES

=over 4

=item *

This primitive is suitable for ESO infra-red cameras with a
calibration lamp.

=item *

Processing only occurs for calibration-lamp frames.

=item *

The data are deemed to be polarimetry if the frame internal header
ORAC_FILTER contains the string "pol".

=item *

A list of the distinct filters within the group is stored in an
array stored by refereence in the group user header FILTER_LIST.

=back

=head1 AUTHORS

Malcolm J. Currie (JAC, Starlink) <mjc@star.rl.ac.uk>

=head1 COPYRIGHT

Copyright (C) 1998-2003 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
