#+
# Name:
#    _CHOP_SKY_JITTER_STEER_
#
# Purpose:
#    Steers processing for CHOP_SKY_JITTER recipes.
#
# Language:
#    Perl5
#
# Description:
#    This primitive control processing for CHOP_SKY_JITTER recipes through
#    steering headers listed below.  It also reports whether each frame is
#    of the target or of some sky background.
#
# Steering Headers:
#    CYCLE_NUMBER = INTEGER
#       Number of the cycle, a cycle being a set of frames to complete a
#       pass through the recipe.  The first cycle is 0.
#    INITIAL_SKY = LOGICAL (Given)
#       Whether or not the first frame of the observation is of the sky.
#       For ESO data the observation comprises a series of alternating sky
#       and target frames in an object-sky-sky-object pattern, therefore
#       the value is false.
#    JITTER_NUMBER = INTEGER
#       The number of frames in the jitter.
#    MAKE_FLAT = LOGICAL
#       Whether or not to make the flat.  A flat is made once all the
#       frames are available.
#    MAKE_MOSAIC = LOGICAL
#       Whether or not register the frames and make the full mosaic.
#       The grand mosaic is made once all the frames in a cycle are
#       available.
#    REFERENCE_FRAME = LOGICAL
#       A true value specifies the reference sky frame to which all
#       subsequent skies are normalised.  It is true for the first frame
#       and false for all subsequent frames in the observation.
#    SUBTRACT_SKY = LOGICAL
#       Whether or not it is time to subtract the sky.  Sky subtraction
#       occurs once all the frames in a cycle are available.
#    TARGET_OR_SKY = CHARACTER
#       This is "target" for a target frame, and "sky" for a sky calibration
#       frame.  The target and sky alternate for these recipes.
#    TARGET_NUMBER = INTEGER
#       When TARGET_OR_SKY is "target", this counts the target frames,
#       starting from zero.  It is used for interpolation between sky
#       measurements.
#    USE_VARIANCE = LOGICAL
#       Whether or not variance processing is to occur.  This equates to
#       argument USEVAR.
#
# Arguments:
#    NUMBER = INTEGER (Given)
#       The number of target frames in the jitter.  It's the n in
#       CHOP_SKY_JITTERn.  If the argument is absent, a value is derived
#       from the number of offsets, as given by internal header
#       ORAC_NUMBER_OF_OFFSETS.  The formula is
#       (ORAC_NUMBER_OF_OFFSETS - 1) / 2.  An error results should
#       ORAC_NUMBER_OF_OFFSETS be even.  If neither is available, 4 is
#       used.  An error state arises if the number of jittered frames is
#       fewer than 3.  The minimum is 3 because this number of blank skies
#       are needed to form a flat field.  []
#    USEVAR = LOGICAL (Given)
#       Whether or not to create and propagate variance arrays.  [0]
#
# Notes:
#    -  This primitive is suitable for ISAAC or NACO in imaging mode.
#    -  Processing only occurs for object and sky frames.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC)
#
# Copyright:
#    Copyright (C) 1998-2003 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# This should only be performed on OBJECT frames.
    if ( $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "OBJECT" ||
         $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "SKY" ) {

# Obtain the number of frames in the jitter pattern.
# ==================================================

# First see if a hardwired value has been supplied in the recipe.  This
# deals with old recipes with fixed numbers and cases where a frame or
# more is missing.
       my $number = $_CHOP_SKY_JITTER_STEER_{NUMBER};

# Otherwise use the ORAC_NUMBER_OF_OFFSETS internal header.  In full ORAC,
# this stores the total number of offsets used, but ignores repeat iterators.
# By convention there is always an offset to the origin at the end.
# There must be an odd number of offsets (even number of frames).
# Mimic this for ESO.  So assuming the ABBA (object-sky-sky-object)
# and avoiding the numerical problem of a non-integer number,
# the recipe demands number target frames, and number skies.  With
# the mimicked return to the origin, that's an odd number.
       if ( ! defined ( $number ) ) {
          $number = $Frm->uhdr( "ORAC_NUMBER_OF_OFFSETS" );
          if ( defined( $number ) ) {
             if ( $number % 2 == 1 ) {
                $number = ( $number - 1 ) / 2;
             } else {
                orac_throw "The number of offsets ($number) is not odd, " .
                           "suggesting an additional bracketing sky " .
                           "frame or offset.\n";
             }

# Finally default to the 4 for a double ABBA.
          } else {
             $number = 4;
          }
       }

# Report invalid number of frames.
       if ( $number < 3 ) {
          orac_throw "The number of jittered frames ($number) is fewer " .
                     "than 3.\n";
       }

# Set steering headers.
# =====================

# Obtain the USEVAR argument.  Default to false.
       my $usevar = $_CHOP_SKY_JITTER_STEER_{USEVAR};
       $usevar = defined( $usevar ) ? $usevar : 0;
       $Frm->uhdr( "USE_VARIANCE", $usevar );

# Obtain the initial_sky argument.  Default to false for ESO
# AutoJitterOffset ABBA pattern.
       my $initial_sky = 0;
       $Frm->uhdr( "INITIAL_SKY", $initial_sky );

# Define handy variables: the frame number, the total number of
# frames in a cycle, and the frame number within the cycle.
       my $fn = $Grp->num;
       my $totframe = $number * 2 + $initial_sky;
       my $cycle_fn = $fn % $totframe;

# Specify the second frame as the frame whose mode will be
# the reference for the normalisation of the sky frames.
       if ( $fn % $totframe == 1 ) {
          $Frm->uhdr( "REFERENCE_FRAME", 1 );
       } else {
          $Frm->uhdr( "REFERENCE_FRAME", 0 );
       }

# Classify this frame as either a target or blank-sky object frame.
# Allow for multiple cycles.
       my $filename;
       if ( $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "SKY" ) {
          $Frm->uhdr( "TARGET_OR_SKY", "sky" );
          $filename = $Frm->file;
          orac_print "$filename is of blank sky.\n";

# Also include the frame number.
       } else {
          $Frm->uhdr( "TARGET_OR_SKY", "target" );
          my $target_count = 0;
          if ( defined( $Grp->uhdr( "TARGET_NUMBER" ) ) ) {
             $target_count = $Grp->uhdr( "TARGET_NUMBER" ) + 1;
          }
          $Frm->uhdr( "TARGET_NUMBER", $target_count );
          $Grp->uhdr( "TARGET_NUMBER", $target_count );
          $filename = $Frm->file;
          orac_print "$filename is of the target.\n";
       }

# Specify during processing of which frames should the flat be created,
# sky-level subtracted, and a mosaic created.
       if ( $cycle_fn == ( $totframe - 1 ) ) {
          $Frm->uhdr( "MAKE_FLAT", 1 );
          $Frm->uhdr( "MAKE_MOSAIC", 1 );
          $Frm->uhdr( "SUBTRACT_SKY", 1 );

       } else {
          $Frm->uhdr( "MAKE_FLAT", 0 );
          $Frm->uhdr( "MAKE_MOSAIC", 0 );
          $Frm->uhdr( "SUBTRACT_SKY", 0 );
       }

# Insert the number of frames in the jitter.
       $Frm->uhdr( "JITTER_NUMBER", $number );

# Insert the recipe cycle number of the set of NUMBER frames.
       my $cycle_number =  max( 0, int( $fn / $totframe ) );
       $Frm->uhdr( "CYCLE_NUMBER", $cycle_number );
    }

# Podule
# ======

=head1 NAME

_CHOP_SKY_JITTER_STEER_ -- Steers processing for CHOP_SKY_JITTER recipes.

=head1 DESCRIPTION

This primitive control processing for CHOP_SKY_JITTER recipes through
steering headers listed below.  It also reports whether each frame is
of the target or of some sky background.

=head1 STEERING HEADERS

=over 4

=item CYCLE_NUMBER = INTEGER

Number of the cycle, a cycle being a set of frames to complete a
pass through the recipe.  The first cycle is 0.

=item INITIAL_SKY = LOGICAL (Given)

Whether or not the first frame of the observation is of the sky.
For ESO data the observation comprises a series of alternating sky
and target frames in an object-sky-sky-object pattern, therefore
the value is false.

=item JITTER_NUMBER = INTEGER

The number of frames in the jitter.

=item MAKE_FLAT = LOGICAL

Whether or not to make the flat.  A flat is made once all the
frames are available.

=item MAKE_MOSAIC = LOGICAL

Whether or not register the frames and make the full mosaic.
The grand mosaic is made once all the frames in a cycle are
available.

=item REFERENCE_FRAME = LOGICAL

A true value specifies the reference sky frame to which all
subsequent skies are normalised.  It is true for the first frame
and false for all subsequent frames in the observation.

=item SUBTRACT_SKY = LOGICAL

Whether or not it is time to subtract the sky.  Sky subtraction
occurs once all the frames in a cycle are available.

=item TARGET_OR_SKY = CHARACTER

This is "target" for a target frame, and "sky" for a sky calibration
frame.  The target and sky alternate for these recipes.

=item TARGET_NUMBER = INTEGER

When TARGET_OR_SKY is "target", this counts the target frames,
starting from zero.  It is used for interpolation between sky
measurements.

=item USE_VARIANCE = LOGICAL

Whether or not variance processing is to occur.  This equates to
argument USEVAR.

=back

=head1 ARGUMENTS

=over 4

=item NUMBER = INTEGER (Given)

The number of target frames in the jitter.  It's the I<n> in
CHOP_SKY_JITTERI<n>.  If the argument is absent, a value is derived
from the number of offsets, as given by internal header
ORAC_NUMBER_OF_OFFSETS.  The formula is (ORAC_NUMBER_OF_OFFSETS - 1) / 2.
An error results should ORAC_NUMBER_OF_OFFSETS be even.  If
neither is available, 4 is used.  An error state arises if the number
of jittered frames is fewer than 3.  The minimum is 3 because this
number of blank skies are needed to form a flat field.  []

=back

=head1 NOTES

=over 4

=item *

This primitive is suitable for ISAAC or NACO in imaging mode.

=item *

Processing only occurs for object and sky frames.

=back

=head1 AUTHORS

MJC: Malcolm J. Currie (JAC)

=head1 COPYRIGHT

Copyright (C) 1998-2003 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
