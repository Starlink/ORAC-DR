#+
# Name:
#    _SET_REFERENCE_PIXEL_OFFSET_
#
# Purpose:
#    Sets the offsets of the reference pixel of the current frame.
#
# Language:
#    Perl5
#
# Description:
#    This primitive determines the displacements of the reference pixel
#    with respect to the centre of the current frame and stores the
#    displacements in the referenceoffset calibration system.  The
#    reference pixel is where a star would be placed for photometry
#    or the centre of a chopped and nodded pattern.
#
#    The reference pixel is specified by the ORAC_X_REFERENCE_PIXEL and
#    ORAC_Y_REFERENCE_PIXEL internal headers in the frame.  If either
#    of these does not exist or is undefined, then the reference offset
#    is zero for both axes.
#
# Notes:
#    - This primitive is suitable for UFTI, IRCAM, and Michelle and
#    UIST in imaging mode.
#    - There is an information report giving the reference offset
#    for non-zero shifts.
#    - Should be called only from _IMAGING_HELLO_.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC)
#
# Copyright:
#    Copyright (C) 2002 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# Set the default reference offsets.
    my $xshift = 0;
    my $yshift = 0;

# Check whether or not the reference-pixel headers exist.
# The default origin of (1,1) will be used.
    if ( exists $Frm->uhdr->{ "ORAC_X_REFERENCE_PIXEL" } &&
         exists $Frm->uhdr->{ "ORAC_Y_REFERENCE_PIXEL" } ) {

# Obtain the location of the reference pixel from the headers.
       my $xref = $Frm->uhdr( "ORAC_X_REFERENCE_PIXEL" );
       my $yref = $Frm->uhdr( "ORAC_Y_REFERENCE_PIXEL" );

# Allow for old Michelle data where the headers were not available.
       if ( defined( $xref ) && defined( $yref ) ) {

# Find the central pixel.  To avoid yet another NDFTRACE call,
# merely use the bounds internal headers where available.
          my ( $xmid, $ymid );
          if ( exists $Frm->uhdr->{ "ORAC_X_LOWER_BOUND" } &&
               exists $Frm->uhdr->{ "ORAC_X_UPPER_BOUND" } &&
               exists $Frm->uhdr->{ "ORAC_Y_LOWER_BOUND" } &&
               exists $Frm->uhdr->{ "ORAC_Y_UPPER_BOUND" } ) {

              $xmid = nint( ( $Frm->uhdr( "ORAC_X_UPPER_BOUND" ) +
                      $Frm->uhdr( "ORAC_X_LOWER_BOUND" ) ) / 2 );
              $ymid = nint( ( $Frm->uhdr( "ORAC_Y_UPPER_BOUND" ) +
                      $Frm->uhdr( "ORAC_Y_LOWER_BOUND" ) ) / 2 );

          } else {

# Obtain the central pixel.
             _GET_FRAME_CENTRE_
             $xmid = $_GET_FRAME_CENTRE_{XCENTRE};
             $ymid = $_GET_FRAME_CENTRE_{YCENTRE};
          }

# Derive the shift in pixels.
          $xshift = $xref - $xmid;
          $yshift = $yref - $ymid;
       }
    }

# For storage correct to the nearest pixel.
    my $xoffset = nint( $xshift );
    my $yoffset = nint( $yshift );
    my @offsets = ( $xoffset, $yoffset );

# File the base location in the calibration system.
# =================================================

# The value needs to be in a hash.  Use the current frame as a reference
# for this.
    my %hash = ( %{ $Frm->hdr }, REFERENCEOFFSET => \@offsets );
    $Cal->referenceoffsetindex->add( $Frm->file, \%hash );

    if ( $xshift != 0 || $yshift != 0 ) {
       orac_print "Reference pixel offset of $xoffset, $yoffset pixels with respect to the frame centre filed\n" .
                  "    with the calibration system.\n";
    }

# Podule
# ======

=head1 NAME

_SET_REFERENCE_PIXEL_OFFSET_ -- Sets the offsets of the reference pixel of the current frame.

=head1 DESCRIPTION

This primitive determines the displacements of the reference pixel
with respect to the centre of the current frame and stores the
displacements in the referenceoffset calibration system.   The
reference pixel is where a star would be placed for photometry
or the centre of a chopped and nodded pattern.

The reference pixel is specified by the ORAC_X_REFERENCE_PIXEL and
ORAC_Y_REFERENCE_PIXEL internal headers in the frame.  If either of
these does not exist or is undefined, then the reference offset is
zero for both axes.

=head1 NOTES

=over 4

=item *

This primitive is suitable for UFTI, IRCAM, and Michelle and
UIST in imaging mode.

=item *

There is an information report giving the reference offset for non-zero
shifts.

=item *

Should be called only from L<_IMAGING_HELLO_|_IMAGING_HELLO_>.

=back

=head1 AUTHORS

MJC: Malcolm J. Currie (JAC)

=head1 COPYRIGHT

Copyright (C) 2002 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
