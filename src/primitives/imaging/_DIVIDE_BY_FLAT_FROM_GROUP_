#+
# Name:
#    _DIVIDE_BY_FLAT_FROM_GROUP_
#
# Purpose:
#    Flat-fields the frames within the current group.
#
# Language:
#    Perl5
#
# Description:
#    This primitive divides the frames of the current $Grp group by
#    the most-recent and matching flat-field frame from $Cal->flat
#    method.  Upon completion the group members are the flat-fielded
#    frames.
#
# Notes:
#    -  This primitive is suitable for infra-red imagers.
#    -  Processing only occurs for object, sky, or lamp frames, and
#    when the steering header MAKE_FLAT is true.
#    -  The frames' titles are each propagated.
#
# Output Data:
#    -  Flat-fielded frames each inheriting its corresponding input
#    frame's name but with the _ff suffix.
#
# Tasks:
#    CCDPACK: FLATCOR.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC)
#
# Copyright:
#    Copyright (C) 1998-2003 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# This should only be performed on OBJECT, SKY or calibration LAMP
# frames, when it's time make a flat.
    if ( $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "OBJECT" ||
         $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "SKY" ||
         $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "LAMP" ) {

# Test whether or not it is time to make a flat.
       my $makeflat = $Frm->uhdr( "MAKE_FLAT" );
       if ( $makeflat ) {

# Generate list of input and output filenames.  Inquire the number of files
# to be processed.
          ( my $inref, my $outref ) = $Grp->members_inout( "_ff" );
          my $numfiles = @$inref;

# Convert list to comma-separated list.
          my $inlist = join( ",", @$inref );
          my $outlist= join( ",", @$outref );

# Create text files to hold the list of input and output files, one per line.
# This is needed because expanded lists of files may make the command line
# too long for the ADAM message system.
          unlink( "flatcor.inlist$$", "flatcor.outlist$$" );

          open( my $fh_inlist, ">flatcor.inlist$$" ) ||
            orac_throw "Unable to open flatcor.inlist$$ to create a list of frames to flat-field.  Error: $!.\n";
          print $fh_inlist join( "\n", @$inref ), "\n";
          close( $fh_inlist );

          open( my $fh_outlist, ">flatcor.outlist$$" ) ||
            orac_throw "Unable to open flatcor.outlist$$ to create a list of flat-field frames.  Error: $!.\n";
          print $fh_outlist join( "\n", @$outref ), "\n";
          close( $fh_outlist );

# Set the parameters for the task.
          my $flat = $Cal->flat;
          my $param1 = "in=\'^flatcor.inlist$$\' flat=$flat out=\'^flatcor.outlist$$\'";
          my $param2 = "title=!";

# Flatfield all the object frames.  Generate output names from the
# input list, switching filename suffices.
          $Mon{ "ccdpack_red" }->obeyw( "flatcor", "$param1 $param2" );
          unlink( "flatcor.inlist$$", "flatcor.outlist$$" );

# Report the processing status.
          orac_print "Frames $inlist have been flat-fielded with $flat\n";
          orac_print "The flat-fielded frames are $outlist\n\n";

# Now update the output filenames for each member of the group.  This
# is fine until we come round again and find that we are trying to
# flat-field files that were flatfielded last time.  It is up to the
# _MAKE_FLAT_FROM_GROUP_ primitive to work out which files are meant to
# be used for input here.
          $Grp->membernames( @$outref );
       }
    }

# Podule
# ======

=head1 NAME

_DIVIDE_BY_FLAT_FROM_GROUP_ -- Flat-fields the frames within the current group.

=head1 DESCRIPTION

This primitive divides the frames of the current $Grp group by
the most-recent and matching flat-field frame from $Cal-E<gt>flat
method.  Upon completion the group members are the flat-fielded
frames.

=head1 NOTES

=over 4

=item *

This primitive is suitable for infra-red imagers.

=item *

Processing only occurs for object, sky, or lamp frames, and when
the steering header MAKE_FLAT is true.

=item *

The frames' titles are each propagated.

=back

=head1 OUTPUT DATA

=over 4

=item *

Flat-fielded frames each inheriting its corresponding input
frame's name but with the _ff suffix.

=back

=head1 TASKS

CCDPACK: FLATCOR.

=head1 AUTHORS

MJC: Malcolm J. Currie (JAC)

=head1 COPYRIGHT

Copyright (C) 1998-2003 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
