#+
# Name:
#    _ARRAY_TEST_STATISTICS_
#
# Purpose:
#    Determines the state of the IRCAM detector for the ARRAY_TESTS
#    recipe.
#
# Language:
#    Perl5
#
# Description:
#    This primitive evaluates the STARE and ND_STARE readout noises in
#    electrons and the dark current in electrons per second for IRCAM.
#    The script also reports these statistics, indicating whether or
#    not they are nominal.  The results are logged to a file named
#    ircam3_array_tests.log in the $ORAC_DATA_OUT directory.
#
#    The primitive expects sets of eight frames.  In each set the first
#    refresh frame is ignored, then come two STARE and two ND_STARE-mode
#    frames all of one-second duration, followed by a long (60s)
#    STARE-mode dark, an ignored refresh frame, and another one-second
#    STARE dark.
#
# Notes:
#    -  The exposure times used to normalise the dark current to
#    electrons per second comes from the ORAC_EXPOSURE_TIME header in
#    the sixth and eighth frames, defaulting to 60 and 1 second
#    respectively if the associated header is undefined.  The gain
#    in electrons per ADU comes from primitive _GET_GAIN_.
#    -  If the log file does not exist, a new one is created containing
#    a two-line heading, and the fact is reported.
#    -  The log file stores the results in a fixed columnar format.
#    -  The STARE noise is the standard deviation per frame of the
#    second and third frames subtracted, and converted from counts to
#    electrons.  ND_STARE is calculated in the same fashion for the
#    fourth and fifth frames.
#    -  The dark current is the median of the dark count normalised to
#    unit time and converted to electrons.
#    -  The nominal STARE and ND_STARE readout noises are 56 and 38
#    electrons respectively, and the nominal dark current is 0 to 5
#    electron per second.
#
# Deficiencies:
#    -  There is no validation of the frame types.
#
# Tasks:
#    KAPPA: HISTAT, STATS, SUB.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC)
#
# Copyright:
#    Copyright (C) 1998-2002 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# Create the results file if needed.
# ==================================

# For OS-independent filename creation.
    use File::Spec;

# Create a results file if one does not exist.
     my $results = File::Spec->catfile( $ENV{'ORAC_DATA_OUT'}, "ircam3_array_tests.log" );
     my $fh_results;
     if ( !( -e $results ) ) {
        open( $fh_results, ">$results" ) ||
          orac_throw "Unable to open a new array-test $results log file.  Error: $!.\n";
        orac_print "Creating a new results file called $results\n";

# Write the column headings into the result file.
        print $fh_results "UT Date / Hour    STARE_RN    ND_STARE_RN    Dark Current\n";
        print $fh_results "--------------    --------    -----------    ------------\n\n";

     } else {
        open( $fh_results, ">>$results" ) ||
          orac_throw "Unable to append to the array-test $results log file.  Error: $!.\n";
     }

# Compute the statistics.
# =======================

# Wait until all the array tests are completed.  Should be eight frames.
# Change to be "every eight frames" rather than "eight frames".
     if ( ( $Grp->num + 1 ) % 8 == 0 ) {

# Obtain gain (electrons/ADU), the date and time.
        _GET_GAIN_
        my $adu = $_GET_GAIN_{GAIN};
        $adu = defined( $adu ) ? $adu : 6.0;
        my $utdate = $Frm->uhdr( "ORAC_UTDATE" );
        my $uthour = $Frm->uhdr( "ORAC_UTSTART" )->hour
                   + $Frm->uhdr( "ORAC_UTSTART" )->minute / 60
                   + $Frm->uhdr( "ORAC_UTSTART" )->second / 3600;

# Obtain the names of the group members.
        my @frames = $Grp->membernames;

# Difference the first two useable frames.  The first array_test frame
# is ignored.
        my $param = "in1=$frames[1] in2=$frames[2] out=array_test";
        $Mon{ "kappa_mon" }->obeyw( "sub", "$param" );

# Find the standard deviation of the difference frame.
        $Mon{ "kappa_mon" }->obeyw( "stats", "ndf=array_test" );
        ( my $ORAC_STATUS, my $stare_sigma ) = $Mon{ "kappa_mon" }->get( "stats", "sigma" );
        my $stare_rn = $stare_sigma / sqrt( 2.0 ) * $adu;

# Difference the next two (ND_STARE) frames.
        $param = "in1=$frames[3] in2=$frames[4] out=array_test";
        $Mon{ "kappa_mon" }->obeyw( "sub", "$param" );

# Find the standard deviation of the difference frame.
        $Mon{ "kappa_mon" }->obeyw( "stats", "ndf=array_test" );
        ( $ORAC_STATUS, my $nd_stare_sigma ) = $Mon{ "kappa_mon" }->get( "stats", "sigma" );
        my $nd_stare_rn = $nd_stare_sigma / sqrt( 2.0 ) * $adu;

# Difference the next two useable frames.
        $param = "in1=$frames[5] in2=$frames[7] out=array_test";
        $Mon{ "kappa_mon" }->obeyw( "sub", "$param" );

# Find the median of the difference frame.
        $Mon{ "kappa_mon" }->obeyw( "histat", "ndf=array_test" );
        ( $ORAC_STATUS, my $median ) = $Mon{ "kappa_mon" }->get( "histat", "median" );

# Obtain the exposure times of the final pair of useable frames.
        my $lexptime = $Grp->frame(5)->uhdr( "ORAC_EXPOSURE_TIME" );
        $lexptime = defined( $lexptime ) ? $lexptime : 60.0;
        my $sexptime = $Grp->frame(7)->uhdr( "ORAC_EXPOSURE_TIME" );
        $sexptime = defined( $sexptime ) ? $sexptime : 1.0;

# Protect against divide-by-zero accidents and other erroneous times.
        my $dark = "-999.999";
        if ( $lexptime - $sexptime < 9 ) {
           orac_warn "Exposure times for the final two frames, $lexptime " .
                     "and $sexptime, are too similar.\n";

# Derive the average dark current in electrons per second.
        } else {
           $dark = $median / ( $lexptime - $sexptime ) * $adu;
        }

# Remove the temporary file.
        _DELETE_A_FRAME_ FRAME=array_test

# Output the results.
# ===================

# Write the results to the engineering file, which is then closed.
        printf $fh_results "%8s %5.2f %10.3f %13.3f %14.3f\n",
                       $utdate, $uthour, $stare_rn, $nd_stare_rn, $dark;
        close( $fh_results );

# Format the results to give a sensible number of decimal places.
        my $fs_stare_rn = sprintf( "%.3f", $stare_rn );
        my $fs_nd_stare_rn = sprintf( "%.3f", $nd_stare_rn );
        my $fs_dark = sprintf( "%.3f", $dark );

# Report the results to the user.
        orac_print "\n";
        orac_print "STARE    mode readout noise (e-) = $fs_stare_rn\n";
        orac_print "ND_STARE mode readout noise (e-) = $fs_nd_stare_rn\n";
        orac_print "Dark current (e-/sec)            = $fs_dark\n";
        orac_print "\n";

# Report upon the status of the results.
# ======================================

# STARE readout noise.  May need to make the nominal value a parameter.
        if ( $stare_rn < 30.0 ) {
           orac_warn "STARE readout noise is LOW with respect to nominal 56e-\n";
        } elsif ( $stare_rn > 70.0 ) {
           orac_warn "STARE readout noise is HIGH with respect to nominal 56e-\n";
        } else {
           orac_print "** STARE readout noise is nominal **\n";
        };

# ND_STARE readout noise.  May need to make the nominal value a parameter.
        if ( $nd_stare_rn < 20.0 ) {
           orac_warn "ND_STARE readout noise is LOW with respect to nominal 38e-\n";
        } elsif ( $nd_stare_rn > 50.0 ) {
           orac_warn "ND_STARE readout noise is HIGH with respect to nominal 38e-\n";
        } else {
           orac_print "** ND_STARE readout noise is nominal **\n";
        };

# Dark current.  May need to make the nominal value a parameter.
        if ( $dark < 0.0 ) {
           orac_warn "Dark current is NEGATIVE, wierd; nominal is 0 to 5e-/sec\n";
        } elsif ( $dark > 10.0 ) {
           orac_warn "Dark current is HIGH with respect to nominal 0 to 5e-/sec\n";
        } else {
           orac_print "** Dark current is nominal **\n";
        }
        orac_print "\n";

# File the readnoises in the calibration system.
# ==============================================

# First store the nd_stare readnoise.  The value needs to be in a hash.
# Use the fourth frame as a reference for this, as it has the same readout
# mode and speed/well depth as the others.
        my $tempFrm = $Grp->frame(3);
        my %hash = ( %{ $tempFrm->hdr }, READNOISE => $nd_stare_rn );
        $Cal->readnoiseindex->add( $tempFrm->file, \%hash );
        orac_print "ND_STARE readnoise filed with the calibration system.\n";

# Do likewise for the STARE read noise.  This time use the second frame.
        $tempFrm = $Grp->frame(1);
        %hash = ( %{ $tempFrm->hdr }, READNOISE => $stare_rn );
        $Cal->readnoiseindex->add( $tempFrm->file, \%hash );
        orac_print "STARE readnoise filed with the calibration system.\n\n";
     }

# Podule
# ======

=head1 NAME

_ARRAY_TEST_STATISTICS_ -- Determines the state of the IRCAM detector for the ARRAY_TESTS recipe.

=head1 DESCRIPTION

This primitive evaluates the STARE and ND_STARE readout noises in
electrons and the dark current in electrons per second for IRCAM.
The script also reports these statistics, indicating whether or
not they are nominal.  At UKIRT, the results are also logged to a
text file, F</ukirt_sw/logs/ircam3_array_tests.log>, to monitor
secular trends.

The primitive expects sets of eight frames.  In each set the first
refresh frame is ignored, then come two STARE and two ND_STARE-mode
frames all of one-second duration, followed by a long (60s)
STARE-mode dark, an ignored refresh frame, and another one-second
STARE dark.

=head1 NOTES

=over 4

=item *

The exposure times used to normalise the dark current to electrons per
second comes from the ORAC_EXPOSURE_TIME header in the sixth and
eighth frames, defaulting to 60 and 1 second respectively if the
associated header is undefined.  The gain in electrons per ADU comes
from primitive _GET_GAIN_.

=item *

If the log file does not exist, a new one is created containing
a two-line heading, and the fact is reported.

=item *

The log file stores the results in a fixed columnar format.

=item *

The STARE noise is the standard deviation per frame of the
second and third frames subtracted, and converted from counts to
electrons.  ND_STARE is calculated in the same fashion for the
fourth and fifth frames.

=item *

The dark current is the median of the dark count normalised to
unit time and converted to electrons.

=item *

The nominal STARE and ND_STARE readout noises are 56 and 38
electrons respectively, and the nominal dark current is 0 to 5
electron per second.

=back

=head1 DEFICIENCIES

=over 4

=item *

There is no validation of the frame types.

=back

=head1 TASKS

KAPPA: HISTAT, STATS, SUB.

=head1 AUTHORS

MJC: Malcolm J. Currie (JAC)

=head1 COPYRIGHT

Copyright (C) 1998--2002 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
