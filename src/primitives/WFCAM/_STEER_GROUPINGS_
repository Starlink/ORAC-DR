    # See if observations were done at the tile level

    my $tilenum = $Frm->hdr("TILENUM");
    my $tlevel = ($tilenum != 0);
    $Grp->uhdr("TLEVEL",$tlevel);

    # Define a superframes group

    my $sfg = $Grp->new;
    $Grp->sfgroup($sfg);

    # Now look to see where we are in the current ustep sequence...If it's the
    # first one, then create a subgroup and add this frame into it. If it's not
    # the first one, then just add the frame into the current subgroup

    my $nustep = $Frm->hdr("NUSTEP");
    my $ustep_i = $Frm->hdr("USTEP_I");
    my $ustep_finished = ($nustep == $ustep_i);
    $Grp->uhdr("USTEP_COMPLETE",$ustep_finished);
    if ($ustep_i == 1) {
        my $obsnum = $Frm->hdr("OBSNUM");
	my $subgrp = $Grp->new("$obsnum");
	$Frm->ugrp($subgrp->name);
	$subgrp->push($Frm);
	$Grp->pushugroup($subgrp);
    } else {
        my @allugroups = $Grp->allugroups;
        my $subgrp = pop @allugroups;
	$subgrp->push($Frm);
	$Frm->ugrp($subgrp->name);
    }
    my $line = sprintf("This is frame: %d of %d in microstep sequence %05d\n",
        $ustep_i,$nustep,$Grp->uhdr("USTEPNUM")) if ($nustep > 1);
    $line .= "***This microstep sequence is complete!\n";
    orac_print($line) if ($ustep_finished && $nustep > 1);

    # Do a similar thing for jitter sequences. 

    my $njitter = $Frm->hdr("NJITTER");
    my $jitter_i = $Frm->hdr("JITTER_I");
    my $jitter_finished = ($njitter == $jitter_i && $ustep_finished);
    $Grp->uhdr("JITTER_COMPLETE",$jitter_finished);
    if ($jitter_i == 1) {
        my $obsnum = $Frm->hdr("OBSNUM");
	my $subgrp = $Grp->new("$obsnum");
	$subgrp->push($Frm);
	$Grp->pushjgroup($subgrp);
	$Frm->jgrp($subgrp->name);
    } else {
	my @alljgroups = $Grp->alljgroups;
	my $subgrp = pop @alljgroups;
	$subgrp->push($Frm);
	$Frm->jgrp($subgrp->name);
    }
    $line = sprintf("Frame in position %d of %d in jitter sequence: %05d\n",
        $jitter_i,$njitter,$Frm->hdr("GRPNUM"));
    $line .= "***This jitter sequence is complete!\n" if ($jitter_finished);
    orac_print($line);

    # Have a look at the number of jitter sequences that have been done. If
    # it's four then the current tile is complete.

    my @alljgroups = $Grp->alljgroups;
    my $n = @alljgroups;
    if ($n == 4 && $jitter_finished) {
	$Grp->uhdr("TILE_COMPLETE",1);
	orac_print("***This tile sequence is complete\n");
    } else {
	$Grp->uhdr("TILE_COMPLETE",0);
    }


=head1 NAME

_STEER_GROUPING_ -- A steering routine to set up flags for each of the
groupings in the tiling hierarchy.

=head1 DESCRIPTION

This is a basic steering primtive which looks at header keywords and decides
where we are in the tiling hiearchy. 

=head1 ARGUMENTS

None

=head1 NOTES

This won't work if tiling is done in any way other than with four filled
paw prints.

=head1 AUTHORS

JRL: Jim Lewis (CASU, IoA)

=head1 COPYRIGHT

Copyright (C) 2004-2007 Cambridge Astronomy Survey Unit.
All Rights Reserved

=cut

