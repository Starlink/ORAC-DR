    # Don't do this unless we have all the images from a particular jitter

    if ($Frm->uhdr("JITTER_COMPLETE")) {
        orac_print "Doing JITTER_SKY sky estimation for group " . $Grp->name .
	    "\n";

        # Get all the files in the group and do background stats on them.

        foreach my $frm ($Grp->allmembers) {
            my $Frm = $frm;
            _IMSTAT_ CPMLOC=CIR_CPM HDRUP=1
        }

	# Get some header keywords...

	my $readkey = $Frm->hdrkeys("READNOISE");
	my $gainkey = $Frm->hdrkeys("GAIN");
	my $expkey = $Frm->hdrkeys("EXPOSURE_TIME");

        # Get a file name...

        my $outfile = "SKY_" . $Grp->name . $Frm->fitssuffix;

        # If this file already exists, then you don't need to make a new one

        if (! -e $outfile) {

            # Scale the images first

            _ICSCALE_

            # Loop for each image extension...

            orac_print "Forming mean sky file $outfile\n";
	    foreach my $i (1 .. $Frm->findnsubs) {

		# Get a temporary file

		my $tmpfil = ORAC::TempFile->new;
		my $fh = $tmpfil->handle;

		# Write the names of the input images to the file

		foreach my $frm ($Grp->allmembers) {
		    print $fh $frm->getasubframe($i)->file . "\n";
		}
		$fh->close;

		# Output file name specification

                my $snum = $Frm->getasubframe($i)->subfrmnumber;
		my $outf = sprintf("%s[%d]",$outfile,$snum);

		# Do the combination now...

		my $errmsg;
		my $retval = cir_imcombine($tmpfil->file,$outf,"","CIR_CPM",
		    MEDIANCALC,0,0,1,NOBPM,REJPOISSON,1,1,1,3.0,3.0,$readkey,
		    $gainkey,$expkey,$errmsg);
		if ($retval != CIR_OK) {
		    orac_throw "CIR_IMCOMBINE: failed in _JITTER_SKY_\n$errmsg\n";
		}
   
                # Ditch temporary file

                $tmpfil->DESTROY;
            }

            # Make a frame object from the file and add some info into the
            # header. Also, do some stats that we can use later...

            my $skyfrm = $Frm->new($outfile);
            $skyfrm->update_header("WFRTYPE",TSTRING,CALSKY,
                "Calibration frame type");
            {
                my $Frm = $skyfrm;
                _IMSTAT_ CPMLOC=CIR_CPM HDRUP=1
            }

            # File this away now...

            _FILE_SKY_ NAME=$outfile
        }

        # Now, if you haven't formed the sky here, then get it from the index.

        my $skyfil = $Cal->sky;
        my $skyfrm = $Frm->new($skyfil);            

        # Right, now do the sky subtraction. Loop for each file and each
        # image extension...

        foreach my $frm ($Grp->allmembers) {
            my $nextn = $frm->findnsubs;
            orac_print "Doing sky subtraction for " . $frm->file . "\n";
            foreach my $i (1 .. $nextn) {
                my $subfrm = $frm->getasubframe($i);
                my $subskyfrm = $skyfrm->getasubframe($i);

                # Has this one already been done?

                my $isdone = $subfrm->uhdr("SKYSUB");
                next if (defined($isdone) && $isdone =~ /^Done/);

                # Open a file and write this info to it
                
                my $tmpfil = ORAC::TempFile->new;
                my $fh = $tmpfil->handle;
                printf $fh "%s %g\n",$subskyfrm->file,1.0;
                $fh->close;

                # Do the subtraction

                my $errmsg;
                my $retval = cir_mscale_subt($subfrm->file,$tmpfil->file,
                    $subskyfrm->hdr("CIRMED"),"",$errmsg);
                if ($retval != CIR_OK) {
                    orac_throw "CIR_MSCALE_SUBT: failed in _JITTER_SKY_\n$errmsg\n";
                }

                # Delete file...

                $tmpfil->DESTROY;

                # Add a line to the header...

                my $hline = sprintf "Done _JITTER_SKY_: %s %0.3f",
                    $subskyfrm->file,1.0;
                $frm->getasubframe($i)->update_header("SKYSUB",TSTRING,$hline,
                    "");
                $frm->hdr("SKYSUB",$hline);
            }
        }
    }

=head1 NAME

_JITTER_SKY_ -- Create a sky frame for a jitter group and subtract it.

=head1 DESCRIPTION

This primitive creates a mean sky frame from all of the images in a given
jitter group.  It then subtracts that frame from each image in the group.

=head1 ARGUMENTS

None

=head1 NOTES

=over 4

=item *

If you create a mean sky image, then it is filed away in an index file.

=back

=head1 AUTHORS

JRL: Jim Lewis (CASU, IoA)

=head1 COPYRIGHT

Copyright (C) 2003-2006 Cambridge Astronomy Survey Unit. 
All Rights Reserved

=cut
