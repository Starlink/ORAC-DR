# _ARRAY_STATISTICS_			-*-cperl-*-

# basic statistics for an IR array

# frossie@jach.hawaii.edu


=head1 NAME

ARRAY_STATISTICS - report frame statistics

=head1 DESCRIPTION

Reports minimum, maximum, mean and standard deviation of all subframes
in the current Frame.

=head1 PARAMETERS

=over 4

=item XMIN

Only data points whose X co-ordinate is larger than this value are used to generate statistics information

=item XMAX

Only data points whose X co-ordinate is smaller than this value are used to generate statistics information

=item YMIN

Only data points whose Y co-ordinate is larger than this value are used to generate statistics information

=item YMAX

Only data points whose Y co-ordinate is smaller than this value are used to generate statistics information

=back

=head1 ORAC

=head2 Engines referenced

KAPPA

=head2 Tasks called

=over 4

=item kappa_mon

stats

=item ndfpack_mon

ndftrace

=back

=head2 Objects addressed

$Frm

=head2 Other files

Logs to log.stats in $ORAC_DATA_OUT

=head1 NOTES

Uses NDFTRACE to get bounds since at this time a complete FITS header
is not available.

=head2 REVISION

$Id$

=head1 AUTHOR

frossie@jach.hawaii.edu

=head1 COPYRIGHT

Copyright 1998-2001 Particle Physics and Astronomy Research Council.
All Rights Reserved.

=cut

my $xmin = ($_ARRAY_STATISTICS_{XMIN} || undef);
my $xmax = ($_ARRAY_STATISTICS_{XMAX} || undef);
my $ymin = ($_ARRAY_STATISTICS_{YMIN} || undef);
my $ymax = ($_ARRAY_STATISTICS_{YMAX} || undef);


# log object for this primitive
my $log = new ORAC::LogFile("log.stats");


# with this header
my @headers = ("Frame                         Mean       Min        Max      Std    ",
	    "--------------------------------------------------------------------");

# write header to log file
$log->header(@headers);

# write header to screen
orac_print(join("\n",@headers)."\n");

# use this format
my $format = "%-25s %10.4e %10.4e %10.4e %10.4e";
my $ndfsection;
my $ORAC_STATUS;

# foreach subframe
foreach my $frame ($Frm->files) {


  # if any of these have been set
  if ($xmin || $xmax || $ymin || $ymax) {

     $Mon{'ndfpack_mon'}->obeyw("ndftrace","ndf=$frame quiet");

     ($ORAC_STATUS, my @lbound) = $Mon{'ndfpack_mon'}->get('ndftrace','lbound');
     ($ORAC_STATUS, my @ubound) = $Mon{'ndfpack_mon'}->get('ndftrace','ubound');

     ($xmin) || ($xmin = $lbound[0]);
     ($xmax) || ($xmax = $ubound[0]);
     ($ymin) || ($ymin = $lbound[1]);
     ($ymax) || ($ymax = $ubound[1]);

     $ndfsection = "($xmin:$xmax,$ymin:$ymax)";


   } else {

     $ndfsection = "";

   };


  # run stats

  $Mon{'kappa_mon'}->obeyw("stats","ndf=$frame$ndfsection");

  # get the parameters from stats
  ($ORAC_STATUS, my $mean) = $Mon{"kappa_mon"}->get("stats","mean");
  ($ORAC_STATUS, my $min) = $Mon{"kappa_mon"}->get("stats","minimum");
  ($ORAC_STATUS, my $max) = $Mon{"kappa_mon"}->get("stats","maximum");
  ($ORAC_STATUS, my $std) = $Mon{"kappa_mon"}->get("stats","sigma");

  # generate reporting string
  my $string = sprintf($format,$frame,$mean,$min,$max,$std);

  # write string to log and screen
  orac_print("$string\n\n");
  $log->addentry($string);

};
