# -*-cperl-*-

=head1 NAME

_CREATE_NEP_CUBE_ - create a cube or hypercube of NEP values

=head1 SYNOPSIS

 _CREATE_NEP_CUBE_

 _CREATE_NEP_CUBE_ HEAT=0

=head1 DESCRIPTION

This primitive construct a 3-d cube or 4-d hypercube from the noise
files in the current global Frame object.

The two primary arguments are flags to indicate which axes should be
created in the output file. The default behaviour is to create a 4-d
hypercube of the form BOLX-BOLY-HEATER-BIAS. If either flag is set to
false (0) then a 3-d cube will be created using the other data as the
third axis.

=head1 ARGUMENTS

The following arguments are supported:

=over 4

=item B<BIAS>

Flag to denote whether to add a detector bias heater axis. Default is
1.

=item B<HEAT>

Flag to denote whether to add a pixel heater axis. Default is 1.

=item B<VERBOSE>

Flag to denote whether to display extra information. Default is 1
(yes).

=back

=head1 EXTERNAL TASKS

The following external tasks are used:

=head1 KAPPA

NDFCOPY, NDFTRACE, PASTE, SETBOUND, SETORIGIN

=head1 ATOOLS

ASTADDFRAME, ASTCMPFRAME, ASTCMPMAP, ASTFRAME, ASTFRAMESET,
ASTGETFRAME, ASTGETMAPPING, ASTWINMAP

=back

=head1 OUTPUT DATA

None.

=head1 OUTPUT FILES

One file per subarray with the suffix C<_nep>.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2010 University of British Columbia. All Rights
Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# Get flags for adding the heater and bias axes to the output
my $heataxis = get_prim_arg($_PRIM_ARGS_, "HEAT", 1);
my $biasaxis = get_prim_arg($_PRIM_ARGS_, "BIAS", 1);

# Should we be verbose (default to yes)?
my $verbose = get_prim_arg( $_PRIM_ARGS_, "VERBOSE", 1);

# Get arrays of heater and bias settings
my @heat = sort($Frm->hdrvals("PIXHEAT"));
my @bias = sort($Frm->hdrvals("DETBIAS"));
# Now produce a lookup table from value to index into the above arrays
my %heat = map { $heat[$_] => $_ } 0..$#heat;
my %bias = map { $bias[$_] => $_ } 0..$#bias;

# Do not create non-significant axes
$heataxis = 0 if (@heat == 1);
$biasaxis = 0 if (@bias == 1);

# Number of dimensions in output (hyper)cube
my $cubedim = 2;
$cubedim++ if ($heataxis);
$cubedim++ if ($biasaxis);

# Set domain for extra dimensions
my $domain = "Heater-Bias";
$domain = "Heater" if (!$biasaxis);
$domain = "Bias" if (!$heataxis);

# Bounds for expanded images before adding to cube
my @bounds = ("1:1");
push(@bounds, "1:1") if ($heataxis && $biasaxis);

# Sort data by subarray
my @subfrms = $Frm->subfrms("SUBARRAY");

#$Grp->allmembers(@subfrms);

# Common NEP cube name for all subarrays
my $basename = $Frm->hdr("UTDATE") ."_". (sprintf "%05d", $Frm->hdr("OBSNUM"))
  ."_nep";
# Common thresholding arguments
my $thrargs = "thrlo=".($Frm->uhdr("NEP_MIN"))." thrhi=".($Frm->uhdr("NEP_MAX"))
  ." newlo=bad newhi=bad";

orac_print "Thresholding NEP images between ".($Frm->uhdr("NEP_MIN"))." and "
  .($Frm->uhdr("NEP_MAX"))." W Hz**-0.5\n";

# Loop over each subarray
foreach my $frmobj (@subfrms) {
  my @planes;
  # Threshold the input NEP file, add extra dimensions and set the
  # origin accordingly
  for my $i (1..$frmobj->nfiles) {
    my $infile = $frmobj->file($i) .".more.smurf.nep";
    my $nepfile = $frmobj->inout("_thrnoi", $i);
    my $args = "in=$infile out=$nepfile $thrargs";
    $Mon{kappa_mon}->obeyw("thresh","$args");

    # Push onto intermediates so it will be deleted at end of
    # processing
    $Frm->push_intermediates($nepfile);

    # Skip the rest if the output `cube' has only 2 dimensions
    next if ($cubedim == 2);

    # Define origin of heater/bias axes
    my @origin;
    push (@origin, $heat{$frmobj->hdrval("PIXHEAT",$i-1)}) if ($heataxis);
    push (@origin, $bias{$frmobj->hdrval("BIASHEAT",$i-1)}) if ($biasaxis);

    # Retrieve coords of first pixel
    $Mon{ndfpack_mon}->obeyw("ndftrace","ndf=$nepfile");
    my ($ORAC_STATUS, @xy) = $Mon{ndfpack_mon}->get("ndftrace","lbound");
    # Now add 1-pixel-wide third and optionally fourth axes
    $args = "ndf=$nepfile(,,".(join(",",@bounds)).")";
    $Mon{ndfpack_mon}->obeyw("setbound","$args");
    # Set the origin accordingly
    $args = "ndf=$nepfile origin=[$xy[0],$xy[1],"
      .(join(",",@origin))."]";
    $Mon{ndfpack_mon}->obeyw("setorigin","$args");

    # Store this file
    push(@planes, $nepfile);
  }

  # Combine individual NEP images into a 3-D cube or 4-D hypercube
  my $subarray = lc($frmobj->hdr("SUBARRAY"));
  my $cubename = $subarray . $basename;
  if ($cubedim == 2) {
    # Just copy the thresholded NEP image to the output `cube'
    orac_print "Creating NEP image for $subarray...\n";
    $Mon{ndfpack_mon}->obeyw("ndfcopy","in=$planes[0] out=$cubename");
  } else {
    my $hypercube = ($cubedim == 4) ? "hypercube" : "cube";
    orac_print "Creating NEP $hypercube for $subarray...\n";
    my $inlist = write_file_list( @planes );
    my $title = "'".(uc($subarray))." NEP $hypercube'";
    my $args = "in=^$inlist p1=! out=$cubename title=$title";
    $Mon{kappa_mon}->obeyw("paste","$args");
    undef $inlist;

    my ($wcs, $mapping, $frame);
    unless ($Grp->uhdr("WCSFRAMESET")) {
      # Add WCS info - begin with a template file
      my $template = $frmobj->file(1);

      # Get existing bolo frame from one plane of the hypercube
      $args = "this=$template iframe1=ast__base iframe2=ast__current result=bolomap$$.ast";
      $Mon{atools_mon}->obeyw("astgetmapping","$args");
      $args = "this=$template iframe=ast__current result=boloframe$$.ast";
      $Mon{atools_mon}->obeyw("astgetframe","$args");

      # Define the mapping from grid to heater/bias
      my $ncoord = $cubedim - 2;
      my $ina = join(",", map { 1 } 1..$ncoord);
      my ($inb, $min, $max);
      if ($heataxis) {
	$inb = scalar(@heat);
	$min = $heat[0];
	$max = $heat[-1];
	if ($biasaxis) {
	  $inb = join(",", $inb, scalar(@bias));
	  $min = join(",", $min, $bias[0]);
	  $max = join(",", $max, $bias[-1]);
	}
      } elsif ($biasaxis) {
	$inb = scalar(@bias);
	$min = $bias[0];
	$max = $bias[-1];
      }
      my $setting = "Heater and Bias";
      $setting = "Heater" if (!$biasaxis);
      $setting = "Bias" if (!$heataxis);
      my @labels;
      push (@labels, "Pixel heater") if $heataxis;
      push (@labels, "Detector bias") if $biasaxis;
      my $options = "Title=$setting settings,Label(1)=$labels[0],Unit(1)=DAC units,Domain=$domain";

      $options .= ",Label(2)=$labels[1],Unit(2)=DAC units" if ($ncoord == 2);

      # Define a Frame for the heater/bias axes
      $args = "naxes=$ncoord result=nepframe$$.ast options='$options'";
      $Mon{atools_mon}->obeyw("astframe","$args");

      $args = "ncoord=$ncoord ina=[$ina] inb=[$inb] outa=[$min] outb=[$max] options=! result=nepmapping$$.ast";
      $Mon{atools_mon}->obeyw("astwinmap","$args");

      # Create a compound mapping and frameset from the bolo and
      # heater/bias settings
      $args = "frame1=boloframe$$.ast frame2=nepframe$$.ast options=! result=cmpframe$$.ast";
      $Mon{atools_mon}->obeyw("astcmpframe","$args");
      $args = "map1=bolomap$$.ast map2=nepmapping$$.ast series=f options=! result=cmpmap$$.ast";
      $Mon{atools_mon}->obeyw("astcmpmap","$args");

      # Create an empty 4-d GRID frame
      $args = "naxes=$cubedim options='Domain=GRID' result=grid$$.ast";
      $Mon{atools_mon}->obeyw("astframe","$args");
      # Add it to a skeleton FrameSet
      $args = "frame=grid$$.ast options=! result=wcs$$.ast";
      $Mon{atools_mon}->obeyw("astframeset","$args");

      # Cache the FrameSet, mapping and frame info
      $wcs = "wcs$$.ast";
      $mapping = "cmpmap$$.ast";
      $frame = "cmpframe$$.ast";
      $Grp->uhdr("WCSFRAMESET", $wcs);
      $Grp->uhdr("MAPPING", $mapping);
      $Grp->uhdr("FRAME", $frame);
    } else {
      $wcs = $Grp->uhdr("WCSFRAMESET");
      $mapping = $Grp->uhdr("MAPPING");
      $frame = $Grp->uhdr("FRAME");
    }
    # Now add the frameset to the output image
    $args = "this=$wcs iframe=ast__base map=$mapping frame=$frame result=$cubename";
    $Mon{atools_mon}->obeyw("astaddframe","$args");

    # Remove all of the .ast text files with the mapping/frame info if
    # necessary
    if ($frmobj == $subfrms[-1]) {
      $Grp->uhdr("WCSFRAMESET", 0);
      $Grp->uhdr("MAPPING", 0);
      $Grp->uhdr("FRAME", 0);
      unlink glob( "*$$.ast" );
    }
  }
}
