# -*-cperl-*-

=head1 NAME

_MAKEMAP_FRAME_ - convert time-series data into an image

=head1 SYNOPSIS

  _MAKEMAP_FRAME_

=head1 DESCRIPTION

This primitive takes time-series data for the current Frame and uses
SMURF MAKEMAP to construct an image. The user may specify either the
simple rebinning method or the full iterative solution.

For the iterative map-maker a configuration file is obtained based on
the Frame uhdr entry C<MAKEMAP_CONFIG_TYPE>, though this may be
overridden with a recipe parameter. See
L<_GET_MAKEMAP_CONFIG_|_GET_MAKEMAP_CONFIG_> for further details.

The output file size is kept below 2 GB which in practice corresponds
to an image with about 60 million pixels (7745 pixels on a side, or
6.45 degrees on the sky for 3-arcsec pixels). Currently the primitive
creates a smaller image if the limit is exceeded. Each dimension is
reduced in proportion. The primitive aborts if the memory requirements
for processing the data exceeds that available.

There is some flexibility in determining which files should go in to
making the map.  The default behaviour is to use all files in the
current Frame object (specified by C<METHOD=FRAME>). Alternative
options are C<NEW> to use all data taken since last map was made,
C<OBS> for all files which are part of the current observation, C<ALL>
for all valid files for all Frames in the current Group object or
C<LUT> to use a lookup table stored in the Frame uhdr (as
C<SUBSCAN_LUT>). The LUT must be a hash with filenames as keys and 1/0
as values and files with a value of 1 are used to make the map.

This is a many to one operation, with only a single output file
created.

=head1 ARGUMENTS

The following arguments are supported:

=over 4

=item B<CONFIG_TYPE>

Type of MAKEMAP configuration file to use. The choice is determined by
the type of object observed (and hence by the calling recipe). May be
C<BRIGHT>, C<EXTENDED>, C<FAINT> or C<JSA_GENERIC> which use the bright_compact,
extended, faint or JSA generic versions of the MAKEMAP configuration
file. Default is C<FAINT>.

=item B<INFILES>

A switch to determine how many files are given to MAKEMAP.  Valid
values are C<ALL>, C<FRAME>, C<LUT>, C<NEW> or C<OBS>.

=item B<FTSPORT>

FTS-2 port selection to be passed to MAKEMAP if specified.

=item B<METHOD>

Image reconstruction method. May be C<REBIN> to use a simple rebinning
scheme or C<ITER> to use the iterative map-maker. The default is
C<ITER>.

=item B<NOCHUNKING>

If set to a non-zero value, abort when MAKEMAP would chunk
the data.  Overrides the NOCHUNKING uhdr (from the corresponding
recipe parameter).

=item B<SYSTEM>

Coordinate system in which to create map. Default is C<TRACKING>.

=item B<JSA_TILES>

Can be used to override the C<JSA_TILES> uhdr which determines
whether or not MAKEMAP should be instructed to make JSA tiles.

=item B<SHIFT_NON_CAL>

Can be used to cause the _SHIFT_IMAGES_ step to be applied
even if the OBJECT is not a recognised flux calibrator.

=item B<SHIFT_USE_SKYREF>

Can be used to set the USE_SKYREF argument of the call to
_SHIFT_IMAGES_, i.e. to request that the SkyRef attribute
be used as the source position even if the source is not
a recognised calibrator.

=back

=head1 EXTERNAL TASKS

The following external tasks are used:

=over 4

=item KAPPA

SETTITLE

=item SMURF

MAKEMAP

=item ORAC-DR PRIMITIVES

_CHECK_MAP_BOUNDS_, _GET_MAKEMAP_ARGS_, _GET_MAKEMAP_CONFIG_

=back

=head1 NOTES

The user should take care when calling with the B<INFILES> parameter
set to C<ALL> or C<OBS>. It is possible that MAKEMAP will be run
multiple times on almost the same set of data files. This is probably
not desirable. See L<_SUMMIT_STEER_|_SUMMIT_STEER_> and
L<_PROCESS_SCAN_DATA_|_PROCESS_SCAN_DATA_> for information on one way
to avoid this.

For B<INFILES>=C<FRAME> it is assumed that the data are not
flatfielded and thus dark frames need to be added to the list of input
files. Conversely, for C<ALL> it is assumed that the input data are
already flatfielded and thus there is no need for dark frames.

If uhdr entries B<POINTING_OFFSET_X> and/or B<POINTING_OFFSET_Y>
are present then they will be passed to MAKEMAP via a pointing
file.

=head1 OUTPUT DATA

The Frame C<isgood> flag is set to false if MAKEMAP fails for any
reason.

The Frame uhdr entries C<SKY_SUBTRACTION> and C<EXTINCTION> are set to
1 on success.

If L<_SHIFT_IMAGES_|_SHIFT_IMAGES_> is used to determine the
image shifts of a known calibrator, then the results are placed
in the primitive hash as an entry B<IMAGE_SHIFTS>.

=head1 OUTPUT FILES

Creates a single output file with a C<_fmos> suffix or multiple files
if C<JSA_TILES> is enabled (by uhdr or argument).

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2008-2014 University of British Columbia.
Copyright (C) 2008 Particle Physics and Astronomy Research Council.
Copyright (C) 2017 Sciemce and Technology Facilites Council.
All Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either Version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# Check for dark-only Frames and set a few uhdr flags so that
# subsequent primitives are skipped.
if ($Frm->tagexists("dark")) {

  # Do not calibrate these data.
  $Frm->uhdr("NOCALIB",1);
  return ORAC__OK;
}

# Declare varisble for the shifts determined for calibrators.
my $image_shifts = undef;

# Obtain a method for reconstructing map.
my $method = lc(get_prim_arg( $_PRIM_ARGS_, "METHOD", "iter"));
my $iterate = 0;
my $system = uc(get_prim_arg( $_PRIM_ARGS_, "SYSTEM", "TRACKING"));

# Apply a minimum match on the method.
if ( $method =~ /^iter/ ) {
  $method = "iterate";
  $iterate = 1;
} elsif ( $method =~ /^reb/ ) {
  $method = "rebin";
} else {
  orac_warn "Unrecognized MAKEMAP METHOD, $method: assuming REBIN\n";
  $method = "rebin";
}

# Obtain the FTS-2 port.
my $fts_port = get_prim_arg($_PRIM_ARGS_, "FTSPORT", undef);

# Obtain how many files are we combining to make a map.
my $infiles = get_prim_arg($_PRIM_ARGS_, "INFILES", "FRAME");

# Decie whether to include fastflats (which have been processed separately)
# in the list of files. Yes for the SUMMIT pipeline provided the Frame does not
# contain flatfielded data, and also yes for focus observations. Otherwise
# no (they are not necessary for offline pipeline as the Frame will
# already contain the fastflats).
my $incfastflats = (($Frm->uhdr("SUMMIT") && !$Frm->tagexists("flatfield")) ||
		     $Frm->uhdr("ORAC_OBSERVATION_TYPE") eq "focus") ? 1 : 0;

# Are we making JSA tiles?
my $jsa_tiles = get_prim_arg($_PRIM_ARGS_, 'JSA_TILES', undef)
             // $Frm->uhdr('JSA_TILES')
             // 0;

# Ensure the JSA_TILES uhdr is set if this primitive is run in tiled mode.
$Frm->uhdr('JSA_TILES', 1) if $jsa_tiles;

# Obtain arguments whether to apply shifts to all sources.
my $shift_non_cal = get_prim_arg($_PRIM_ARGS_, 'SHIFT_NON_CAL', 0);
my $shift_use_skyref = get_prim_arg($_PRIM_ARGS_, 'SHIFT_USE_SKYREF', 0);

my @infiles;
my $suffix = "_fmos";
my $obsnum = $Frm->hdr("OBSNUM");
if ( $infiles eq "FRAME" ) {

  # Use all files in current Frame.
  _RETRIEVE_GOOD_DATA_FRAME_ FASTFLATS=$incfastflats
  @infiles = @{$_RETRIEVE_GOOD_DATA_FRAME_{FILES}}
    if (defined $_RETRIEVE_GOOD_DATA_FRAME_{FILES});
} elsif ( $infiles eq "OBS" ) {

  # Create a sub-group for the current observation only.
  my $subGrp = $Grp->subgrp(OBSNUM => $obsnum);
  foreach my $frmobj ( $subGrp->members ) {

    # Only add data from frames that do not contain images.
    my $no_images = ($frmobj->isgood && !$frmobj->tagexists("newimage") &&
      !$frmobj->tagexists("olddata")) ? 1 : 0;

    # However, in the SUMMIT pipeline, make sure that only flatfielded
    # data are included if appropriate.  Test this by looking for
    # flatfielded data in the current Frame.
    if ($Frm->uhdr("SUMMIT") && $Frm->tagexists("flatfield")) {
      $no_images = 0 unless ($frmobj->tagexists("flatfield"));
    }
    if ($no_images) {
      _RETRIEVE_GOOD_DATA_FRAME_ FASTFLATS=$incfastflats FRAME=$frmobj
      push (@infiles, @{$_RETRIEVE_GOOD_DATA_FRAME_{FILES}})
	if (defined $_RETRIEVE_GOOD_DATA_FRAME_{FILES});
    }

    # Don't need this frame any more so mark it as old.
    $frmobj->tagset("olddata");
  }
} elsif ( $infiles eq "NEW" ) {

  # Use all new data in the current observation that were taken since
  # last map was made.
  my $subGrp = $Grp->subgrp(OBSNUM => $obsnum);
  foreach my $frmobj ( $subGrp->members ) {

    # Use frame objects that are not tagged as "olddata".
    unless ($frmobj->tagexists("olddata")) {
      _RETRIEVE_GOOD_DATA_FRAME_ FASTFLATS=$incfastflats FRAME=$frmobj
      push (@infiles, @{$_RETRIEVE_GOOD_DATA_FRAME_{FILES}})
	if (defined $_RETRIEVE_GOOD_DATA_FRAME_{FILES});

      # Don't need this frame any more so mark as old.
      $frmobj->tagset("olddata");
    }
  }
} elsif ( $infiles eq "ALL" ) {

  # Use ALL data from all members of Group.
  foreach my $frmobj ( $Grp->members ) {
    if ($frmobj->isgood) {
      _RETRIEVE_GOOD_DATA_FRAME_ FASTFLATS=$incfastflats FRAME=$frmobj
      push (@infiles, @{$_RETRIEVE_GOOD_DATA_FRAME_{FILES}})
	if (defined $_RETRIEVE_GOOD_DATA_FRAME_{FILES});
    }
  }
  $suffix = "_mos";
} elsif ( $infiles eq "LUT" ) {
  my $subscan_lut = $Frm->uhdr("SUBSCAN_LUT");
  if ($subscan_lut) {
    my %subscan_lut = %{$subscan_lut};
    @infiles = map { $_ } grep { $subscan_lut{$_} == 1 } keys %subscan_lut;
  } else {

    # No LUT equals a fatal error.
    orac_termerr "No LUT for retrieving good files in current Frame\n";
  }
}

if ($Frm->uhdr("NOFASTFLATS")) {
  orac_warn "No fastflats could be retrieved: map will be made using internal flatfield and is likely to be sub-optimal\n";
}

# Quit processing if there are no data to make a map.
my $object = $Frm->hdr('OBJECT');
orac_term "No good data for $object from obs $obsnum with which to make a map\n"
  unless (@infiles);

my $point_focus = ($Frm->hdr("OBS_TYPE") =~ /focus|pointing/i) ? 1 : 0;

# Write a list of filenames to a temporary file.
my $inlist = write_file_list( @infiles );

# Set the output filename.
my ($in, $out) = $Frm->inout($suffix);
$out = $Frm->rewrite_outfile_subarray( $out );

# Set BBM parameter to null until we find BBMs that are worth using.
my $bbm ="!";

# Since the map bound are being checked and the bounds returned
# explicitly to keep the output file below 2 GB, just set tiledims to
# null.  Note that MAKEMAP ignores this parameter when JSATILES
# is set, so it should be mostly harmless in JSA tile mode.
my $tiledims = "!";

# Define MAKEMAP argument list, first the  mandatory arguments.
my $args = "in=^$inlist out=$out bbm=$bbm tiledims=$tiledims";

# Now dDefine the variable arguments to MAKEMAP.
my %makemapargs = ( method => $method );

# Retrieve any MAKEMAP arguments given as recipe parameters.
_GET_MAKEMAP_ARGS_ SYSTEM=$system
my %newmakemapargs = %{$_GET_MAKEMAP_ARGS_{ARGS}};

# Store these new arguments in the hash.  Existing entries will be
# overridden.
foreach my $arg (keys %newmakemapargs) {
  $makemapargs{$arg} = $newmakemapargs{$arg};
}

# Construct an argument list for MAKEMAP.
foreach my $arg (keys %makemapargs) {
  $args .= " $arg=$makemapargs{$arg}";
}

# Add the FTS-2 configuration, if present.
$args .= ' ftsport=' . $fts_port if defined $fts_port;

my $memcheck = get_prim_arg($_PRIM_ARGS_, 'NOCHUNKING', undef)
            // $Frm->uhdr("RECPAR_NOCHUNKING")
            // 0;

# Append name of config file to the argument list.
# Do not override the _SCUBA2_HELLO_ value unless an explicit
# argument has been given to this primitive.
my $config;
if ( $iterate ) {

  # Obtain the type of config file to use: NORMAL, BLANK_FIELD,
  # BRIGHT_COMPACT etc.
  my $conftype = get_prim_arg($_PRIM_ARGS_, "CONFIG_TYPE","");

  if ($conftype) {
    if ($conftype eq 'BRIGHT') {
      $conftype = "bright_compact";
    } elsif ($conftype eq 'EXTENDED') {
      $conftype = "bright_extended";
    }
    $Frm->uhdr("MAKEMAP_CONFIG_TYPE", lc($conftype) );
  }

  _GET_MAKEMAP_CONFIG_
  $config = $_GET_MAKEMAP_CONFIG_{CONFIG};

  $config .= ",memcheck=1" if ($memcheck);
}
$args .= " config='^$config'" if ($config);

# Include alternative resistor values if they exist
$args .= " resist=".($Frm->uhdr("RECPAR_RESIST_CFG"))
  if (defined $Frm->uhdr("RECPAR_RESIST_CFG"));

# Call MAKEMAP--really must have some sort of feedback to the user.
orac_print "Calling MAKEMAP using $method method\n";

# Do we have pointing offsets?
my $pointing_file;
if (defined $Frm->uhdr('POINTING_OFFSET_X')
    or defined $Frm->uhdr('POINTING_OFFSET_Y')) {
  my $dx = $Frm->uhdr('POINTING_OFFSET_X') // 0;
  my $dy = $Frm->uhdr('POINTING_OFFSET_Y') // 0;
  orac_say("Using pointing offsets $dx, $dy");

  # Write the pointing offsets to a pointing file for MAKEMAP.
  $pointing_file = new ORAC::TempFile();
  my $fh = $pointing_file->handle();
  print $fh "# SYSTEM=TRACKING\n";
  print $fh "# TAI DLON DLAT\n";
  print $fh $Frm->uhdr('ORAC_UTSTART')->mjd(), " $dx $dy\n";
  print $fh $Frm->uhdr('ORAC_UTEND')->mjd(), " $dx $dy\n";
  $fh->close();

  # Add this pointing file to the MAKEMAP arguments.
  $args .= ' pointing='.$pointing_file->file();
}

# Declare the error-message text.
my $msg;

# Check the output map bounds.
my $proceed = 1;
unless ($jsa_tiles) {
  unless (defined $fts_port) {
    _CHECK_MAP_BOUNDS_ IN=$inlist TASK=MAKEMAP
  }
  else {
    _CHECK_MAP_BOUNDS_ IN=$inlist TASK=MAKEMAP FTSPORT=$fts_port
  }
  if ( $_CHECK_MAP_BOUNDS_{NEWBOUNDS} == 1 ) {
    my @lbound = @{ $_CHECK_MAP_BOUNDS_{LBOUND} };
    my @ubound = @{ $_CHECK_MAP_BOUNDS_{UBOUND} };

    # Append the new bounds to the list of arguments.
    $args .= " lbnd=[".join(",",@lbound)."] ubnd=[".join(",",@ubound)."]";
  } elsif ( $_CHECK_MAP_BOUNDS_{NEWBOUNDS} == -1 ) {

    # The bounds check failed, so mark the current frame as bad and
    # set an error message.
    $Frm->isgood(0);
    $proceed = 0;
    $msg = "Checking map size failed - skipping this observation\n";
  }
}

my $fault_msg = "If this failure is not associated with a bad observation please file a fault\n";

if ($proceed) {
  my $nfiles = scalar(@infiles);

  # Now create the output map, using the new bounds if necessary.
  orac_print "Making map from $nfiles input files";
  orac_print " - this may take a while..." if ($nfiles > 16);
  if ($nfiles > 64) {
    my $long = ($nfiles > 128) ? "REALLY long" : "long";
    orac_print " a $long while... please be patient...";
  }
  orac_print "\n";
  my $outfiles_obj = new ORAC::TempFile(0); # New name to avoid destruction.
  my $outfiles = $outfiles_obj->file(); # read_file_list() fails otherwise!
  unlink $outfiles; # MAKEMAP fails if the file already exists!
  $args .= " outfiles=$outfiles";
  $args .= ' jsatiles' if $jsa_tiles;
  my $SMURF_STATUS = $Mon{smurf_mon}->obeyw("makemap","$args");
  my @out = read_file_list($outfiles);

  # Check for SMF__NOMEM return status.
  if ( $SMURF_STATUS == ORAC__OK ) {

    # Check that an output file was actually created.  In some
    # instances bad data can be dealt with by MAKEMAP, which then exits
    # with good status and no output file.
    # Is this test necessary any more when using "outfiles"?
    @out = grep {
        my $fullout = File::Spec->catfile($ENV{ORAC_DATA_OUT}, $_);
        $fullout .= ".sdf" unless ($fullout =~ /.sdf$/); # hack...
        -e $fullout;
    } @out;

    if (scalar @out) {

      # Now, is there any analysis of the model-component files that could
      # be useful?

      # Set the TITLE to include the source name, and add the
      # observation number if the recipe is running at the summit.
      my $title = ($Frm->uhdr("SUMMIT")) ? "$object / $obsnum" : "$object";

      # Place Parameter TITLE before Parameter NDF to prevent object names
      # containing parentheses from confusing the ADAM command-line parser,
      # which attributes the parentheses to the NDF.
      _FILTER_QUOTED_STRING_ SINGLE=1 STRING=$title
      my $quoted_title = $_FILTER_QUOTED_STRING_{'QUOTED'};
      foreach $out (@out) {
         $Mon{ndfpack_mon}->obeyw("settitle", "title='$quoted_title' ndf=$out");
      }

      # Register the new files and tag them as MAKEMAP output. Re-read
      # the header because the Frame probably now contains a different number
      # of files.
      $Frm->force_product_update("makemap", @out);
      $Frm->tagset("makemap");
      $Frm->readhdr();
      $Frm->product("newimage");
      $Frm->tagset("newimage");

      # Check the image to see if the target is covered by good pixels.
      my $ngood = 0;
      my %sigma;
      foreach $out (@out) {
        _FIND_IMAGE_STATISTICS_ IMAGE=$out VERBOSE=0 HISTAT=0
        $ngood += $_FIND_IMAGE_STATISTICS_{RESULTS}->{NUMGOOD};
	$sigma{$out} = $_FIND_IMAGE_STATISTICS_{RESULTS}->{SIGMA};
      }
      @out = map {$_} grep {$sigma{$_} > 0} @out;

      my $nbol = sprintf "%d", $Frm->hdr("NBOLOEFF");
      if ($nbol > 0 && $ngood > 0 && scalar(@out)) {
        foreach $out (@out) {
	  orac_print "Thank you for waiting: image $out created using $nbol bolometers\n";
	  orac_warn "Map created from only $nbol bolometers: check map quality\n"
	    if ($nbol < 100);

	  # Print the config parameters to the screen. This will only
	  # show up if the pipeline is run with -verbose, such as at
	  # CADC.
	  $Mon{kappa_mon}->obeyw("configecho","ndf=$out config=! name=! application=makemap");
        }

	# Mark the current frame as good now that we have a map.
	$Frm->isgood(1);

	# Recordthat sky subtraction and extinction correction have been
	# applied, though there really isn't much point to setting these
	# other than a desire for consistency.
	$Frm->uhdr("SKY_SUBTRACTION",1);
	$Frm->uhdr("EXTINCTION",1);
	$Frm->uhdr("FLATFIELD",0) if ($Frm->uhdr("FLATFIELD"));

	# Shift the image to nominal source position if a calibrator,
	# except for pointing (obviously...!) and focus observation (done
	# later) and JSA tiles. The exception for pointing/focus is
	# when processed using a non-pointing/focus recipe where the
	# map is of actual scientific interest.
	if ($point_focus) {
          my $out = $out[0]; # Just check the attribute in the first file.
	  $Mon{ndfpack_mon}->obeyw("wcsattrib","ndf=$out mode=get name=skyrefis");
	  my ($ORAC_STATUS, $skyrefis) = $Mon{ndfpack_mon}->get("wcsattrib","value");
	  $point_focus = 0 if (lc($skyrefis) ne "origin");
	}
	if (($Cal->iscalsource($object) || $shift_non_cal)
            and ! $point_focus and ! $jsa_tiles) {
	  _SHIFT_IMAGES_ INPLACE=1 USE_SKYREF=$shift_use_skyref
          $image_shifts = $_SHIFT_IMAGES_{'FITPEAKS'};
	}
      } else {

        # Mark this frame as BAD since every bolometer was flagged.
	$Frm->isgood(0);
	$msg = "Makemap created a blank map: data from this Frame will be ignored.\n$fault_msg";
      }
    } else {

      # Mark this frame as BAD. The only reason we would be here is
      # that the data are bad/corrupt.
      $Frm->isgood(0);
      $msg = "Makemap failed to create an output file (corrupt data): data from this Frame will be ignored.\n\n$fault_msg";
    }
  } else {

    # OK we have bad exit status, hence check for known reasons.
    if ( $SMURF_STATUS == 235176770 ) {

      # SMF__NOMEM: Memory allocation error, or data require chunking
      # if memcheck = 1.
      if ($memcheck) {
	$msg = "Data will be split into chunks - will not continue at user request\n";
      } else {
	$msg = "Makemap could not allocate the memory required to create a map - will not continue\n";
      }

    # AST__NOMEM: memory allocation error (can be triggered from smf_fft_data)
    } elsif ( $SMURF_STATUS == 233933562 ) {
      $msg = "MAKEMAP could not allocate the memory required to create a map - will not continue\n";

    # SMF__INSMP: insufficient samples for statistics
    } elsif ( $SMURF_STATUS == 235176794 ) {
      $msg = "All data flagged bad: insufficient samples available for calculating statistics - will not continue\n";

    # SMF__NOSCI: no science data found
    } elsif ( $SMURF_STATUS == 235176858 ) {
      $msg = "No science data found: data from this Frame will be ignored\n";

    # SMF__TELSTAT: telescope is stationary
    } elsif ( $SMURF_STATUS == 235176866 ) {
      $msg = "Telescope is stationary or moving very slowly: data from this Frame will be ignored\n";

    # ORAC__BADENG - indicates SMURF has crashed.
    } elsif ( $SMURF_STATUS == ORAC__BADENG ) {

      # Mark the Frame as bad and return immediately with the current status.
      orac_err "***** SMURF has crashed - aborting recipe *****\n";
      delete $Mon{smurf_mon};
      $Frm->isgood(0);
      return $SMURF_STATUS;
    } else {

      # Everything else...
      $msg = "MAKEMAP failed for an unknown reason: data from this Frame will be ignored\n$fault_msg";
    }
    $Frm->isgood(0);
  }
}

# If no new image was created issue an error saying why.
unless ($Frm->isgood) {
  $Frm->uhdr("NOCALIB",1);
  $Frm->uhdr("EXTINCTION",1);
  $Frm->uhdr("SKY_SUBTRACTION",1);

  # Terminate the recipe unless we're processing the last member and
  # have more than one member in the group, in which case we still
  # want a Group file to be created if possible.
  my @members = $Grp->allmembers;
  if ($Grp->lastmember($Frm) && @members > 1) {
    orac_err $msg;
  } else {
    orac_termerr $msg;
  }
}

# Delete the temporary config file if one was created.
if ($Frm->uhdr("FAKEMAP") && $config) {
  my $tmpconfig = $config;
  if ($tmpconfig =~ /,/ && $tmpconfig =~ /_copy/) {
    my @cfgstr = split(/,/, $config, 2);
    $tmpconfig = $cfgstr[0];
  }
  unlink $tmpconfig if (-e $tmpconfig && $tmpconfig =~ /_copy$/);
}

$_PRIM_ARGS_->{'IMAGE_SHIFTS'} = $image_shifts;
