# -*-cperl-*-

=head1 NAME

_CALC_IMAGE_NOISE_ - Determine noise in an image

=head1 SYNOPSIS

_CALC_IMAGE_NOISE_ METHOD=VAR

=head1 DESCRIPTION

This primitive is the top-level method for calculating the image
noise. It may be used on a particular image or the current Frame or
Group file. If no image is specified via the B<IMAGE> parameter then
the B<GROUP> flag is used to determine whether to use the Frame or
Group image. If an image is given the standard deviation is returned
as an entry in the primitive hash and no log entry is written.

The noise may be calculated using one of three methods:

=over 4

=item MASK

Mask out all sources detected in the image and calculate the
statistics of the remaining valid pixels.

=item MINIMUM

Systematically place a series of apertures on the image and calculates
the image statistics within that aperture. The minimum value is the
sky noise.

=item VARIANCE

Calls the _FIND_VARIANCE_ primitive to return the standard deviation
of the stacked image via the VARIANCE component of the input file.

=back

=head1 ARGUMENTS

The following arguments are supported:

=over 4

=item B<GROUP>

If true, use the current Group file as input, otherwise use the
current Frame. Default is 0 (false: use current Frame).

=item B<IMAGE>

Name of an image to be analyzed. Default is to use either the current
Frame or Group image, depending on the valued of B<GROUP>.

=item B<LOGFILE>

A flag to indicate whether log files should be written containing the
results of the flux and noise calculations. Default is 1
(true). However, if an input file is specified no log is written.

=item B<METHOD>

Method by which the noise is determined. Valid values are C<MINIMUM>,
C<MASK> and C<VARIANCE>. Default is variance. Minimum matching is
supported.

=item B<RADIUS>

Radius of apertures to use for aperture photometry and/or noise
estimates. Default is 20 arcsec. A special value of -1 may be
specified in which case the aperture is set by the major and minor
axes of the object.

=item B<VERBOSE>

Flag to indicate whether informational messages should be
given to the user. Default is true.

=back

=head1 EXTERNAL TASKS

The following external tasks are used:

=over 4

=item KAPPA:

ARDMASK, NDFTRACE

=item ORAC-DR PRIMITIVES:

_ADD_NOISE_LOG_ENTRY_, _FIND_INTEGRATED_INTENSITY_,
_FIND_IMAGE_STATISTICS_, _FIND_VARIANCE_, _GET_PIXEL_SCALE_

=back

=head1 NOTES

This primitive must be run after images have been mosaicked.

If the Group image is specified, the noise is only calculated if a new
Group image has been created (as determined by the Group uhdr entry
C<NEWIMAGE>) in order to prevent multiple logging of unchanged values.

The minimum-aperture method is designed to be used on relatively small
maps (up to 10 arcmin across) and assumes that the source emission is
located at the map centre.

=head1 OUTPUT DATA

The following entry in the primitive hash is returned unless a log
file entry is written:

=over 4

=item SIGMA

The standard deviation in the image

=back

=head1 OUTPUT FILES

If the B<LOGFILE> parameter is true, then the noise is written to
C<log.noise>.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2007-2008 University of British Columbia. All Rights
Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# Write logfiles (default to true)
my $uselog = get_prim_arg( $_PRIM_ARGS_, "LOGFILE", 1 );

# Should I be verbose? (Default to no)
my $verbose = get_prim_arg( $_PRIM_ARGS_, "VERBOSE", 0);

# Set aperture radius - default is 20 arcsec
my $radius = get_prim_arg( $_PRIM_ARGS_, "RADIUS", 20);

# Determine method for calculating noise
my $method = lc(get_prim_arg( $_PRIM_ARGS_, "METHOD", "var" ));

# See if an input file has been specified
my $in = get_prim_arg( $_PRIM_ARGS_, "IMAGE", undef );

# Determine if the user wants to use the current stacked Grp or Frm image
my $Obj;
# Flag to denote whether this primitive can be executed
my $calcnoise = 0;
my $group = get_prim_arg( $_PRIM_ARGS_, "GROUP", 0 );
unless ( defined $in ) {
  if( $group ) {
    $Obj = $Grp;
  } else {
    $Obj = $Frm;
  }
  if ( $Obj->nfiles == 1 ) {
    $in = $Obj->file;
    # Set calcnoise if the requested file exists
    if ( $Obj->file_exists ) {
      # Check if NEWIMAGE flag is set for Group image
      if ( $group ) {
	$calcnoise = ( $Grp->uhdr("NEWIMAGE") ) ? 1 : 0;
      } else {
	$calcnoise = 1;
      }
    } else {
      orac_warn "File $in does not exist: skipping this primitive\n" if $verbose;
    }
  } else {
    orac_throw "Error: _CALC_IMAGE_NOISE_ must be run after images have been combined\n";
  }
} else {
  # Unset uselog if an input file has been specified
  $uselog = 0;
  $calcnoise = 1;
}

# Proceed if all's well
if ( $calcnoise ) {
  my ($maskbright, $minap, $var) = (0, 0, 1);
  my $catalogue;
  # Set flags for each method
  if ( $method =~ /^ma/ ) {
    # Find all of the sources in the current frame
    _FIND_SOURCES_ IMAGE=$in
    # Retrieve catalogue
    $catalogue = $_FIND_SOURCES_{CATALOGUE};
    if ( $catalogue->sizeof == 0 ) {
      orac_warn "Unable to use chosen MASK method for noise estimation - no sources in catalogue\nFalling back on default method `variance'\n";
      $maskbright = 0;
      $var = 1;
    } else {
      $maskbright = 1;
    }
  } elsif ( $method =~ /^mi/ ) {
    $minap = 1;
  } elsif ( $method =~ /^var/ ) {
    $var = 1;
  } else{
    orac_warn "Unknown choice for parameter METHOD, $method - falling back on default value of `variance'\n";
    $maskbright = 0;
    $var = 1;
    $minap = 0;
  }
  my $sigma = undef;
  my $ORAC_STATUS;
  # 1) Mask out bright sources
  if ( $maskbright ) {
    # Create ARD file
    my $ard = new ORAC::TempFile;
    if (starversion_gt('kappa','V0.18-0')) {
      print {$ard->handle} "COFRAME(SKY,System=FK5)\n";
    }
    # Loop over all sources in image
    print {$ard->handle} "(\n";
    my $imax = $catalogue->sizeof;
    for my $i ( 1 .. $imax ) {
      my $source = $catalogue->starbyindex( $i - 1 );
      # Retrieve Astro::Coords object associated with current source
      my $coords = $source->coords;
      my $ra = $coords->ra;
      my $dec = $coords->dec;
      # Now retrieve morphology
      my $morph = $source->morphology;
      # Position angle
      my $pa = $morph->position_angle_world;
      # Major/minor axes - make 50% larger
      my $a = 7200.0 * $morph->major_axis_world;
      my $b = 7200.0 * $morph->minor_axis_world;
      # Elliptical aperture
      print {$ard->handle} "ELLIPSE($ra, $dec, 00:00:$a, 00:00:$b, $pa )\n";
    }
    print {$ard->handle} ")\n";
    $ard->handle->close;
    # Create temporary output file for ardmask
    my $outobj = new ORAC::TempFile;
    my $out = $outobj->file;
    # Name of ARD file
    my $ardin = $ard->file;
    my $ardargs;
    if (starversion_lt('kappa','V0.18-0')) {
      $ardargs = " cosys=data ";
    } else {
      $ardargs = " "; # in the ard file
    }
    # Mask out the bright sources
    $Mon{kappa_mon}->obeyw("ardmask", "in=$in out=$out ardfile=$ardin $ardargs");
    # Calculate statistics for the rest of the image
    _FIND_IMAGE_STATISTICS_ IMAGE=$out VERBOSE=0 HISTAT=0
    $sigma = $_FIND_IMAGE_STATISTICS_{RESULTS}->{SIGMA};
  } elsif ( $minap ) {
    # 2) Systematically plonk down an aperture across the image and
    # calculate stats within each aperture: lowest std dev = sky noise
    # The idea is to get 4 or 8 apertures over the map, which must be at
    # least 2 aperture-diameters across, preferably >4.
    my $apdiam = 2.0*$radius;

    # Retrieve map size from FITS header
    # Assume map is oriented along axes for now
    my $map_wdth = $Frm->hdr("MAP_WDTH");
    my $map_hght = $Frm->hdr("MAP_HGHT");

    # No point continuing if the map is too small/aperture too large.
    # Return undef.
    my $minmapsize = 4.0 * $radius;
    if ( ($map_wdth < $minmapsize) || ($map_hght < $minmapsize) ) {
      orac_warn "Unable to estimate noise using apertures - map too small ($map_wdth x $map_hght arcsec) or aperture too large ($apdiam arcsec)\n";
    } else {

    # Now ascertain how many apertures are being used
    $minmapsize *= 2;
    my $nxap = ( $map_wdth > $minmapsize ) ? 3 : 2;
    my $nyap = ( $map_hght > $minmapsize ) ? 3 : 2;

    # Obtain the pixel scale, including the pixel-to-sky mapping for later
    _GET_PIXEL_SCALE_ IMAGE=$in VERBOSE=0 UNITS=arcsec WANTMAP=1
    my $pixscale = sqrt( $_GET_PIXEL_SCALE_{AREA} );
    my $pixdiam = $apdiam / $pixscale;
    my $pixradius = $radius / $pixscale;

    # Retrieve bounds in pixel coords - note the implicit assumption
    # that the NDF has 2 dimensions.
    $Mon{ndfpack_mon}->obeyw("ndftrace","ndf=$in");
    ($ORAC_STATUS, my @lbound) = $Mon{ndfpack_mon}->get("ndftrace","lbound");
    ($ORAC_STATUS, my @ubound) = $Mon{ndfpack_mon}->get("ndftrace","ubound");

    # Calculate size of image in pixels...
    my $xsize = $ubound[0] - $lbound[0] + 1;
    my $ysize = $ubound[1] - $lbound[1] + 1;
    # ...and the aperture spacing...
    my $dx = int($xsize / $nxap);
    my $dy = int($ysize / $nyap);
    # ...and the first aperture centre
    my $xoff = ($dx > $pixdiam) ? $dx/2 : $pixradius;
    my $yoff = ($dy > $pixdiam) ? $dy/2 : $pixradius;
    my $x1 = $lbound[0] + $xoff;
    my $y1 = $lbound[1] + $yoff;

    # Define centre positions of the apertures
    my (@xpixels, @ypixels);
    my $nap = ($nxap == 3 && $nyap == 3) ? 8 : $nxap * $nyap;
    my $apindex = -1;
    for my $i (1..$nxap) {
      for my $j (1..$nyap) {
	$apindex++;
	# Skip the centre position for the 3x3 case as we know it
	# contains the source
	next if ($nap == 8 && $apindex == 4);
	push( @xpixels, $x1 + ($i-1)*$dx );
	push( @ypixels, $y1 + ($j-1)*$dy );
      }
    }

    # Pixel to SKY mapping
    my $pixtosky = $_GET_PIXEL_SCALE_{MAPPING};

    # Convert pixel positions to sky positions in one call
    my ($xsky, $ysky) = $pixtosky->Tran2( \@xpixels, \@ypixels, 1 );

    # Calculate number of pixels within an aperture
    use Astro::SLA qw/ :constants /;
    my $npix = DPI * $radius*$radius / $_GET_PIXEL_SCALE_{AREA};

    # Now loop over number of apertures
    orac_print "Looping over $nxap x $nyap apertures\n" if ($verbose);
    use Astro::Coords;
    my @imnoise;
    for my $i (1..$nap) {
      # Convert X, Y positions to sexagesimal string via an Astro::Coords object
      my $coords = new Astro::Coords( ra => $xsky->[$i-1], dec => $ysky->[$i-1],
				      units => 'radians', type => 'J2000' );
      my @xy = ( $coords->ra(format=>'s'), $coords->dec(format=>'s') );
      _FIND_INTEGRATED_INTENSITY_ RADIUS=$radius IMAGE=$in XYPOS=\@xy VERBOSE=0
      my $imnoise = $_FIND_INTEGRATED_INTENSITY_{SIGMA};
      my $ngood = $_FIND_INTEGRATED_INTENSITY_{NUMGOOD};
      push(@imnoise, $imnoise) if ($ngood/$npix > 0.5);
    }
    use Statistics::Descriptive;
    my $stats = new Statistics::Descriptive::Full;
    $stats->add_data(\@imnoise);
    $sigma = $stats->mean;

    # Throw away the max value if excessive as it likely indicates
    # presence of another source. If the mean exceeds the median, then
    # there are values significantly higher than the median. This
    # could indicate outliers - so check for mean > median. This
    # method appears to work well enough for the small numbers of
    # points in these data arrays.
    my @newnoise;
    if ($stats->mean > $stats->median) {
      # If the deviation between the median and the max is greater
      # than that between the median and the minimum then the max
      # value is probably too high to be included in the estimate of
      # the image noise.
      if (abs($stats->median - $stats->max) > abs($stats->median - $stats->min)) {
	foreach my $datum (@imnoise) {
	  push(@newnoise, $datum) if (($datum != $stats->max));
	}
	# Recalculate the statistics
	$stats->clear();
	$stats->add_data(\@newnoise);
	$sigma = $stats->mean;
      }
    }
  }
  } elsif ( $var ) {
    # 3) Use the Variance component within the NDF as a measure of the noise
    _FIND_VARIANCE_ IMAGE=$in COMP=err
    $sigma = $_FIND_VARIANCE_{RESULT};
    # Check if sigma is a BAD value
    if ( $sigma == &NDF::VAL__BADD() ) {
      undef $sigma;
    }
  }

  # Record and/or print sigma if successful
  if ( $sigma ) {
    # Write info to log file if requested
    if ( $uselog ) {
      my $logtype = "noise";
      my @names = ("Sigma");
      # Reformat sigma for printing
      $sigma = sprintf "%12.7f", $sigma;
      my @values = ($sigma);
      _ADD_LOG_ENTRY_ LOGFILE=$logtype ENTRIES=\@values PARAMS=\@names GROUP=$group
    }
    $Mon{ndfpack_mon}->obeyw("ndftrace","ndf=$in");
    my ($ORAC_STATUS, $units) = $Mon{ndfpack_mon}->get("ndftrace","units");
    orac_print "Noise in image = $sigma $units\n" if $verbose;
  } else {
    orac_warn "No valid standard deviation found\n";
  }

  # If we are not logging these data to a file, assume we are being
  # called from another primitive and store the result in the primitive
  # hash
  unless ($uselog) {
    $_CALC_IMAGE_NOISE_{SIGMA} = $sigma;
  }
}
