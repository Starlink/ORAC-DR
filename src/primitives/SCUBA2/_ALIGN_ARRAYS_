#! -*-perl-*-

=head1 NAME

_ALIGN_ARRAYS_ - Align all images to a reference image

=head1 SYNOPSIS

 _ALIGN_ARRAYS_

=head1 DESCRIPTION

This primitive takes all the images within a Frm object and aligns
them to a common coordinate system as defined by a reference image. By
default this primitive operates on files within the current Frm, but
this can be over-ridden by supplying an optional Frm object.

If a group file exists, that is used as the reference, else the
current reference image in the Cal object, or finally the first frame
is chosen as reference if neither of those exist.

=head1 ARGUMENTS

=over 4

=item B<METHOD>

Keyword specifying the method for determing how the output pixels are
populated. The default is nearest-neighbour resampling
(C<nearest>). See the documentation for WCSALIGN for further
details. The recommended choices are C<nearest>, C<bilinear>, or
C<sincsinc>.

=item B<REBIN>

A flag to determine whether to rebin or resample. Default is 0
(resample). See the WCSALIGN documentation for further information.

=item B<REFIMAGE>

The name of the reference image to use for alignment in the SKY
domain. If left blank, the primitive will call _GET_REFERENCE_IMAGE_.

=item B<SKYREF>

A flag to denote whether to align the images to a celestial coordinate
frame. The default is 0 (false) and the images will be aligned with
either the group file if it exists or the first image in the Frame.

=item B<FRAME>

An ORAC-DR Frm object with files to be aligned. If left unset, the
current Frm is used.

=item B<COMP>

A flag to denote whether to also align any NDFs within the parent
file. Default is 0 (do not align). The only sub-NDF currently
supported is C<.MORE.SMURF.EXP_TIME>. Others should be added as
necessary.

=back

=head1 ALGORITHMS

=over 4

=item Starlink applications

KAPPA WCSALIGN

=item ORAC-DR primitives

_GET_REFERENCE_IMAGE_

=head1 FILE SUFFIX

Creates an output image per input image, each with a suffix of '_al'.

=head1 NOTES

There is currently no checking that other NDF components exist before
alignment (if COMP = 1).

=head1 AUTHOR

Tim Jenness E<lt>t.jenness@jach.hawaii.eduE<gt>
Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2004-2007 Particle Physics and Astronomy Research
Council. Copyright (C) 2004-2008 the University of British
Columbia. All Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA

=cut

# Check arguments
# Has the user passed in a Frame object?
my $Frmobj = get_prim_arg( $_PRIM_ARGS_, "FRAME", $Frm );

# Check that the user has really passed in a Frm!
unless ( UNIVERSAL::isa($Frmobj, "ORAC::Frame") ) {
  orac_throw "Error: input FRAME to _ALIGN_ARRAYS_ is not an ORAC-DR Frm object\n";
}

# Has the Frame object already been aligned? Only proceed if not.
my $aligned = $Frmobj->uhdr("ALIGNED");
if ( !$aligned ) {
  # Rebin or resample? Default is resample (rebin = 0).
  my $rebin = get_prim_arg( $_PRIM_ARGS_, "REBIN", 0 );

  # Method for determining the output pixel values
  my $method = lc(get_prim_arg( $_PRIM_ARGS_, "METHOD", "bilinear" )); 

  # Set reference image if specified
  my $refimage = get_prim_arg( $_PRIM_ARGS_, "REFIMAGE", "" );

  # Are we aligning images in a SKY frame? Default to yes.
  my $skyalign = get_prim_arg( $_PRIM_ARGS_, "SKYREF", 1 );

  # Flag to decide whether to align other components
  my $comp = get_prim_arg( $_PRIM_ARGS_, "COMP", 0 );

  # Verbose?
  my $verbose = get_prim_arg( $_PRIM_ARGS_, "VERBOSE", 0 );

  # If refimage not given then retrieve current reference image
  if ( $refimage eq "" ) {
    _GET_REFERENCE_IMAGE_ SKYREF=$skyalign
    $refimage = $_GET_REFERENCE_IMAGE_{REFIMAGE};
  }

  # Check if we have a moving object
  my $moving = $Grp->uhdr("MOVING");
  orac_print("Setting attributes for moving sources... ") 
    if ($moving && $verbose);

  # Write alignment text files
  my $intmp = new ORAC::TempFile;
  my $outtmp = new ORAC::TempFile;
  my (@in, @out);
  my $nfiles = $Frmobj->nfiles;
  for my $i (1..$nfiles) {
    my ($in, $out) = $Frmobj->inout('_al', $i);
    print {$intmp->handle} $in ."\n";
    # Set SkyRefIs and AlignOffset attributes for moving sources
    if ( $moving ) {
      my $args = "ndf=$in mode=mset setting='skyrefis=origin,alignoffset=1'";
      $Mon{ndfpack_mon}->obeyw("wcsattrib","$args");
    }
    push(@out, $out);
    push(@in, $in);
    orac_warn "Input and output files are the same\n" if ( $in eq $out );
    print {$outtmp->handle} "$out\n";
  }

  orac_print("Aligning $nfiles ".( ($nfiles == 1) ? "image" : "images")." to common coordinate frame...\n");

  $Mon{kappa_mon}->obeyw("wcsalign","ref=$refimage lbnd=! ubnd=! method=$method out=^". $outtmp->file ." in=^". $intmp->file);

  undef $intmp;
  undef $outtmp;

  # If we are also aligning any sub-components then do that now.
  if ( $comp ) {
    # Start with exp_time - add more as necessary. Note that there is NO
    # checking to see if the component exists before attempting to align.
    my @comp = (".more.smurf.exp_time");
    foreach my $ndfcomp ( @comp ) {
      my $component = "exposure times" if ($ndfcomp =~ /exp_time$/);
      orac_print "Aligning $component... " if $verbose;
  
      $intmp = new ORAC::TempFile;
      $outtmp = new ORAC::TempFile;
      for my $i (0..$#in) {
	# Set SkyRefIs and AlignOffset attributes for moving sources -
	# note that these components have separate WCS and must therefore
	# also be modified for moving sources
	if ( $moving ) {
	  my $args = "ndf=$in[$i] mode=mset setting='skyrefis=origin,alignoffset=1'";
	  $Mon{ndfpack_mon}->obeyw("wcsattrib","$args");
	}
	print {$intmp->handle} $in[$i].$ndfcomp."\n";
	print {$outtmp->handle} $out[$i].$ndfcomp."\n";
      }
      $Mon{kappa_mon}->obeyw("wcsalign","ref=$refimage lbnd=! ubnd=! method=$method out=^". $outtmp->file ." in=^". $intmp->file);
    }
  }

  # Set uhdr variables to indicate the files in the given Frame object
  # are now aligned to a common coordinate frame, store the name of
  # the reference image for sanity checking.
  $Frmobj->uhdr("ALIGNED",1);
  $Frmobj->uhdr("REFIMAGE",$refimage);
  # Register new file names
  $Frmobj->files( @out );

  orac_print("done.\n") if $verbose;
}
