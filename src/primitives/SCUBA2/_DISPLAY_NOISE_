# -*-cperl-*-

=head1 NAME

_DISPLAY_NOISE_ - Display bolometer noise

=head1 SYNOPSIS

_DISPLAY_NOISE_

=head1 DESCRIPTION

This primitive displays the bolometer noise data in a Kapview
window. The subarrays are mosaicked in the focal plane and this image
is displayed, allowing a direct comparison of the relative noise in
each subarray. The noise image is displayed in the top row and a
histogram in the bottom row.

The scale for the display (both the image and the histogram) is set by
noise specification as defined in the QA input file (C<qa.ini>).

=head1 ARGUMENTS

This primitive supports the following arguments:

=over 4

=item B<COMP>

Component to display. Supported values are C<DATA> and C<ERR>. Default
is C<DATA>.

=item B<LOGFILE>

Flag to denote whether or not to write a log file. Default is 1 (yes).

=item B<THRESH>

Upper limit for display in units of the median noise. Default is 4.

=back

=head1 EXTERNAL TASKS

The following external tasks are used by this primitive:

=over 4

=item KAPPA:

NDFTRACE, SETTITLE, THRESH

=item ORAC-DR PRIMITIVES:

_FIND_IMAGE_STATISTICS_, _MAKE_MOSAIC_FPLANE_

=back

=head1 OUTPUT DATA

The noise mosaic is stored in the current Frame (suffix C<_noimos>)
and tagged with C<noimos>. The percentage-change noise image (suffix
and tag C<_pcnoi>) and NEP mosaic (C<_nepmos>) are also stored, but
will be deleted at the end of processing.

=head1 OUTPUT FILES

The mosaic of the noise images has a suffix C<_noimos> or C<_avnoimos>
for the noise and average-noise mosaics respectively

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2009-2013 the University of British Columbia.  All
Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# Only continue if noise data actually exists in the Frame
return ORAC__OK unless ($Frm->tagexists("noise"));

# Skip the display if the last noise result for FOCUS observations
if ($Frm->hdr("OBS_TYPE") =~ /focus/i && ($Frm->uhdr("QUICKLOOK") || $Frm->uhdr("SUMMIT"))) {
  return ORAC__OK if ($Frm->hdrval("NSUBSCAN") > 1);
}

# Only display every third (noise) subscan in the QL pipeline
my $dispnoise = 1;
if ($Frm->uhdr("QUICKLOOK")) {
  $dispnoise = 0
    if ($Frm->hdr("SEQ_TYPE") =~ /science/i && $Frm->hdr("NSUBSCAN") % 3 != 0);
}

# Which component is being displayed?
my $comp = lc(get_prim_arg( $_PRIM_ARGS_, "COMP", "data" ));
# Reset comp if we get something we don't recognize
$comp = "data" unless ( $comp =~ /^err/ );

# Threshold for displaying noise data - limit to 10 times the median
# otherwise what's the point?
my $thr = get_prim_arg( $_PRIM_ARGS_, "THRESH", 4 );
$thr = 10 if ($thr > 10);

# Check for the total number of good bolometers and the maximum value
# for plotting - these were calculated by _ANALYZE_NOISE_
my $sumnumgood = $Frm->uhdr("NOISE_NUMGOOD");
my $plotmax = $Frm->uhdr("NOISE_SPEC");
my $display_ok = ($sumnumgood && $sumnumgood > 0) ? 1 : 0;

my $units = "pA s**0.5";
# If there are good bolometers, proceed with making a focal-plane
# noise mosaic and displaying it
if ($display_ok) {
  # Reference subarray
  my $short = ($Frm->uhdr("ORAC_FILTER") =~ /450/) ? 1 : 0;
  my $refsub =  ($short) ? "s4d" : "s8b";
  # Mosaic noise files
  my %ref;
  # The current Frame should contain 1 file per subarray
  $Frm->tagset("display_noise_in");
  foreach my $file ($Frm->files) {
    # Never trust the filenames...
    my $fitshdr = new Astro::FITS::Header::NDF( File => $file );
    $ref{lc($fitshdr->value("SUBARRAY"))} = $file;
  }
  my $ref = $ref{$refsub};
  unless ($ref) {
    # Pick first one...
    my @subs = sort keys %ref;
    $refsub = $subs[0];
    $ref = $ref{$refsub};
  }
  # Output file name for responsivity mosaic: _noimos
  my $noimos = "_noimos";
  my $wave = ($short) ? "450 um" : "850 um";
  # Set the title of the noise image to something meaningful
  my $obs = $Frm->hdr("OBSNUM");
  my $extra = "";
  if ($Frm->uhdr("QUICKLOOK")) {
    $extra = "/ scan " .$Frm->hdr("NSUBSCAN"). " (QL)";
  } elsif ($Frm->uhdr("SUMMIT")) {
    $extra = "/ scan " .$Frm->hdr("NSUBSCAN"). " (SUMMIT)";
  }
  my $title = "NOISE $wave: obs $obs $extra";
  if ($Frm->uhdr("AVNOISE")) {
    $noimos = "_avnoimos";
    $title = "Average noise (".($Frm->uhdr("AVNOISE"))." files)";
  }

  my %refimage = ( refsub => $refsub, refimage => $ref );
  _MAKE_MOSAIC_FPLANE_ SUFFIX=$noimos REFERENCE=\%refimage
  my $out = $Frm->file;
  my $args = "ndf=$out title='$title'";
  $Mon{ndfpack_mon}->obeyw("settitle","$args");
  # Delete the SMURF extension from the output file before copying in
  # the NEP mosaic
  $Mon{ndfpack_mon}->obeyw("setext","ndf=$out xname=smurf option=delete noloop");

  my %options;
  if ($dispnoise) {
    if ($plotmax) {
      # Threshold the data at ZMAX and determine how many bolometers lie
      # above that cut
      my $thresh = new ORAC::TempFile;
      my $threshfile = $thresh->file;
      my $thrargs = "in=$out out=$threshfile thrhi=$plotmax newhi=bad thrlo=0 newlo=0";
      $Mon{kappa_mon}->obeyw("thresh","$thrargs");
      _FIND_IMAGE_STATISTICS_ IMAGE=$threshfile COMP=$comp VERBOSE=0
      my $nthrgood = $_FIND_IMAGE_STATISTICS_{RESULTS}->{NUMGOOD};
      $plotmax = sprintf "%g", $plotmax;
      my $threshpc = sprintf "%2.0f", 100.0 * $nthrgood / $sumnumgood;
      orac_print "Displaying noise data between 0 and $plotmax $units (Noise spec)\n", "green";
      orac_print "Histogram shows $nthrgood bolometers ($threshpc %) within noise spec ($plotmax $units)\n", "green";
    } else {
      # Make sure there is a defined upper limit for the histogram
      _FIND_IMAGE_STATISTICS_ IMAGE=$out COMP=$comp VERBOSE=0
      $plotmax = $_FIND_IMAGE_STATISTICS_{RESULTS}->{MAX};
      $plotmax = sprintf "%g", $plotmax;
      orac_print "Displaying noise data between 0 and $plotmax $units\n", "green";
    }

    orac_print "\nDisplaying focal-plane mosaic for $title\n";
    # Display the focal-plane mosaic and histogram
    my %options = ( ZMIN => 0, ZMAX => $plotmax, WINDOW => 1,
		    ZAUTOSCALE => 0, TITLE => $title );
    $Display->display_data($Frm, \%options, -1);
  }

  # Create the percentage difference noise image, but for the QL pipeline
  # only. This requires a previous image for comparison - the pipeline
  # will read its name from a flag file written on the previous pass
  # through the recipe.
  if ($Frm->uhdr("QUICKLOOK")) {
    my $prev;
    # Seeing as the percentage change monitoring extends over multiple
    # observations, the naming convention for this flag file uses the
    # shutter setting and the UT date only
    my $prefix = "snoi_" . ($Frm->hdr("SHUTTER"))."_";
    my $flagfile = File::Spec->catfile($ENV{ORAC_DATA_OUT},
				       ".".$prefix.$Frm->hdrval('UTDATE').".ok");
    if (-e $flagfile) {
      my $files = read_file_list($flagfile);
      $prev = $files->[0];
    }
    if ($prev && $dispnoise) {
      orac_print "Creating and displaying noise percentage change image...\n";
      my $deltafile = $Frm->inout("_pcnoi");
      my $exp = "PA*(IA-IB)/IB";
      my $args = "exp=$exp IA=$out IB=$prev PA=100.0 out=$deltafile";
      $Mon{kappa_mon}->obeyw("maths","$args");
      $title = 'NOISE percentage change';
      $args = "ndf=$deltafile title='$title'";
      $Mon{ndfpack_mon}->obeyw("settitle","$args");
      $Frm->files($deltafile);
      $Frm->tagset("pcnoi");
      %options = ( ZMIN => -10, ZMAX => 10, WINDOW => 1,
		   ZAUTOSCALE => 0, TITLE => $title );

      $Display->display_data($Frm, \%options, -1);
      # Retrieve the focal-plane mosaic
      $Frm->tagretrieve($noimos);
    }
    # Store current noise mosaic in a flag file to be picked up on the
    # next pass through the recipe
    my @out = $Frm->files;
    _WRITE_FLAG_FILE_ PREFIX=$prefix FILES=\@out OBSFLAG=0
  }

  # Finally create and display NEP mosaic
  $units = "W s**0.5";
  orac_print "Creating ".( $dispnoise ? "and displaying " : "")."NEP mosaic\n";
  my $nepmos = "_nepmos";
  my $smurf = "nep";
  $refimage{refimage} = $ref .".more.smurf.nep";
  $Frm->tagretrieve("display_noise_in");
  _MAKE_MOSAIC_FPLANE_ SUFFIX=$nepmos SMURF=$smurf REFERENCE=\%refimage
  my $nepout = $Frm->file;
  # Delete the SMURF extension from the NEP mosaic before it's copied
  # into the top-level file
  $Mon{ndfpack_mon}->obeyw("setext","ndf=$nepout xname=smurf option=delete noloop");

  # Set the title, store the mosaic and display it
  $title = "NEP $wave: obs $obs $extra";
  $args = "ndf=$nepout title='$title'";
  $Mon{ndfpack_mon}->obeyw("settitle","$args");
  if ($dispnoise) {
    $Frm->files($nepout);
    $plotmax = $Frm->uhdr("NEP_SPEC");
    if ($plotmax) {
      $plotmax = sprintf "%g", $plotmax;
      orac_print "Displaying NEP data between 0 and $plotmax $units (NEP spec)\n", "green";
    } else {
      # Make sure there is a defined upper limit for the histogram
      _FIND_IMAGE_STATISTICS_ IMAGE=$out COMP=$comp VERBOSE=0
      $plotmax = $_FIND_IMAGE_STATISTICS_{RESULTS}->{MAX};
      $plotmax = sprintf "%g", $plotmax;
      orac_print "Displaying NEP data between 0 and $plotmax $units\n", "green";
    }
    %options = ( ZMIN => 0, ZMAX => $plotmax, WINDOW => 1,
		 ZAUTOSCALE => 0, TITLE => $title );
    $Display->display_data($Frm, \%options, -1);
  }
  orac_print "\n";

  # Now copy the NEP image into the noise mosaic as an NDF extension
  $args = "in=$nepout out=$out" . ".more.smurf.nep";
  $Mon{ndfpack_mon}->obeyw("ndfcopy","$args");
  # Store the noise file in the Frame
  $Frm->files($out);
  $Frm->tagset("noimos");

} else {
  orac_warn "Skipping display of focal-plane mosaic - no good bolometers\n";
}
