# -*-cperl-*-

=head1 NAME

_GET_MAKEMAP_CONFIG_ - Return a config file suitable for use with MAKEMAP

=head1 SYNOPSIS

 _GET_MAKEMAP_CONFIG_

=head1 DESCRIPTION

Returns the name of a configuration file which may be used by SMURF
MAKEMAP. The default behaviour is to return the system-installed
default configuration file. The Frame uhdr entry
C<MAKEMAP_CONFIG_TYPE> is used to determine the name of the (default)
config file if specified.

The user may over-ride the default behaviour by specifying their own
config file. This can be one in one of two ways. Either supply the
name of a config file via the B<CONFIG> parameter, or via the
C<MAKEMAP_CONFIG> recipe parameter. If not specified as an absolute
path, the file must exist in one of five locations (in the following
order):

=over 2

=item *

the current working directory;

=item *

the directory specified by $MAKEMAP_CONFIG_DIR;

=item *

the directory specified by $ORAC_DATA_OUT;

=item *

the directory specified by $ORAC_DATA_CAL;

=item *

the directory specified by C<share/smurf> under $STARLINK_DIR.

=back

The first match is used. The system default is used if that file can
not be found.

=head1 ARGUMENTS

The primitive has no arguments.

=head1 OUTPUT DATA

On exit, the following entry exists in the primitive hash:

=over 2

=item B<CONFIG>

Full path to the makemap configuration file to be used.

=back

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2009-2011,2013 University of British Columbia. All
Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

my $syscfg;
# Define a basic default config file
if ( $Frm->uhdr("QUICKLOOK") || $Frm->uhdr("SUMMIT") ) {
  my $ql = ($Frm->uhdr("QUICKLOOK")) ? 1 : 0;
  # Use the base dimmconfig unless it is overwritten by specific
  # MAKEMAP_CONFIG_TYPE uhdr.
  my $config_type = "base";
  my $pipeline = $ql ? "ql" : "summit";

  # The QL pipeline only creates images for pointing and focus
  # observations, so use that config file every time.
  if ($ql) {
    $config_type = $Frm->uhdr("MAKEMAP_CONFIG_TYPE")
      if ($Frm->uhdr("MAKEMAP_CONFIG_TYPE") eq "veryshort_planet" ||
	  $Frm->uhdr("MAKEMAP_CONFIG_TYPE") eq "pointing");
  } else {
    $config_type = $Frm->uhdr("MAKEMAP_CONFIG_TYPE")
      if ($Frm->uhdr("MAKEMAP_CONFIG_TYPE") eq "moon" ||
	  $Frm->uhdr("MAKEMAP_CONFIG_TYPE") eq "pointing" ||
	  $Frm->uhdr("MAKEMAP_CONFIG_TYPE") eq "bright_compact"
	 );
  }
  # Retrieve pipeline-specific default.
  $syscfg = $Cal->makemap_config( pipeline => $pipeline,
				  config_type => $config_type );
} else { # For the regular pipeline (not quicklook, not summit).

  # Get the requested CONFIG_TYPE and the appropriate dimmconfig.
  # Use jsa_generic rather than base if nothing is defined.
  if ( defined $Frm->uhdr("MAKEMAP_CONFIG_TYPE") ) {
    $syscfg = $Cal->makemap_config( config_type => $Frm->uhdr("MAKEMAP_CONFIG_TYPE") );
    orac_say("MAKEMAP_CONFIG_TYPE is " . $Frm->uhdr("MAKEMAP_CONFIG_TYPE") );
  } else {
    $syscfg = $Cal->makemap_config( config_type => 'jsa_generic' );
    orac_say("No specific MAKEMAP_CONFIG_TYPE set: using default of jsa_generic");
  }

  # Of course, it could have any old name so check that the file
  # actually exists...
  unless ( -e $syscfg ) {
    orac_warn "Requested MAKEMAP_CONFIG_TYPE '".
      $Frm->uhdr("MAKEMAP_CONFIG_TYPE")."' does not exist: using system default type jsa_generic.\n";
    $syscfg = $Cal->makemap_config( config_type => 'jsa_generic' );
  }
}
# Pointing observations of OMC1 need to have extra parameters added
if ($Frm->hdr("OBJECT") eq "OMC1" && $Frm->uhdr("MAKEMAP_CONFIG_TYPE") eq "pointing") {
  $syscfg .= ",ast.zero_snr=5,ast.zero_circle=<undef>";
  orac_say("Pointing observation of OMC-1: " .
           "adding ,ast.zero_snr=5,ast.zero_circle=<undef> to dimmconfig",
           'cyan');
}

# Now modify the config for particular conditions - just append
# these to the config string, though this might run into problems if
# the string is too long

# Change high-pass filter to 200" for DAISY mapping of extended sources
if ( (defined $Frm->uhdr("MAKEMAP_CONFIG_TYPE")) and $Frm->uhdr("MAKEMAP_CONFIG_TYPE") eq "bright_extended"
        and $Frm->hdr("SCAN_PAT") =~ /daisy/i) {
  orac_say("Daisy observation with bright_extended: " .
           "adding flt.filt_edge_largescale=200",
           'cyan');
  $syscfg .= ",flt.filt_edge_largescale=200";
}

# For Mars pointing and focus observations, disable common-mode
# flagging of bolometers because it's so bright
if ($Frm->hdr("OBJECT") =~ /mars/i && $Frm->hdr("OBS_TYPE") =~ /point|focus/i){
  $syscfg .= ",com.noflag=1";
  orac_say("Pointing or focus observation of Mars: " .
           "disabling common-mode bolometer flagging (com.noflag=1).",
           'cyan');

}
# Disable EXT model for focus observations only
if ($Frm->hdr("OBS_TYPE") =~ /focus/i) {
  $syscfg .= ",modelorder=(com,gai,flt,ast,noi)";
  orac_say("Focus observation: disabling EXT model by adding " .
           " ,modelorder=(com,gai,flt,ast,noi) to dimmconfig",
           'cyan');
}

# A specified config file over-rides the default
my $configfile = $syscfg;
my $config = $Frm->uhdr("RECPAR_MAKEMAP_CONFIG")
  if ( defined $Frm->uhdr("RECPAR_MAKEMAP_CONFIG") );

if ( defined $config ) {
  orac_say("User has requested config $config; checking if this exists");
  use File::Spec;
  # Test for an absolute path
  my $abs = File::Spec->file_name_is_absolute( $config );
  if ( $abs ) {
    # Use this file if it exists
    $configfile = $config if ( -e $config );
    orac_say("User requested $configfile exists.")
  } else {
    # OK not an absolute path - check in current working dir, and
    # other known locations if necessary. Use the first match.
    orac_say("User requested $config is not an absolute path: searching for file.");
    use Cwd;
    my $configdir = File::Spec->catdir($ENV{STARLINK_DIR}, "share", "smurf");
    my @locations = ( getcwd(), $ENV{MAKEMAP_CONFIG_DIR}, $ENV{ORAC_DATA_OUT},
		      $ENV{ORAC_DATA_CAL}, $configdir );
    foreach my $testlocation ( @locations ) {
      my $new_config = File::Spec->catfile( $testlocation, $config );
      if ( -e $new_config ) {
	$configfile = $new_config;
	last;
      }
    }
  }

  # If the specified config file exists then store it, otherwise fall
  # back on the default defined above.
  unless (defined $configfile) {
    orac_warn "Unable to locate given config file, $config - using system default\n";
    $configfile = $syscfg;
  }
}

# If we're doing fakemap processing, add the relevant parameters
# to the config file
if (defined $Frm->uhdr("FAKEMAP")) {
  orac_say("FAKEMAP processing requested: adjusting dimmconfig.");
  # Create a local config referencing the current config and adding fakemap params
  use File::Basename qw/basename/;
  my $tmpconfig = File::Spec->catfile($ENV{ORAC_DATA_OUT}, basename($configfile) ."_copy");
  open my $TMPFH, ">", "$tmpconfig"
    or orac_term "Unable to open temporary config file, $tmpconfig\n";
  print $TMPFH "^$syscfg\n";
  print $TMPFH "fakemap=".($Frm->uhdr("FAKEMAP"))."\n";
  print $TMPFH "fakescale=".($Frm->uhdr("FAKESCALE"))."\n"
    if (defined $Frm->uhdr("FAKESCALE"));
  close($TMPFH);
  $configfile = $tmpconfig;
}

$_GET_MAKEMAP_CONFIG_{CONFIG} = $configfile;
orac_say("Makemap is using dimmconfig file $configfile", "yellow");
