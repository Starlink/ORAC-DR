# -*-cperl-*-

=head1 NAME

_CREATE_BAD_BOLO_MASK_ - Create a bad bolometer mask

=head1 SYNOPSIS

 _CREATE_BAD_BOLO_MASK_ BEST_PERCENT=75

=head1 DESCRIPTION

This primitive creates a bad bolometer mask based on the distribution
of Noise Equivalent Power values. The C<bad> bolometers may be defined
as those which do not meet the (dark) specifications (though see the
B<RELAX> argument below), or as a percentage of the total (see the
B<BEST_PERCENT> argument below).

The best 50 % of bolometers will be used by default (corresponding to
a median cutoff - note that twice the median often corresponds to a
cutoff of about 80 %). No mask is created if both arguments are less
than zero.

=head1 ARGUMENTS

The following arguments are supported:

=over 4

=item B<BEST_PERCENT>

The number of bolometers expressed as a percentage of the total which
should be used to create the mask. Default is 50 (equivalent of using
the median). Will be ignored if less than zero (causing B<RELAX> to be
used instead).

=item B<RELAX>

A factor by which the NEP criteria can be relaxed to include
out-of-spec bolometers. A special value of 0 may be given which skips
the creation of a bad-bolo mask (i.e. continue to use all working
bolometers). Default is 1 (no relaxation). Will be ignored if less
than zero.

=item B<VERBOSE>

Flag to denote whether to display extra information. Default is 1
(yes).

=back

=head1 EXTERNAL TASKS

The following external tasks are used:

=over 4

=item KAPPA:

HISTAT, STATS, THRESH

=back

=head1 OUTPUT DATA

The output bad bolometer masks contain three new FITS headers:

=over 4

=item MASK_NEP

NEP value used to define mask

=item NBOLFLAT

Number of bolometers in NEP image (i.e. number of bolometers with
valid flatfield solutions)

=item NBOLMASK

Number of bolometers in the mask

=back

=head1 OUTPUT FILES

Creates an output file for each subarray present with suffix C<_bbm>,
which is stored in the calibration system.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2008-2010 University of British Columbia. All Rights
Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# First step: do noise data exist from which to create a bad-bolo mask?
if ( $Frm->uhdr("NOISE") ) {

  # Should we be verbose (default to yes)?
  my $verbose = get_prim_arg( $_PRIM_ARGS_, "VERBOSE", 1);

  # Are the NEP criteria being relaxed?
  my $relax = get_prim_arg( $_PRIM_ARGS_, "RELAX", "1.0" );

  # Alternatively, use the best N % of bolometers
  my $best_pc = get_prim_arg( $_PRIM_ARGS_, "BEST_PERCENT", 50);

  my $nep_spec;
  my $proceed = 0;
  my $usebest = 0;
  if ( $best_pc > 0 ) {

    # Override with recipe parameter if it exists
    $best_pc = $Frm->uhdr("RECPAR_BESTBOL_PERCENT")
      if (defined $Frm->uhdr("RECPAR_BESTBOL_PERCENT"));

    $proceed = 1;
    $usebest = 1;
    # Announce our presence to the world
    orac_print "Deriving a bad bolometer mask using best $best_pc % of bolometers" if ($verbose);
  } elsif ( $relax > 0 ) {
    $proceed = 1;
    # Catch numbers which look like sqrt(2)
    $relax = sqrt(2) if ($relax =~ /^1.4/);

    # Define NEP spec for defining the mask
    $nep_spec = $relax * $Cal->nep;

    # Announce our presence to the world
    if ($verbose) {
      my $factor = ($relax > 1) ? "with NEP criteria relaxed by ".(sprintf "%3.1f", $relax) : "";
      orac_print "Deriving a bad bolometer mask $factor\n";
    }
  }

  if ( $proceed ) {
    # Loop over the files in the Frame - there should be no more than
    # 4 files (future check), corresponding to each of the four
    # subarrays at the current wavelength.
    foreach my $i ( 1 .. $Frm->nfiles ) {

      # NEP file
      my $nepfile = $Frm->file( $i ).".MORE.SMURF.NEP";

      # Calculate the NEP value corresponding to the best percentage
      # specified - this is the nep_spec
      if ( $usebest ) {
	my $args = "ndf=$nepfile percentiles=$best_pc";
	$Mon{kappa_mon}->obeyw("histat","$args");
	(my $ORAC_STATUS, $nep_spec) = $Mon{kappa_mon}->get("histat","perval");
	orac_print " with NEP < ". (sprintf "%5.3e", $nep_spec). "\n" if ($verbose);
      }

      # Threshold to set out-of-range values to bad
      my $thresh = new ORAC::TempFile;
      my $threshfile = $thresh->file;
      my $args = "in=$nepfile out=$threshfile thrhi=$nep_spec thrlo=0 newhi=bad newlo=bad";
      $Mon{kappa_mon}->obeyw("thresh","$args");

      # Threshold again with inverted selection to set everything to
      # unity and hey presto - instant bad bolo mask
      # Define name of mask from name of current file
      my $mask = $Frm->inout("_bbm", $i);
      $args = "in=$threshfile out=$mask thrhi=0 thrlo=$nep_spec newlo=1";
      $Mon{kappa_mon}->obeyw("thresh","$args");

      # Find number of good bolometers in output mask
      $Mon{kappa_mon}->obeyw("stats","ndf=$mask");
      my ($ORAC_STATUS, $ngood) = $Mon{kappa_mon}->get("stats","numgood");
      if ( $ngood == 0 ) {
	orac_err "Bad bolometer mask has too few good bolometers ($ngood good)\n";
      } else {
	orac_print "Bad bolometer mask $mask created from $ngood bolometers\n"
	  if ($verbose);

	# Write some helpful FITS keywords:
	# 1) The NEP spec used to create the mask - MASK_NEP
	$args = "ndf=$mask edit=write keyword=MASK_NEP value=$nep_spec mode=interface comment='[W Hz**-0.5] NEP value used to define mask' position=!";
	$Mon{ndfpack_mon}->obeyw("fitsmod","$args");
	# 2) Number of bolometers in mask - NBOLMASK
	$args = "ndf=$mask edit=write keyword=NBOLMASK value=$ngood mode=interface comment='Number of bolometers in mask' position=!";
	$Mon{ndfpack_mon}->obeyw("fitsmod","$args");
	# 3) Number of good bolometers from flatfield - NBOLFLAT
	$Mon{kappa_mon}->obeyw("stats","ndf=$nepfile");
	($ORAC_STATUS, my $nbol) = $Mon{kappa_mon}->get("stats","numgood");
	$args = "ndf=$mask edit=write keyword=NBOLFLAT value=$nbol mode=interface comment='Number of valid bolometers from flatfield' position=!";
	$Mon{ndfpack_mon}->obeyw("fitsmod","$args");

	# Register with the Calibration system - note that cal system must
	# deal with the different subarrays. The easiest way is to add a
	# uhdr entry for SUBARRAY and put the right value in it.
	$Frm->subarray($i);
	$Cal->maskindex->add($mask, {%{$Frm->hdr}, %{$Frm->uhdr}});
      }
    }
  } else {
    orac_warn "Skipping creation of bad-bolometer mask\n" if ($verbose);
  }
}
