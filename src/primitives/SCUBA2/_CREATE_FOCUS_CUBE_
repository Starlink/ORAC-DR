# -*-cperl-*-

=head1 NAME

_CREATE_FOCUS_CUBE_ - Create a cube suitable for analysis by the JCMT POINTING_FOCUS task

=head1 SYNOPSIS

 _CREATE_FOCUS_CUBE_

=head1 DESCRIPTION

This primitive create a 3-D data cube for analysis by the
POINTING_FOCUS task to determine the FOCUS offsets for the current SMU
axis. The cube has axes of Azimuth, Elevation and SMU offset in mm.

The primitive calculates the mean number of images per focus position,
and only creates the cube when the number of images for the final
focus position equals that mean value.

The sub-group mosaics created for each focus position (C<FOCPOSN>)
have a third axis added before they are combined into a cube. A new
WCS component is created based on the existing SKY frame with the
addition of a mapping from grid to mm offset for the third axis.

The image corresponding to each focus position is displayed in a
Kapview window.

Once the cube has been constructed, the primitive writes the name of
the file into a flag file which the POINTING_FOCUS task reads before
calculating the FOCUS offset.

=head1 ARGUMENTS

The following argument is supported:

=over 4

=item B<VERBOSE>

Flag to indicate whether informational messages should be given to the
user. Default is 1 (true).

=back

=head1 EXTERNAL TASKS

The following external tasks are used:

=over 4

=item ATOOLS:

ASTADDFRAME, ASTCMPFRAME, ASTCMPMAP, ASTFRAME, ASTFRAMESET,
ASTGETFRAME, ASTGETMAPPING, ASTWINMAP

=item CCDPACK:

MAKEMOS

=item KAPPA:

NDFCOPY, NDFTRACE, PASTE, SETBOUND, SETORIGIN, STATS, WCSATTRIB,
WCSSLIDE

=item ORAC-DR PRIMITIVES:

_CROP_IMAGE_ALL_, _FIND_MAP_PEAK_, _WRITE_FLAG_FILE_

=back

=head1 NOTES

None.

=head1 OUTPUT DATA

The list of focus settings is stored in the C<FOCPOSNS> Group uhdr
entry.

The Group contains the name of the cube, while the product and current
tag are set to C<focus_cube>.

=head1 OUTPUT FILES

The cube has suffix C<_foc>.

This primitive writes a .ok file in $ORAC_DATA_OUT for the JCMT
POINTING_FOCUS task to calculate the actual pointing offsets used by
the telescope.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2007-2008,2012,2014 the University of British Columbia. All
Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# Should I be verbose? (Default to yes)
my $verbose = get_prim_arg($_PRIM_ARGS_, "VERBOSE", 1);

# Store Frame entry settings should they need to be reset
$Frm->tagset("create_focus_cube");
my $frmproduct = $Frm->product;

# Get current focus position
my $curfocposn = $Frm->hdrval("FOCPOSN");

# Work out how many focus positions we have actually got, store
# results as hash reference. Note that as the Grp gets larger, the
# memberhdrvals method becomes too slow to use in QL mode which is why
# we are counting.
my @focposns;
if ( $Grp->uhdr("FOCPOSNS") ) {
  @focposns = @{ $Grp->uhdr("FOCPOSNS") };
  # Flag for determining whether we have a new focus position
  my $new = 1;
  foreach my $focposn ( @focposns ) {
    if ( $curfocposn == $focposn ) {
      $new = 0;
    }
  }
  # If NEW is still set, then we have a new FOCPOSN to add to the array
  if ($new) {
    push (@focposns, $curfocposn);
    $Grp->uhdr("FOCPOSNS", \@focposns);
  }
} else {
  push (@focposns, $curfocposn);
  $Grp->uhdr("FOCPOSNS", \@focposns);
}

# Determine the mean number of Frames per focus position. Count the
# number of times this primitive is entered, and store the results in
# a hash (reference) indexed by the focus position. The mean is
# calculated every time.
my %focusnum;
if ( $Grp->uhdr("FOCUSNUM") ) {
  %focusnum = %{ $Grp->uhdr("FOCUSNUM") };
  if ( defined $focusnum{$curfocposn} ) {
    $focusnum{$curfocposn}++;
  } else {
    $focusnum{$curfocposn} = 1;
  }

  # Now calculate the mean number of focus images *excluding* the current
  my %prevfocus = %focusnum;
  delete $prevfocus{$curfocposn};
  my @numbers = values %prevfocus;
  use Statistics::Descriptive;
  my $stats = Statistics::Descriptive::Sparse->new();
  $stats->add_data( @numbers );
  my $mean = $stats->mean;
  if (defined $mean) {
    # Convert to an integer, round down
    $Grp->uhdr("FOCMEANUM",int($mean));
    # Store hash reference for next time
    $Grp->uhdr("FOCUSNUM",\%focusnum);
  }
} else {
  # Enter first key/value pair in hash and store reference in uhdr
  $focusnum{$curfocposn} = 1;
  $Grp->uhdr("FOCUSNUM",\%focusnum);
}

# Flag to indicate if we have a FOCUS observation - skip sequences
# that consist solely of non-focus data (eg fastflats)
my $focusobs = ( lc($Frm->uhdr("ORAC_OBSERVATION_TYPE")) eq "focus" ) ? 1 : 0;
$focusobs = 0 if (defined $Frm->hdr("SEQ_TYPE") && $Frm->hdr("SEQ_TYPE") !~ /focus/i);
# However, what happens if we get to the end of the obs and no cube
# has been created? Just try creating a cube with whatever data exist.
if ($Frm->uhdr("OBSEND") && !$Grp->product) {
  if ($Cal->isplanet($Frm->hdr("OBJECT"))) {
    orac_print "\nObservation has ended with no cube created - creating one now\n";
    $focusobs = 1;
    # In the QL pipeline, if the observation has ended with no cube
    # created, the current Frame will contain fastflat data which will
    # have different a header than image-processing primitives expect
    # so use the first "good" member of the Group as a template
    # instead
    if ($Frm->uhdr("QUICKLOOK") && $Frm->hdr("SEQ_TYPE") =~ /fastflat/i) {
      $Frm = ($Grp->members)[0];
    }
  }
}

# Make use of the fact that the SMU is stepped an equal number of
# positions either side of a nominal zero point (0.0 mm). Note this
# primitive will still fail to produce a cube if the last SMU position
# is skipped in the QL/SUMMIT pipelines but hopefully that will be a
# rarer event than skipping any one of the other positions.
my $lastpos = 0.5 * ($Frm->hdr("NFOCSTEP") - 1.0) * $Frm->hdr("FOCSTEP");
my $is_lastpos = (abs($lastpos - $curfocposn) < 1e-5 || $Frm->uhdr("OBSEND")) ? 1 : 0;

# Check if we are on the last focus position and whether we have
# enough data to create the Focus cube, by setting the OBSEND flag to
# true.
if ( scalar(@focposns) == $Frm->hdr("NFOCSTEP") || $is_lastpos ) {
  my $curnum = $focusnum{$curfocposn};
  my $mean = $Grp->uhdr("FOCMEANUM");
  if ( defined $mean && $curnum >= $mean ) {
    $Frm->uhdr("OBSEND", 1);
  } else {
    $Frm->uhdr("OBSEND", 0);
  }
}

my $product = "focus_cube";
# Define a flag which is true if a focus cube does not exist
my $nocubeproduct = ((defined $Grp->product && $Grp->product ne $product) ||
                     !$Grp->product) ? 1 : 0;

# Do we have a completed FOCUS observation, and does the cube already
# exist? Other checks: should be a standard source
if ( $focusobs && $Frm->uhdr("OBSEND") && $nocubeproduct) {
  orac_print "Enough data collected - creating FOCUS cube\n", 'green';
  # Which axis are we dealing with?
  my $axis = $Frm->hdr("FOCAXIS");

  # Sort focus positions from low to high
  @focposns = sort { $a <=> $b } @focposns;

  # Store this list for subsequent use by _FIND_FOCUS_OFFSETS_
  $Grp->uhdr("FOCPOSNS",\@focposns);
  # Create lookup table for focus position and plane file extension
  my $nfoc = scalar( @focposns );
  my %focposidx = map { $focposns[$_] => "_foc".($_+1) } 0..$#focposns;

  # Create the cube:
  # 1) Retrieve SKY mapping/frame for later use
  # 2) Add a third axis to each plane image, shift the pixel origin of
  #    that axis to correspond to the file extension
  # 3) Define name (add _foc at end)
  # 4) Paste the individual images together to make the cube
  # 5) Create new 3-D WCS frameset for cube using stored SKY mapping &
  #    frame
  # 6) Shift the WCS so source in middle focus position lies at 0,0
  # 7) Write .ok file for the POINTING_FOCUS task to pick up

  # Group all the frames by FOCPOSN (which is guaranteed to be distinct
  # in each Frm)
  my @unsortedsubgrps = $Grp->subgrps( "FOCPOSN" );

  # The subgrps array must be sorted by FOCPOSN
  my @subgrps;
  for my $i ( 0 .. $#focposns ) {
    foreach my $subgrp ( @unsortedsubgrps ) {
      if ( @{ $subgrp->members } ) {
       push ( @subgrps, $subgrp)
         if ($subgrp->members->[0]->hdrval("FOCPOSN") == $focposns[$i]);
      }
    }
  }

  # Check that it was possible to create the subgroups - if not then
  # all data are bad and the pipeline should exit immediately
  unless (@subgrps) {
    _WRITE_FLAG_FILE_ FILE=none
    orac_err "********************************************************\n";
    orac_err "**** FOCUS observation failed:                      ****\n";
    orac_err "**** Unable to create focus cube - all data are bad ****\n";
    orac_err "**** Writing dummy flag file to avoid timeouts      ****\n";
    orac_termerr "********************************************************\n";
  }

  # Loop over each Frame to create a Frame mosaic
  my %planes;
  my $args;
  my $i = 0;
  my $obsnum = $Frm->hdr("OBSNUM");
  foreach my $subgrp ( @subgrps ) {
    $i++;
    # Select files from the members that contain images
    my @focfiles = map { $_->files } grep {$_->tagexists("newimage")} $subgrp->members;

    my $focposn = $subgrp->members->[0]->hdrval("FOCPOSN");
    orac_print "Processing focus position $axis = ".(sprintf "%5.2f", $focposn)." mm (offset #$i)\n"
      if $verbose;

    # Create single image for the current focus plane
    my $focfile;
    if ( @focfiles > 1 ) {
      $focfile = $Frm->inout($focposidx{$focposn});
      my $moslist = write_file_list( @focfiles );
      my $mosargs = "in=^$moslist genvar=! variance method=nearest out=$focfile ref=! lbnd=! ubnd=!";
      $Mon{kappa_mon}->obeyw("wcsmosaic","$mosargs");
    } else {
      $focfile = $focfiles[0];
    }

    # Store output file in current subgrp
    $subgrp->files( $focfile );

    # Store image name for pasting into the cube
    $planes{$focposn} = $focfile;
    $subgrp->uhdr("FOCPOSN",$focposn);
  }

  # Find the peak position of the centre focus step (presumed to be
  # nearest the actual focus). The number of SMU positions should be
  # odd but make it an int just in case...
  my $midplane = int((scalar(@focposns) + 1) / 2) - 1;
  my $midfile = $planes{$focposns[$midplane]};
  if (!$midfile) {
    # No midfile means that the centre SMU position was skipped by the
    # pipeline - see if the neighbouring files are defined instead
    if (defined $planes{$focposns[$midplane+1]}) {
      $midfile = $planes{$focposns[$midplane+1]};
    } elsif (defined $planes{$focposns[$midplane-1]}) {
      $midfile = $planes{$focposns[$midplane-1]};
    }
  }
  my @newpos;
  if ($midfile) {
    $Frm->files($midfile);
    $Frm->tagset("midplane");
    $Frm->product("midplane");
    # Find the peak position just in case the source is way off centre
    # and adjust the WCS accordingly
    my $initpos = [0,0];
    _FIND_MAP_PEAK_ PLOTFIT=0 FITPEAK=1 INITPOS=$initpos VERBOSE=0 FITBOX=19
    # Proceed with shift if the peak could be determined with S/N > 3
    my $SNR_MIN = 3.0;
    if (defined $_FIND_MAP_PEAK_{PEAK} &&
        (defined $_FIND_MAP_PEAK_{SNR} && $_FIND_MAP_PEAK_{SNR} > $SNR_MIN)) {
      @newpos = ($_FIND_MAP_PEAK_{POSITION}->[0], $_FIND_MAP_PEAK_{POSITION}->[1]);
    } else {
      orac_warn "Unable to locate a significant maximum - WCS will not be adjusted\n";
    }
  }

  orac_print "Adjusting WCS to put source at image centre...\n"
    if ($verbose && @newpos);
  my @crop_planes;
  my $plane_index = 0;
  # Now shift each plane to a new (0,0) if necessary and set them up
  # with an SMU axis to add to the cube
  foreach my $subgrp (@subgrps) {
    $plane_index++;
    my $focposn = $subgrp->uhdr("FOCPOSN");
    my $plane = $subgrp->file;
    # Set to offset coordinate system
    $args = "ndf=$plane mode=mset setting='skyrefis=origin,alignoffset=1'";
    $Mon{ndfpack_mon}->obeyw("wcsattrib","$args");

    if (@newpos && ($newpos[0] ne "0" || $newpos[1] ne "0") ) {
      my $args = "stype=relative fid='0.0,0.0,0' obj='".
        $newpos[0].",".$newpos[1].",0' ndf=$plane";
      $Mon{kappa_mon}->obeyw("wcsslide","$args");
    }
    $Frm->files($plane);
    $Frm->readhdr;

    _CROP_IMAGE_ALL_ HEIGHT=120 WIDTH=120 VERBOSE=0
    # Set the NDF title string
    my $title = $focposidx{$focposn};
    $title =~ s/\_foc//;        # remove underscores
    my $obs = $obsnum;
    if ($plane_index == 1) {
      my $wave = ($Frm->hdr("FILTER") =~ /850/) ? "850 um" : "450 um";
      if ($Frm->uhdr("QUICKLOOK")) {
        $obs .= " ($wave QL)";
      } elsif ($Frm->uhdr("SUMMIT")) {
        $obs .= " ($wave summit)";
      }
    }

    $title = "'Foc".$title." $axis=$focposn mm / Obs $obs'";

    # Define output file based on Group file name
    my $planeimage = $Grp->inout( $focposidx{$focposn} );

    my $cpargs = "in=".$Frm->file." out=$planeimage title=$title";
    $Mon{ndfpack_mon}->obeyw("ndfcopy","$cpargs trim=true trimwcs=true");

    # Derive sky mapping and frame and store for later use
    unless ($Grp->uhdr("MAPPING")) {
      $args = "this=$planeimage iframe1=ast__base iframe2=ast__current result=skymap$$.ast";
      $Mon{atools_mon}->obeyw("astgetmapping","$args");
      $args = "this=$planeimage iframe=ast__current result=skyframe$$.ast";
      $Mon{atools_mon}->obeyw("astgetframe","$args");
      $Grp->uhdr("MAPPING",1);
    }

    # Retrieve coords of first pixel
    $Mon{ndfpack_mon}->obeyw("ndftrace","ndf=$planeimage");
    my ($ORAC_STATUS, @xy) = $Mon{ndfpack_mon}->get("ndftrace","lbound");
    # Now add a 1-pixel-wide third axis
    $args = "ndf=$planeimage(,,1:1)";
    $Mon{ndfpack_mon}->obeyw("setbound","$args");
    # Set the origin accordingly
    $args = "ndf=$planeimage origin=[$xy[0],$xy[1],$plane_index]";
    $Mon{ndfpack_mon}->obeyw("setorigin","$args");

    # Set the format to display arcsec offsets
    my $newformat = "s.0";
    my $args = "ndf=$planeimage mode=mset setting='Format(1)=$newformat,Format(2)=$newformat'";
    $Mon{ndfpack_mon}->obeyw("wcsattrib","$args");

    push(@crop_planes, $planeimage);
    $subgrp->files($planeimage);

    # Restore current Frame to original state since we have been
    # storing files in it
    $Frm->tagretrieve("create_focus_cube");
  }

  $Grp->files(@crop_planes);
  $Grp->tagset("focus_images");

  # Check that we can make at least 3 planes on a cube to fit a quadratic
  orac_warn "Number of SMU positions ($nfoc) is not sufficient to create a valid FOCUS cube\n"
    if ( $nfoc < 3 );

  # Define output cube name
  my $cubename = $Grp->inout("_foc");

  # Combine individual FOCPOSN mosaics into a 3-D cube
  orac_print "Creating FOCUS cube..." if $verbose;
  my $inlist = write_file_list( @crop_planes );
  $args = "in=^".$inlist." p1=! out=$cubename title='FOCUS cube'";
  $Mon{kappa_mon}->obeyw("paste","$args");
  undef $inlist;

  # Now create suitable 3-D frameset to ensure 3rd axis is
  # offset in millimetres. Method mirrors that in
  # _CREATE_CUBE_WCS_ First create a FRAME for the focus offsets
  orac_print " adding WCS... " if $verbose;
  $args = "naxes=1 result=focusframe$$.ast options='Title=SMU offset on $axis axis,Label=SMU offset,Unit=mm,Domain=SMU'";
  $Mon{atools_mon}->obeyw("astframe","$args");
  # Combine this FRAME with the above SKYFRAME to create a compound frame, CMPFRAME
  $args = "frame1=skyframe$$.ast frame2=focusframe$$.ast options=! result=cmpframe$$.ast";
  $Mon{atools_mon}->obeyw("astcmpframe","$args");
  # Define a WINMAP to describe the third axis
  my $focmin = $focposns[0];
  my $focmax = $focposns[-1];
  $args = "ncoord=1 ina=1 inb=$nfoc outa=$focmin outb=$focmax options=! result=focusmap$$.ast";
  $Mon{atools_mon}->obeyw("astwinmap","$args");
  # Combine this WINMAP with the above SKYMAP to create a compound mapping, CMPMAP
  $args = "map1=skymap$$.ast map2=focusmap$$.ast series=f options=! result=cmpmapping$$.ast";
  $Mon{atools_mon}->obeyw("astcmpmap","$args");
  # Create an empty 3-D GRID frame
  $args = "naxes=3 options='Domain=GRID' result=grid$$.ast";
  $Mon{atools_mon}->obeyw("astframe","$args");
  # Add it to a skeleton FRAMESET
  $args = "frame=grid$$.ast options=! result=newwcs$$.ast";
  $Mon{atools_mon}->obeyw("astframeset","$args");
  # Add the CMPFRAME to the FRAMESET using the CMPMAP to tie it
  # to the GRID frame. Store as WCS in the output cube.
  $args = "this=newwcs$$.ast iframe=ast__base map=cmpmapping$$.ast frame=cmpframe$$.ast result=$cubename";
  $Mon{atools_mon}->obeyw("astaddframe","$args");

  # Remove all of the .ast text files with the mapping/frame info
  unlink glob( "*$$.ast" );

  # Store the cube name as the Grp file and update product and tags
  $Grp->force_product_update($product, $cubename);
  $Grp->tagset($product);

  orac_print "\n";
  orac_print "Cube created successfully and ready for processing\n", 'green';

  # Write the .ok file in the output directory
  _WRITE_FLAG_FILE_ FILE=$cubename

  # Now display the data
  my ($min, $max) = (1.0e10, -1.0e10);
  # Find the stats in a 25 pixel-square box at the image centre,
  # which should include the source if detected
  orac_print "Finding min/max for displaying focus positions\n" if $verbose;
  foreach my $plane ($Grp->files) {
    my $args = "$plane(0:00:00.0~25,0:00:00.0~25,)";
    $Mon{kappa_mon}->obeyw("stats","$args");
    my ($ORAC_STATUS, $planemin) = $Mon{kappa_mon}->get("stats","minimum");
    ($ORAC_STATUS, my $planemax) = $Mon{kappa_mon}->get("stats","maximum");
    $min = $planemin if ($planemin < $min);
    $max = $planemax if ($planemax > $max);
  }
  my %options = ( ZAUTOSCALE => 0, ZMIN => $min, ZMAX => $max, WINDOW => 1 );
  use NDF;
  if ( $min == 1.0e-10 || $min == &NDF::VAL__BADD() ||
       $max == 1.0e10  || $max == &NDF::VAL__BADD() ) {
    orac_warn "Could not determine min/max for images in cube - images will be autoscaled\n";
    %options = (ZAUTOSCALE => 1);
  }
  # For some reason we need to loop over the subgrps and display each
  # in turn, rather than give the display system everything at
  # once. This should be investigated.
  foreach my $subgrp (@subgrps) {
    $Display->display_data( $subgrp, \%options, -1 );
  }

  unless ($Frm->uhdr('QUICKLOOK')) {
    _CREATE_CUBE_GRID_GRAPHIC_ GROUP=1
  }

  # Set Frame back to input status - this is probably not necessary as
  # Frame processing is complete
  $Frm->tagretrieve("create_focus_cube");
  $Frm->product($frmproduct) if ($frmproduct);
}
