=head1 NAME

_ADD_FCF_LOG_ENTRY_ - Write an FCF log entry to disk

=head1 SYNOPSIS

 _ADD_FCF_LOG_ENTRY_ FILTER=450W FCF=52.5 UNITS=ARCSEC

=head1 DESCRIPTION

Write a single log entry containing the results of an
Flux Conversion Factor calculation.

=head1 ARGUMENTS

=over 4

=item GROUP

Whether the group file or frame file has been used.

=item FCF

The Flux conversion factor to write to the log file.

=item UNITS

The units of the FCF. This can be either ARCSEC (for jansky per
square arcseconds) or BEAM (jansky per beam).

=back

=head1 NOTES

=over 4

=item *

FCF must be supplied to this primitive (since that
is the only way to pass the result into the primitive).

=item *

One entry is written each time this primitive is called. If multiple
files are to be processed, this primitive should be called multiple
times.

=item *

Obtains header values from the current frame.

=back

=head1 FORMAT

The logfile has the following columns:

=over 4

=item UT

The UT date in C<YYYYMMDD.frac> format.

=item HST

The time of the observation in Hawaii.

=item Obs

The observation number.

=item Source

The source name.

=item Mode

The observing mode.

=item Filter

The specific filter associated with the fcf result.

=item Elevation

The elevation of the observation.

=item CSO

Equivalent CSO tau derived from the tau used to reduce
the observation.

=item Tau

The tau value used for the reduction.

=item Seeing

The seeing value as stored in the header.

=item FCF

The flux conversion factor

=item FCF Units

The units of the FCF. Can be BEAM (Jy/beam/V) or ARCSEC
(Jy/arcsec**2/V).

=back

=head1 FILES

C<log.fcf> - the log file

=head1 AUTHOR

Tim Jenness E<lt>t.jenness@jach.hawaii.eduE<gt>
Elese Archibald E<lt>e.archibald@jach.hawaii.eduE<gt>

Copyright (C) 2008 Science and Technology Facilities Council.
Copyright (C) 2000 Particle Physics and Astronomy Research Council.
All Rights Reserved.

=cut

# Check the arguments
my $fcf;
my $units;
foreach (qw/UNITS FCF/) {
  unless (exists $_PRIM_ARGS_->{$_}) {
    orac_throw("ADD_FCF_LOG_ENTRY: $_ must be specified as an argument");
  }
}

$units  = $_PRIM_ARGS_->{UNITS};
$fcf    = $_PRIM_ARGS_->{FCF};

my $Obj = ($_PRIM_ARGS_->{GROUP} ? $Grp : $Frm);

# Create the log file object
my $log = new ORAC::LogFile("log.fcf");
# Write header to logfile
my $time = gmtime();

$log->header("# Flux conversion factor log file - created on $time",
	     "\n# UT                HST   Obs Source       Mode         N_int Filter El CSO    Tau   Seeing   FCF    FCF_units",
	     "#---------------------------------------------------------------------------------------------------------------------------",
	    );

# Extract information from headers


# ORACUT
my $utdate = $Obj->hdr('ORACUT');
my $uttime = sprintf "%8.7f", $Obj->hdr('ORACTIME');

# HSTSTART (no decimal places)
my $hst    = $Obj->hdr('HSTSTART');
if (defined $hst) {
  my (@hst)  = split(/:/, $hst);
  $hst = sprintf '%5s', $hst[0] . ":" . $hst[1];
} else {
  my $ut = $Obj->uhdr('ORAC_UTSTART');
  $hst = sprintf('%02d:%02dUT', $ut->hour, $ut->min);
}

# RUN
my $run    = sprintf "%4d", $Obj->uhdr('ORAC_OBSERVATION_NUMBER');

# OBJECT
my $src    = substr((sprintf "%-12s", uc($Obj->uhdr('ORAC_OBJECT'))),0,12);

# MODE
my $mode = $Obj->uhdr("ORAC_OBSERVATION_MODE");
$mode = sprintf "%-11s", $mode;

# Filter System
my $filter= $Obj->uhdr("ORAC_FILTER");

# Average of start and end elevation
my $el     = ($Obj->uhdr('ORAC_ELEVATION_START') 
	      + $Obj->uhdr('ORAC_ELEVATION_END'))/ 2.0;
$el        = sprintf "%2.0f", $el;

# TAU from calibration object
my $tauval = $Cal->tau($filter);
print "Back from tau method\n";
my $tau = "*****";
$tau = sprintf "%5.2f", $tauval if defined $tauval;

# Use that tau to calculate CSO
my $cso = "*****";
if (defined $tauval) {
    my ($csoval, $status) = JCMT::Tau::get_tau('CSO',$filter, $tauval);
    $cso = sprintf "%5.3f", $csoval if ($status == 0 && defined $csoval) ;    
}

# Seeing
my $seeing = $Frm->hdr('SEEING');
my $see = "-----";
$see = sprintf "%5.2f", $seeing if defined $seeing;

# GAIN
$fcf   = sprintf "%8.3f", $fcf;

# Units
$units = sprintf "%-6s", $units;

# Add the entry
$log->addentry("$uttime $hst $run $src $mode   $filter $el $cso $tau $see  $fcf  $units");



