=head1 NAME

_FTS2_HELLO_ - FTS-2 introductory primitive

=head1 SYNOPSIS

 _FTS2_HELLO_

=head1 DESCRIPTION

This primitive should be called at the start of all FTS-2 recipes.

=head1 ARGUMENTS

None.

=head1 EXTERNAL TASKS

None.

=head1 OUTPUT DATA

None

=head1 OUTPUT FILES

None.

=head1 NOTES

None.

=head1 AUTHOR

Graham Bell E<lt>g.bell@jach.hawaii.eduE<gt>

=head1 COPYRIGHT

Copyright (C) 2012 Science and Technology Facilities Council.
All Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut


# Check that FTS-2 is actually in the beam.

my @inbeam = split ' ', $Frm->hdrval('INBEAM');

unless (grep {lc($_) eq 'fts2'} @inbeam) {
  orac_termerr('FTS-2 is not in the beam.');
}


# Read recipe parameters, but only warn about
# missing ZPD parameters if it's a ZPD recipe.
# Where there are different values for 450
# and 850 microns a [450, 850] array is given
# here and _450 and _850 suffices are added
# to the recipe parameter.

my %param = (
              FTS_STAGE_CENTER => 225.0,
              FTS_WN_LBOUND => [22.1, 11.2], # cm^-1
              FTS_WN_UBOUND => [23.3, 12.2], # cm^-1
              FTS_ZPD_ESTIMATE => 3.0,
              FTS_ZPD_TOLERANCE => 0.05,
              FTS_ZPD_BASE_OFFSET => [3, 6],
              FTS_ZPD_BASE_WIDTH => 3,
              FTS_ZPD_PEAK_HEIGHT => [0.2, 0.4],
              FTS_ZPD_PEAK_WIDTH => [0.03, 0.06],
            );

my $comp = $Frm->hdr('FILTER') =~ /850/ ? 1 : 0;

foreach my $key (keys %param) {
  my ($reckey, $defval) = ($key, $param{$key});

  if (ref $defval) {
    $defval = $defval->[$comp];
    $reckey .= [qw/_450 _850/]->[$comp];
  }

  if (exists $RECPARS{$reckey}) {
    $Frm->uhdr($key, $RECPARS{$reckey});
  }
  elsif (not ($key =~ /_ZPD_/ and $Frm->uhdr('ORAC_DR_RECIPE') !~ /_ZPD/)) {
    orac_warn('Assuming default value for ' . $key . ' : ' . $defval . "\n");
    $Frm->uhdr($key, $defval);
  }
}


# Determine which FTS-2 Tracking Port is being used.

my $instap_x = $Frm->hdr('INSTAP_X');

if ($instap_x > 0) {
  # Port 1.
  $Frm->uhdr('FTS_TRACKING_PORT', '8D');
  $Frm->uhdr('FTS_TRACKING_SUBARRAY', $comp ? 's8d' : 's4a');
  $Frm->uhdr('FTS_IMAGE_SUBARRAY', $comp ? 's8c' : 's4b');
}
else {
  # Port 2.
  $Frm->uhdr('FTS_TRACKING_PORT', '8C');
  $Frm->uhdr('FTS_TRACKING_SUBARRAY', $comp ? 's8c' : 's4b');
  $Frm->uhdr('FTS_IMAGE_SUBARRAY', $comp ? 's8d' : 's4a');
}
