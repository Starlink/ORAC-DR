# -*- perl -*-

# Podule
# ======

=head1 NAME

_CREATE_WCS_  -- Corrects the headers of UIST IFU data to enable creation of an AIPS-convention World Co-ordinate System.

=head1 DESCRIPTION

This primitive converts astrometric information in a UIST IFU frame into
an AIPS-convention World Co-ordinate System.  This is achieved by
editing the headers in the FITS airlock. The WCS is not created because
it is not currently possible to define a RA, dec, wavelength WCS system.
The WCS frame can be created on images extracted from the datacube. Axes
are created giving the offset from the centre of the fild of view in arcsec.

The changes are listed below.

a) Put decimal degrees of RABASE in CRVAL1, and DECBASE in CRVAL2.
The comment includes a sexagesimal version of the co-ordinate.

b) Change CRPIXI<n> to the centre of the frame or a supplied x-y
reference pixel.

c) Divide CDELTI<n> by 3600 to convert to degrees.  Make CDELT1
negative.

d) Change CTYPE1 to "RA---TAN", CTYPE2 to "DEC--TAN".

e) Revise the comments for the above to adhere to the UKIRT FITS
document, orac0016-fith, e.g. CRVAL1's comment becomes "[deg] RA
(hh:mm:ss.ss) at reference point" with the sexagesimal
position substituted.

=head1 ARGUMENTS

=over 4

=item XREF = REAL (Given)

The I<x> pixel index corresponding to the base equatorial position.
This defaults to the centre of the array along the RA axis for
sub-arrays, but 20 pixels into the west half for the full array.  If
supplied beyond the range 0 to 1024 or some non-numeric value
supplied, a warning is issued and the default adopted.  []

=item YREF = REAL (Given)

The I<y> pixel index corresponding to the base equatorial position.
This defaults to the centre of the array along the Declination axis
for sub-arrays, but 25 pixels into the south half for the full array.
If supplied beyond the range 0 to 1024, or some non-numeric value
supplied, a warning is issued and the default adopted.  []

=back

=head1 NOTES

=over 4

=item *

Editing only occurs for OBJECT and SKY frames, for which
there is no WCS.  This includes all editing steps.

=item *

The location of the reference pixel is the biggest source
of error, and may vary systematically with telescope operator.
Comparison of real data with the defaults suggests errors
are typically +/-2" in each axis.

=item *

Following a successful introduction of a WCS, the current
AST Frame is SKY.  Any existing WCS Frames besides the default
three---PIXEL, GRID, AXIS---are lost.

=back

=head1 OUTPUT DATA

=over 4

=item *

The modified NDF.

=back

=head1 TASKS

KAPPA: ERASE, FITSMOD, NDFTRACE, WCSFRAME.

=head1 AUTHORS

Malcolm J. Currie (JAC)
adapted for UIST IFU data by Stephen Todd <spt@roe.ac.uk>

=head1 COPYRIGHT

Copyright (C) 1998-2001 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut


# Do headers need editing?
# ========================

    my $instval = "UIST";

# Do not want to redo a frame already converted, otherwise the scale
# will erroneously be reduced each pass through this primitive.
    my $edited = (defined($Frm->uhdr("WCS_ADDED")) && $Frm->uhdr("WCS_ADDED") eq "1");

# Modify the headers should only be performed on OBJECT or SKY
# frames, which have not already been converted.

    my $correct_obstype = $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "OBJECT" ||
                          $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "SKY";
    if ( $correct_obstype && !$edited ) {

# Set the maximum reference pixel index.
       my $xmaxref = 14;
       my $ymaxref = 55;

       my $ndf = $Frm->file;

# Obtain the reference position.
# ==============================

# First see if the reference positions are supplied as arguments.
       my $xref = $_CREATE_WCS_{XREF};
       my $yref = $_CREATE_WCS_{YREF};

# Validate the x position.
       if ( defined( $xref ) ) {
          if ( $xref =~ /^-?(?:\d+(?:\.\d*)?|\.\d+)$/ ) {
             orac_warn "Programming error: x reference pixel '$xref' " .
                       "is not numeric.  Using default.\n";
             undef $xref;
          } elsif ( $xref < 0 || $xref > $xmaxref ) {
             orac_warn "Programming error: x reference pixel '$xref' " .
                       "is out of range.  Using default.\n";
             undef $xref;
          }
       }

# Validate the y position.
       if ( defined( $yref ) ) {
          if ( $yref =~ /^-?(?:\d+(?:\.\d*)?|\.\d+)$/ ) {
             orac_warn "Programming error: y reference pixel '$yref' " .
                       "is not numeric.  Using default.\n";
             undef $yref;
          } elsif ( $yref < 0 || $yref > $ymaxref ) {
             orac_warn "Programming error: y reference pixel '$yref' " .
                       "is out of range.  Using default.\n";
             undef $yref;
          }
       }

       if ( ! ( defined( $xref ) && defined( $yref ) ) ) {

# At least one was not given so define approximate positions with respect
# to the frame centre.

# Find the bounds of the NDF.
          my $ORAC_STATUS;
          $Mon{ "ndfpack_mon" }->obeyw( "ndftrace", "ndf=$ndf" );

# Extract the bounds of the frame.
          ( $ORAC_STATUS, my @lbnd ) = $Mon{ "ndfpack_mon" }->get( "ndftrace", "lbound" );
          ( $ORAC_STATUS, my @ubnd ) = $Mon{ "ndfpack_mon" }->get( "ndftrace", "ubound" );

# Find the mid points.
          my $xmid = nint( ( $lbnd[ 0 ] + $ubnd[ 0 ] ) / 2 );
          my $ymid = nint( ( $lbnd[ 1 ] + $ubnd[ 1 ] ) / 2 );

# Find the dimensions.
          my $xdim = $ubnd[ 0 ] - $lbnd[ 0 ] + 1;
          my $ydim = $ubnd[ 1 ] - $lbnd[ 1 ] + 1;

# Set an x reference pixel if it is not supplied as an argument.
          if ( ! defined( $xref ) ) {
	      $xref = $xmid;
	  }

# Repeat for y position.
          if ( ! defined( $yref ) ) {
	      $yref = $ymid;
	  }

      }

# Obtain headers.
# ===============

# First the reference equatorial position.
       my $rabase = $Frm->uhdr( "ORAC_RA_BASE" );
       my $decbase = $Frm->uhdr( "ORAC_DEC_BASE" );

# Obtain the plate scales.
       my $incrx = $Frm->uhdr( "ORAC_RA_SCALE" );
       my $incry = $Frm->uhdr( "ORAC_DEC_SCALE" );

       # Check whether the datacube has been expanded to equal spatial
       # resolution in x and y
       $incrx *= 2 unless ($Frm->uhdr("EXPANDED"));


# Indicate where to insert the FITS cards.  For UFTI, the WCS-related
# headers already exist so no new location is necessary.
       my $insert_card = " ";

# Convert co-ordinate units.
# ==========================

# Correct the increments from arcseconds to degrees.  Format
# them so header reading picks up the exponent.
#       $incrx /= -3600.0;
#       $incry /= 3600.0;
       my $fs_incrx = sprintf( "%14.6e", $incrx );
       my $fs_incry = sprintf( "%14.6e", $incry );

# Allow for some angles in degrees, at least those from 24 to 360.
# There was a period where the headers were wrong.
       if ( $rabase > 24 ) {
          $rabase /= 15.0;
       }

# Convert RA to Sexagesimal for the comment.
       my $rah = int( $rabase );
       my $rares = $rabase - $rah;
       my $ram = int( 60.0 * $rares );
       $rares -= $ram / 60.0;
       my $ras = sprintf( "%5.2f", 3600.0 * $rares );

# Convert Declination to Sexagesimal for the comment.
       my $decsign = "+";
       if ( $decbase < 0 ) {
          $decsign = "-";
       }
       my $decres = abs( $decbase );
       my $decd = int( $decres );
       $decres = $decres - $decd;
       my $decm = int( 60.0 * $decres );
       $decres -= $decm / 60.0;
       my $decs = sprintf( "%4.1f", 3600.0 * $decres );

# Convert RA from hours to degrees.
       $rabase *= 15.0;
       my $fs_rabase = sprintf( "%14.6e", $rabase );


# Get the angle of rotation from the image rotator
# UIST SLIT_PA is CGS4 SLIT_PA + 90 degrees (degrees S of E). We put the
# slices together in the order that gives us conventionally arranged NESW
#   N                                                 N
# E   W  with rotation, whereas UIST imaging has    W   E
#   S                                                 S
# so our reconstructed images are rotated in the opposite direction by
# increasing PA, so our rotation angle for the WCS is 360-(SLIT_PA+90)

       my $rot = 360 - $Frm->hdr("SLIT_PA") + 90.0;


# Create the FITSMOD steering file.
# =================================

       my $table = "fitsmod$$.lis";
       unlink( $table );

# Open the steering file of editing instructions.  Set the command to
# update as the headers exist.
       open ( my $fh_steering, ">$table" ) ||
         orac_throw "Unable to open $table of WCS editing instructions.  Error: $!.\n";
       my $cmd = "U";

       print $fh_steering "$cmd CTYPE1$insert_card 'RA---TAN' Equatorial tangent-plane projection\n";
       print $fh_steering "$cmd CRPIX1$insert_card $xref Pixel at reference point along axis 1\n";
       print $fh_steering "$cmd CRVAL1$insert_card $fs_rabase [deg] RA ($rah:$ram:$ras) at reference point\n";
       print $fh_steering "$cmd CDELT1$insert_card $fs_incrx [deg] Increment per pixel at reference point\n";
       print $fh_steering "$cmd CUNIT1$insert_card 'deg' Physical units of axis 1\n";
       print $fh_steering "$cmd CTYPE2$insert_card 'DEC--TAN' Equatorial tangent-plane projection\n";
       print $fh_steering "$cmd CRPIX2$insert_card $yref Pixel at reference point along axis 2\n";
       print $fh_steering "$cmd CRVAL2$insert_card $decbase [deg] Dec ($decsign$decd:$decm:$decs) at reference point\n";
       print $fh_steering "$cmd CDELT2$insert_card $fs_incry [deg] Increment per pixel at reference point\n";
       print $fh_steering "$cmd CUNIT2$insert_card 'deg' Physical units of axis 2\n";
       print $fh_steering "W CROTA2$insert_card $rot [deg] Angle declination axis wrt axis 2 ccw\n";

       print $fh_steering "U INSTRUME $instval \$C\n" unless $instval eq " ";
       close( $fh_steering );

# Edit the headers.
       $Mon{ "ndfpack_mon" }->obeyw( "fitsmod", "ndf=$ndf mode=file table=$table" );
       unlink( $table );

       orac_print "AIPS-convention world co-ordinate system added " .
                  "to headers of $ndf.\n   Reference pixel is ($xref,$yref).\n";

# Remove an existing WCS component, so that later when a new WCS is made,
# it will include a SKY co-ordinate frame.  As there may not already be a
# WCS component, make one with wcsframe.
#       $Mon{ "ndfpack_mon" }->obeyw( "wcsframe", "ndf=$ndf frame=pixel" );
#       $Mon{ "ndfpack_mon" }->obeyw( "erase", "object=${ndf}.WCS ok" );

# Create the SKY frame in the WCS component.
#       $Mon{ "ndfpack_mon" }->obeyw( "wcsframe", "ndf=$ndf frame=sky" );
#       orac_print "Astrometry SKY co-ordinate frame created in $ndf.\n";
#       orac_print "\n";


       $incrx *= -3600;
       $incry *= 3600;

# Create axis structures
       $Mon{ "ndfpack_mon" }->obeyw( "setaxis", "ndf=$ndf dim=1 mode=expression exprs=$incrx*(INDEX-($xref))");

       $Mon{ "ndfpack_mon" }->obeyw( "setaxis", "ndf=$ndf dim=2 mode=expression exprs=$incry*(INDEX-($yref))");

# Update the internal header
       $Frm->uhdr("WCS_ADDED", 1);
   }

