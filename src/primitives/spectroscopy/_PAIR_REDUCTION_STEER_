=head1 NAME

_PAIR_REDUCTION_STEER_ - steering file for pair-based recipes.

=head1 DESCRIPTION

This primitive controls the overall state of a recipe involving
pair subtraction. It currently determines whether we are
an OBJECT or a SKY and whether we are the first or second observation
of a pair. This information is required by many of the group primitives
to determine whether or not to act.

=head1 OUTPUT

Stores information in the user header of the current frame
with the following keys:

=over 4

=item PAIR_COMPLETE

True if the frame is the second of a pair. False otherwise.

=item ON_SOURCE

True if we are on the object. False if we are on sky.

=back

=head1 NOTES

=over 4

=item *

Incompatible with the C<-batch> option in ORAC-DR since this primitive
uses the number of frames in the group to determine state.

=item *

The test to see if the observation is either OBJECT or SKY
does not rely on the offset being exactly zero so as to allow for
machine floating point and string conversion truncation etc.
If it is less than 1 milli-arcsec, we assume it is zero.

=item *

Determines that a pair is complete by counting the number of members in
the ORAC-DR group. This will be different from the number of members
in the CGS4 group if the pipeline is restarted. It is
assumed that the pipeline is restarted at the start of a pair
(and the pipeline will fail if this is not the case).
Additionally, the total number of observations in the group so far
(regardless of the ORAC-DR group) is also calculated to determine
the state of the pair. If the two answers are different the recipe
aborts since this is indicative of the pipeline restarting midway
through a pair reduction.

=item *

The pipeline can not be started from the second observation of a pair
since ORAC-DR will have no knowledge of the previous frame.

=item *

When a frame is determined to be a SKY the offset information
(the FITS headers) are stored in the Group object (via the uhdr())
so that subsequent primitives can use this information to determine
the position of the negative row. This is only used if the extraction
is done without reducing a standard and is used to guarantee that
the off position can be determined regardless of the order of the
sky-object pair. The FITS headers stored are C<RAOFF>, C<DECOFF>,
C<RABASE>, C<DECBASE>, C<MEANRA>, C<MEANDEC> and are prefixed by
C<SKY_> when stored in the user header.

=back

=head2 Revision

 $Id$

=head1 AUTHOR

Tim Jenness E<lt>t.jenness@jach.hawaii.eduE<gt>
Paul Hirst <p.hirst@jach.hawaii.edu>

=head1 COPYRIGHT

Copyright (C) 1998-2003 Particle Physics and Astronomy Research
Council. All Rights Reserved.

=cut


# Retrieve the number of frames currently in the group.  This assumes
# no batch mode.
my $num = $Grp->num();

# Set the PAIR_COMPLETE steering header.
if ( ($num+1) % 2 == 1 ) {
   $Frm->uhdr( "PAIR_COMPLETE", 0 );
   orac_print "Steerage: This observation is the start of a pair.\n";
} else {
   $Frm->uhdr( "PAIR_COMPLETE", 1 );
   orac_print "Steerage: This observation completes a pair.\n";
}

# Obtain the telescope offsets.
my $raoff = $Frm->uhdr("ORAC_RA_TELESCOPE_OFFSET");
my $decoff= $Frm->uhdr("ORAC_DEC_TELESCOPE_OFFSET");

# Set or get the base-shift - ie the main beam position
_SET_BEAMOFFSETS_

my $rabo = $Frm->uhdr("RA_BEAMOFFSET");
my $decbo = $Frm->uhdr("DEC_BEAMOFFSET");

$raoff -= $rabo;
$decoff -= $decbo;

# Now calculate whether we are an object or a sky observation.
# This is achieved by determining if there is an offset, i.e.
# the telescope moved.  Allow for rounding and type conversion
# (string <-> numeric) to 1 milli-arcsec level.

if ( ( abs $raoff < 0.001 ) && ( abs $decoff < 0.001 ) ) {
   $Frm->uhdr( "ON_SOURCE", 1 );
   orac_print "Steerage: This is an OBJECT observation: ($raoff, $decoff) from ($rabo, $decbo)\n";
} else {
   $Frm->uhdr( "ON_SOURCE", 0 );
   orac_print "Steerage: This is a SKY observation: ($raoff, $decoff) from ($rabo, $decbo)\n";

   # Store the offset information in the group user header
   $Grp->uhdr( "SKY_RAOFF",   $Frm->uhdr( "ORAC_RA_TELESCOPE_OFFSET" ) );
   $Grp->uhdr( "SKY_DECOFF",  $Frm->uhdr( "ORAC_DEC_TELESCOPE_OFFSET"  ) );

}
