#+
# Name:
#    _MASK_ENDS_OF_SLIT_ 
#
# Purpose:
#    Flags areas of the frame off the end of the slit as bad.
#
# Language:
#    Perl5
#
# Description:
#    This primitive marks as bad pixels those areas of the frame off
#    the end of the slit.  It collapses the frame along the axis parallel
#    to the slit, marks as bad rows which are illuminated less than a limit
#    defined in user header FLAT_MASK_THRESHOLD (normally 95%) or more
#    than 2.
#
# Notes:
#    -  This primitive is suitable for ISAAC in spectroscopy mode.
#    -  Processes all sub-files.
#    -  Each flagged image flat is displayed.
#
# Tasks:
#    KAPPA: COMPAVE, MULT, NDFTRACE, PIXDUPE, STATS, THRESH.
#
# Output Data:
#    The flagged images, inheriting the current frame's name
#    but with the _mnsf suffix.
#
# Authors:
#    PH: Paul Hirst (JAC)
#    MJC: Malcolm J. Currie (JAC)
#
# Copyright:
#    Copyright (C) 1998-2003 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# Loop through the sub-files.
    foreach my $i ( 1 .. $Frm->nfiles ) {

# Inherit the frame name merely changing the suffix.
       my ( $in, $out ) = $Frm->inout( "_msnf", $i );

# Require a temporary file to hold the spectrum.
       my $tmp = new ORAC::TempFile;
       my $tmpfile = $tmp->file;

# Determine the compression factor, being the x dimension.
       $Mon{ 'ndfpack_mon' }->obeyw( "ndftrace", "ndf=$in" );
       my $ORAC_STATUS;
       ( $ORAC_STATUS, my @dims ) = $Mon{ 'ndfpack_mon' }->get( "ndftrace", "dims" );   
       my $xdim = $dims[ 0 ];

# Collapse the frame into the temporary file.  Use COMPAVE rather than
# COLLAPSE as we want a two-dimensional (1xN) image, not a
# one-dimensional spectrum.  We also need to know the x size.
       my $param = "align=first compress=[$xdim,1]";
       $Mon{ 'kappa_mon' }->obeyw( "compave", "in=$in out=$tmpfile $param" );

# Get the median value in the spectrum.
       $Mon{ 'kappa_mon' }->obeyw( "stats", "ndf=$tmpfile clip=3" );
       ( $ORAC_STATUS, my $cmean ) = $Mon{ 'kappa_mon' }->get( "stats", "mean" );

# We need another temporary file.
       my $temp = new ORAC::TempFile;
       my $tempfile = $temp->file;

# The threshold is set into the uhdr by steering primitive.
       my $thr = $Frm->uhdr( "FLAT_MASK_THRESHOLD" );
       my $thrlo = $thr * $cmean;
       my $thrhi = 2 * $cmean;

# Apply the thresholds, setting values outside these ranges to be bad.
       $param = "thrlo=$thrlo thrhi=$thrhi newlo=bad newhi=bad";
       $Mon{ 'kappa_mon' }->obeyw( "thresh", "in=$tmpfile out=$tempfile $param" );

# Set all the good pixels to have value 1.
       $param = "thrlo=1 thrhi=1 newlo=1 newhi=1";
       $Mon{ 'kappa_mon' }->obeyw( "thresh", "in=$tempfile out=$tmpfile $param" );

# Set the variance in the mask to zero, since it is only a mask and
# carries no data information. 
       $Mon{ 'ndfpack_mon' }->obeyw( "setvar", "ndf=$tmpfile variance=0" );

# Expand the spectrum back into an image of the original shape.
       $Mon{ 'kappa_mon' }->obeyw( "pixdupe", "in=$tmpfile out=$tempfile expand=[$xdim,1]" );

# Multiply the input file by that image to get the output file
       $Mon{ 'kappa_mon' }->obeyw( "mult", "in1=$in in2=$tempfile out=$out title=$out" );

# Update the Frm object.
       $Frm->file( $out, $i );

# Inform the user of progress.
       orac_print "$in to $out: Masked off under-illuminated rows\n";
       orac_print "   using threshold: $thr\n";
    }

# Display the frame
   $Display->display_data( $Frm ) if defined $Display;

   orac_print "\n";

# Podule
# ======

=head1 NAME

_MASK_ENDS_OF_SLIT_ - Flags areas of the frame off the end of the slit as bad.

=head1 DESCRIPTION

This primitive marks as bad pixels those areas of the frame off
the end of the slit.  It collapses the frame along the axis parallel
to the slit, marks as bad rows which are illuminated less than a limit
defined in user header FLAT_MASK_THRESHOLD (normally 95%) or more
than 2.

=head1 NOTES

=over 4

=item *

This primitive is suitable for ISAAC in spectroscopy mode.

=item *

Processes all sub-files.

=item *

Each flagged image flat is displayed.

=back

=head1 TASKS

KAPPA: COMPAVE, MULT, NDFTRACE, PIXDUPE, STATS, THRESH.

=head1 OUTPUT DATA

The flagged images, inheriting the current frame's name
but with the _mnsf suffix.

=head1 AUTHOR

Paul Hirst <p.hirst@jach.hawaii.edu>
Malcolm J. Currie <mjc@star.rl.ac.uk>

=head1 COPYRIGHT

Copyright (C) 1998-2003 Particle Physics and Astronomy Research
Council. All Rights Reserved.

=cut
