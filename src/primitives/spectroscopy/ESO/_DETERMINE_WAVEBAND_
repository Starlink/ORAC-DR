#+
# Name:
#    _DETERMINE_WAVEBAND_
#
# Purpose:
#    Determine the waveband for flux calibration and blackbody calculations.
#
# Language:
#    Perl5
#
# Description:
#    This primitive uses information stored in the headers, primarily the
#    central wavelength, to determine the waveband for the current spectrum.
#    It writes the waveband and waveband central wavelength into the
#    WAVEBAND and WAVEBAND_CENTRE internal headers.

# Notes:
#    -  The waveband centre is from intenal header ORAC_GRATING_WAVELENGTH,
#    unless this is undefined, where a lookup table for the waveband
#    is used.
#
# Authors:
#    BC: Brad Cavanagh (JAC)
#    MJC: Malcolm J. Currie (Starlink)
#
# Copyright:
#    Copyright (C) 2004 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# Define the wavelength bands, the bounds measured in microns.
   my %bandedges = ( I => [0.7, 1.0],
                     J => [1.0, 1.4],
                     H => [1.4, 2.0],
                     K => [2.0, 2.4],
                     L => [2.4, 3.6],
                     Lprime => [3.6, 4.2],
                     M => [4.2, 5.5],
                     N => [8.0, 12.0],
                     Q => [20.0, 25.0],
                   );

# Define the band centres, in microns.
   my %bandcentres = ( V => 0.5556,
                       I => 0.88,
                       J => 1.25,
                       H => 1.65,
                       K => 2.20,
                       L => 3.45,
                       Lprime => 3.80,
                       M => 4.80,
                       N => 10.1,
                       Q => 22.5,
                       UNKNOWN => 'UNKNOWN',
                     );

# Obtain the central wavelength.
   my $wlen = $Frm->uhdr( "ORAC_GRATING_WAVELENGTH" );

# Determine to which band it belongs.
   my $wband = "";
   foreach my $band ( keys %bandedges ) {
      if ( ( $wlen > $bandedges{$band}[0] ) &&
           ( $wlen < $bandedges{$band}[1] ) ) {
         $wband = $band;
      }
   }

# Allow for an unrecognised band.
   if ( length( $wband . '' ) == 0 ) {
      $wband = "UNKNOWN";
   }

# ESO wavebands may be offset from the centre and can be narrow, so use the
# central wavelength to avoid the band centre lying outside the
# observed spectral range.
   my $bandcentre = $bandcentres{$wband};
   if ( defined( $wlen ) ) {
      $bandcentre = $wlen;
   }

# Set the internal headers.
   $Frm->uhdr( "WAVEBAND", $wband );
   $Frm->uhdr( "WAVEBAND_CENTRE", $bandcentre );

# Log the values.
   orac_print "Setting waveband to $wband.\n";
   orac_print "Setting waveband centre to $bandcentre microns.\n";

# Podule
# ======

=head1 NAME

_DETERMINE_WAVEBAND_ -- Determines the waveband for flux calibration and blackbody calculations.

=head1 DESCRIPTION

This primitive uses information stored in the headers, primarily the
central wavelength, to determine the waveband for the current spectrum.
It writes the waveband and waveband central wavelength into the
WAVEBAND and WAVEBAND_CENTRE internal headers.

=head1 NOTES

The waveband centre is from intenal header ORAC_GRATING_WAVELENGTH,
unless this is undefined, where a lookup table for the waveband
is used.

=head1 AUTHOR

Brad Cavanagh <b.cavanagh@jach.hawaii.edu>
Malcolm J. Currie <mjc@star.rl.ac.uk>

=head1 COPYRIGHT

Copyright (C) 2004 Particle Physics and Astronomy Research Council.
All Rights Reserved.

=cut
