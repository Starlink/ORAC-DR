# -*-perl-*-

=head1 NAME

_CALCULATE_POINTING_

=head1 DESCRIPTION

=head1 ARGUMENTS

=over 4

=item TYPE = STRING (Given)

The type of pointing being calculated. This is purely for bookkeeping
purposes, and should be either CONTINUUM or LINE. This argument is
case-insensitive. ['LINE']

=back

=head1 NOTES

=over 4

=back

=head1 OUTPUT DATA

=over 4

None.

=back

=head1 TASKS

KAPPA: CENTROID, WCSATTRIB.

=head1 REQUIRED PERL MODULES

=head1 AUTHORS

Brad Cavanagh E<lt>b.cavanagh@jach.hawaii.eduE<gt>

=head1 COPYRIGHT

Copyright (C) 2007 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut

if( ! $Frm->uhdr( "SPARSE_CUBE" ) ) {

  # Find out what type of pointing we're doing, line or
  # continuum. Default to line.
  my $type = ( defined( $_CALCULATE_POINTING_{TYPE} ) ?
               uc( $_CALCULATE_POINTING_{TYPE} ) :
               'LINE' );

  if( $Frm->hdr( "JIGL_CNT" ) == 5 ) {

    _CALCULATE_FIVEPOINT_POINTING_ TYPE=$type

  } else {

    my $in = $Frm->file;

    # Set the offset coordinate system to Origin.
    $Mon{'ndfpack_mon'}->obeyw( "wcsattrib", "ndf=$in mode=get name=SkyRefIs" );
    my ( $ORAC_STATUS, $skyrefis ) = $Mon{'ndfpack_mon'}->get( "wcsattrib", "value" );
    $Mon{'ndfpack_mon'}->obeyw( "wcsattrib", "ndf=$in mode=set name=SkyRefIs newval=Origin" );

    # Retrieve the format of either axis, so that we can set them back
    # when we're done processing.
    $Mon{'ndfpack_mon'}->obeyw( "wcsattrib", "ndf=$in mode=get name='Format(1)'" );
    ( $ORAC_STATUS, my $format1 ) = $Mon{'ndfpack_mon'}->get( "wcsattrib", "value" );
    $Mon{'ndfpack_mon'}->obeyw( "wcsattrib", "ndf=$in mode=get name='Format(2)'" );
    ( $ORAC_STATUS, my $format2 ) = $Mon{'ndfpack_mon'}->get( "wcsattrib", "value" );

    # Set the format of the axes to arcseconds, with four decimal
    # places.
    my $newformat = "s.4";
    $Mon{'ndfpack_mon'}->obeyw( "wcsattrib", "ndf=$in mode=set name='Format(1)' newval=$newformat" );
    $Mon{'ndfpack_mon'}->obeyw( "wcsattrib", "ndf=$in mode=set name='Format(2)' newval=$newformat" );

    # Calculate the centroid, using the centre of the array as the
    # initial position of the object.
    my $params = "ndf=$in init='0,0' mode=Interface cerror=true";
    $Mon{'kappa_mon'}->obeyw( "centroid", "$params" );

    # Retrieve the x and y centre.
    ( $ORAC_STATUS, my $xcen ) = $Mon{'kappa_mon'}->get( "centroid", "xcen" );
    ( $ORAC_STATUS, my $ycen ) = $Mon{'kappa_mon'}->get( "centroid", "ycen" );
    ( $ORAC_STATUS, my $xerr ) = $Mon{'kappa_mon'}->get( "centroid", "xerr" );
    ( $ORAC_STATUS, my $yerr ) = $Mon{'kappa_mon'}->get( "centroid", "yerr" );

    # Make them pretty for output.
    my $p_xcen = sprintf( "%.2f", $xcen );
    my $p_ycen = sprintf( "%.2f", $ycen );
    my $p_xerr = sprintf( "%.2f", $xerr );
    my $p_yerr = sprintf( "%.2f", $yerr );

    # Display the results to the user.
    orac_print( "Pointing results using CENTROID:\n", "blue" );
    orac_print( " DAZ: $p_xcen +/- $p_xerr\n DEL: $p_ycen +/- $p_yerr\n", "blue" );

    # Display a warning if values are large.
    if( abs( $xcen ) > 10 ) {
      orac_err "Absolute DAZ pointing value greater than 10 arcseconds.\n";
    } elsif( abs( $xcen ) > 5 ) {
      orac_warn "Absolute DAZ pointing value greater than 5 arcseconds.\n";
    }
    if( abs( $ycen ) > 10 ) {
      orac_err "Absolute DEL pointing value greater than 10 arcseconds.\n";
    } elsif( abs( $ycen ) > 5 ) {
      orac_warn "Absolute DEL pointing value greater than 5 arcseconds.\n";
    }

    # Store with the calibration system.
    my %calhash = ( %{$Frm->hdr},
                    DAZ => $xcen,
                    DEL => $ycen,
                    DAZ_ERR => $xerr,
                    DEL_ERR => $yerr,
                    POINTING_TYPE => $type,
                    METHOD => 'CENTROID',
                  );
    $Cal->pointingindex->add( $Frm->file, \%calhash );

    # Tidy up the output so we have differentiation between CENTROID and PSF.
    orac_print "\n";

    # Now do the PSF pointing calculation.
    #
    # Find the location of the peak pixel.
    $Mon{'ndfpack_mon'}->obeyw( "setaxis", "ndf=$in mode=wcs dim=1" );
    $Mon{'ndfpack_mon'}->obeyw( "setaxis", "ndf=$in mode=wcs dim=2" );
    $Mon{'kappa_mon'}->obeyw( "stats", "ndf=$in" );
    ( $ORAC_STATUS, my @maxcoord ) = $Mon{'kappa_mon'}->get( "stats", "maxcoord" );
    my $x_max = $maxcoord[0] * 3600;
    my $y_max = $maxcoord[1] * 3600;

    # Create an input positions list file.
    my $psfcat = new ORAC::TempFile;
    my $psfcatfile = $psfcat->file;
    open my $psfcatfile_h, ">", $psfcatfile or orac_err "Cannot open temporary file for writing PSF input position list: $!";
    print $psfcatfile_h "$x_max $y_max\n";
    close $psfcatfile_h;

    # Set up the parameters for PSF.
    $params = "in=$in incat=! cofile=$psfcatfile device=! gauss=false out=!";

    # Run PSF.
    $Mon{'kappa_mon'}->obeyw( "psf", "$params" );

    # Retrieve the position.
    ( $ORAC_STATUS, my $psf_xcen ) = $Mon{'kappa_mon'}->get( "psf", "xcen" );
    ( $ORAC_STATUS, my $psf_ycen ) = $Mon{'kappa_mon'}->get( "psf", "ycen" );

    # Make these pretty for output.
    my $p_psf_xcen = sprintf( "%.2f", $psf_xcen );
    my $p_psf_ycen = sprintf( "%.2f", $psf_ycen );

    # Display the results to the user.
    orac_print( "Pointing results using PSF:\n", "blue" );
    orac_print( " DAZ: $p_psf_xcen\n DEL: $p_psf_ycen\n", "blue" );

    # Display a warning if values are large.
    if( abs( $psf_xcen ) > 10 ) {
      orac_err "Absolute DAZ pointing value greater than 10 arcseconds.\n";
    } elsif( abs( $psf_xcen ) > 5 ) {
      orac_warn "Absolute DAZ pointing value greater than 5 arcseconds.\n";
    }
    if( abs( $psf_ycen ) > 10 ) {
      orac_err "Absolute DEL pointing value greater than 10 arcseconds.\n";
    } elsif( abs( $psf_ycen ) > 5 ) {
      orac_warn "Absolute DEL pointing value greater than 5 arcseconds.\n";
    }

    # Store with the calibration system.
    my %calhash = ( %{$Frm->hdr},
                    DAZ => $psf_xcen,
                    DEL => $psf_ycen,
                    DAZ_ERR => 0,
                    DEL_ERR => 0,
                    POINTING_TYPE => $type,
                    METHOD => 'PSF',
                  );
    $Cal->pointingindex->add( $Frm->file, \%calhash );

    # Set SkyRefIs and Format back to what they were before.
    $Mon{'ndfpack_mon'}->obeyw( "wcsattrib", "ndf=$in mode=set name=SkyRefIs newval=$skyrefis" );
    $Mon{'ndfpack_mon'}->obeyw( "wcsattrib", "ndf=$in mode=set name='Format(1)' newval=$format1" );
    $Mon{'ndfpack_mon'}->obeyw( "wcsattrib", "ndf=$in mode=set name='Format(2)' newval=$format2" );

    # Tidy-up output.
    orac_print "\n";

  }

} else {

  orac_print "Currently unable to calculate pointing values for a sparse cube.\n";

  # Tidy-up output.
  orac_print "\n";

}

