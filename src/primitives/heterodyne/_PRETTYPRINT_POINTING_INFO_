# -*-cperl-*-

=head1 NAME

_PRETTYPRINT_POINTING_INFO_ - Display previously-calculated pointing
statistics.

=head1 DESCRIPTION

This primitive displays information gained from pointing reductions.

=head1 ARGUMENTS

None.

=head1 NOTES

=over 4

=item *

Pointing information is contained in a reference to an array of hashes
located in the POINTING_INFO uhdr item for the current Frame
object. Keys 'DAZ', 'DEL', 'DAZ_ERR', 'DEL_ERR', 'METHOD', and
'POINTING' are used.

=back

=head1 OUTPUT DATA

None.

=head1 TASKS

None.

=head1 REQUIRED PERL MODULES

None.

=head1 AUTHORS

Brad Cavanagh E<lt>b.cavanagh@jach.hawaii.eduE<gt>

=head1 COPYRIGHT

Copyright (C) 2008 Science and Technology Facilities Council.  All
Rights Reserved.

=cut

if( defined( $Frm->uhdr( "POINTING_INFO" ) ) ) {

  # Sort first by METHOD, then by POINTING_TYPE.
  my @sorted = map { $_->[0] }
               sort { $a->[1] cmp $b->[1] }
               sort { $a->[2] cmp $b->[2] }
               map { [$_, $_->{POINTING_TYPE}, $_->{METHOD}] }
               @{$Frm->uhdr( "POINTING_INFO" )};

  orac_say( "Pointing calculation results:", "blue" );

  foreach my $info ( @sorted ) {
    my $daz  = $info->{DAZ};
    my $del  = $info->{DEL};
    my $daz_err = $info->{DAZ_ERR};
    my $del_err = $info->{DEL_ERR};
    my $method  = $info->{METHOD};
    my $type = $info->{POINTING_TYPE};

    orac_say  ( " $method $type:", "blue" );
    orac_print( sprintf( "  DAZ: %.2f", $daz ), "blue" );
    if( $daz_err != 0 ) {
      orac_say( sprintf( " +/- %.2f", $daz_err ), "blue" );
    } else {
      orac_say "";
    }
    orac_print( sprintf( "  DEL: %.2f", $del ), "blue" );
    if( $daz_err != 0 ) {
      orac_say( sprintf( " +/- %.2f", $del_err ), "blue" );
    } else {
      orac_say "";
    }

    # Check to see if we have large offsets.
    if( sqrt( $daz * $daz + $del * $del ) > 10 ) {
      orac_say(" Absolute pointing offset greater than 10 arcseconds.", "red" );
    } elsif( sqrt( $daz * $daz + $del * $del ) > 5 ) {
      orac_say( " Absolute pointing offset greater than 5 arcseconds.", "yellow" );
    }

    # Tidy-up output.
    orac_say "";

  }
}
