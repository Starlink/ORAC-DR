# -*-perl-*-

=head1 NAME

_QA_SYSTEM_TEMPERATURE_ - Run system temperature QA checks.

=head1 DESCRIPTION

This primitive tests raw ACSIS time-series data for variation in the
Tsys of the data. It first finds the mean Tsys for the entire
observation, then calculates the Tsys for each receptor. These values
are then printed and stored for later use.

=head1 ARGUMENTS

None.

=head1 OUTPUT DATA

None.

=head1 NOTES

=over 4

=item *

This primitive is suitable for ACSIS time-series data.

=item *

The receptor Tsys values are stored in the QA_TSYS_VALUES uhdr as a
hash reference, with the key being the OBSIDSS header value and the
value being a further hash reference, where keys are the receptor name
and values are the corresponding Tsys.

=back

=head1 REQUIRED PERL MODULES

JCMT::ACSIS::Array, Starlink::HDSPACK.

=head1 AUTHORS

Brad Cavanagh <b.cavanagh@jach.hawaii.edu>

=head1 COPYRIGHT

Copyright (C) 2008 Science and Technology Facilities Council. All
Rights Reserved.

=cut

use Starlink::HDSPACK qw/ create_hdsobj copy_hdsobj /;
use JCMT::ACSIS::Array;

my %tsys;
my %receptor_tsys;
my %numgood;

my $key = $Frm->uhdr( "ORAC_OBSERVATION_ID" ) . "_" . $Frm->uhdr( "ORAC_SUBSYSTEM_IDKEY" );

my $array_tsys = 0;

foreach my $i ( 1 .. $Frm->nfiles ) {

  my $infile = $Frm->file( $i );

  orac_say "Retrieving Tsys values from $infile.";

  my $tmptsys = new ORAC::TempFile;

  # Copy the Tsys array out to a temporary NDF.
  my $ORAC_STATUS = ( create_hdsobj( "$tmptsys", 'NDF' ) ? ORAC__OK : ORAC__ERROR );
  $ORAC_STATUS = ( copy_hdsobj( "$infile.MORE.ACSIS.TSYS", "$tmptsys.DATA_ARRAY" ) ? ORAC__OK : ORAC__ERROR );

  # Get stats on the Tsys array.
  $Mon{'kappa_mon'}->obeyw( "stats", "ndf=$tmptsys" );

  ( $ORAC_STATUS, my $mean ) = $Mon{'kappa_mon'}->get( "stats", "mean" );

  $array_tsys += $mean;

  # Get stats for each receptor.
  my $array = new JCMT::ACSIS::Array( File => $infile );
  my @receptors = $array->receptors;

  foreach my $receptor ( @receptors ) {
    my $pixel = $array->pixel( $receptor );
    $Mon{'kappa_mon'}->obeyw( "stats", "ndf=$tmptsys($pixel,)" );
    ( $ORAC_STATUS, my $pixmean ) = $Mon{'kappa_mon'}->get( "stats", "mean" );

    if( ! defined( $receptor_tsys{$receptor} ) ) {
      $receptor_tsys{$receptor} = 0;
      $numgood{$receptor} = 0;
    }
    if( $pixmean > 0 ) {
      $receptor_tsys{$receptor} += $pixmean;
      $numgood{$receptor}++;
    }
  }
}

$array_tsys /= $Frm->nfiles;
foreach my $receptor ( keys %receptor_tsys ) {
  if( $receptor_tsys{$receptor} != 0 ) {
    $receptor_tsys{$receptor} /= $numgood{$receptor};
  } else {
    $receptor_tsys{$receptor} = 'bad';
  }
}

orac_say sprintf( " Tsys for entire array:   %7.2f K", $array_tsys );

_PRETTYPRINT_RECEPTOR_QA_ HASH=\%receptor_tsys TYPE=Tsys

$tsys{$key} = \%receptor_tsys;

# Store the hash of Tsys values.
$Frm->uhdr( "QA_TSYS_VALUES", \%tsys );

# Tidy-up output.
orac_print "\n";
