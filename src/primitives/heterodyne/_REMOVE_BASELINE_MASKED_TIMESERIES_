# -*-cperl-*-

=head1 NAME

_REMOVE_BASELINE_MASKED_TIMESERIES_

=head1 DESCRIPTION

This primitive determines a baseline for a time-series cube using
previously masked time-series data, determined by the
_RECREATE_MASKED_TIMESERIES_ primitive. A high-order polynomial is fit
to the masked data to determine the baseline, which is then subtracted
from the original (unmasked) time-series data.

=head1 ARGUMENTS

=over 4

=item KNOTS = INTEGER (Given)

The number of knots to use if spline fitting is being applied.  An
interpolating spline is limited to a 11 knots with a default of 4.
The default is 12 knots for a smoothing spline and up to 60 may be
used.  This argument can be overridden by the RECPAR_BASELINE_KNOTS
recipe parameter.  [!]

=item ORDER = INTEGER (Given)

The polynomial order that will be used to determine the baseline. As
the masked data will be used, a high order is recommended.  The order
of fit used for the baseline.  A special value of "spline" uses a
spline fit, which is only suitable for narrow-line spectra.  This
argument can be overridden by the RECPAR_BASELINE_ORDER recipe
parameter.  [1]

=item SPLINE_TYPE = STRING (Given)

The spline type, which is either 'interpolating' for a least-squares fit
that passes through the knots, or 'smooth' for a smooth spline.  These
may be abbrieviated to 'int' and 'smo', respectively.  See
KAPPA:MFITTREND documentation for details.  It will be ignored unless
the chosen order is set to 'spline'. This argument can be overridden
by the RECPAR_BASELINE_SPLINE_TYPE recipe parameter.  If the resultant
value is neither of the allowed alternatives, it is defaulted to
'interpolating'.  ['interpolating']

=back

=over 4

=item *

This primitive is suitable for ACSIS data.

=item *

The masked time-series data filenames must be stored in a hash
reference in the Group MASKED_TIMESERIES uhdr, with the key being the
original unmasked time-series data, and the value being the masked
time-series data.

=item *

The output files are logged as group intermediates.

=back

=head1 OUTPUT DATA

=over 4

=item *

Baselined time-series data with _blNNN suffix.

=back

=head1 TASKS

KAPPA: MFITTREND, SUB

=head1 REQUIRED PERL MODULES

None.

=head1 AUTHORS

Brad Cavanagh E<lt>b.cavanagh@jach.hawaii.eduE<gt>

Malcolm J. Currie  E<lt>mjc.star.rl.ac.ukE<gt>

=head1 COPYRIGHT

Copyright (C) 2008, 2012, 2016, 2022 Science and Technology Facilities Council.
All Rights Reserved.

=head1 LICENCE

This program is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public License as
published by the Free Software Foundation; either Version 3 of
the License, or (at your option) any later version.

This program is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied
warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public
License along with this program; if not, write to the Free
Software Foundation, Inc., 59 Temple Place, Suite 330, Boston,
MA 02111-1307, USA.

=cut

# Retrieve parameters.
my $knots = get_prim_arg( $_PRIM_ARGS_, "KNOTS", undef );
my $order = get_prim_arg( $_PRIM_ARGS_, "ORDER", 1 );
my $spline_type = lc( get_prim_arg( $_PRIM_ARGS_, "SPLINE_TYPE", 'interpolating' ) );
$knots = $spline_type =~ /smo/ ? 12 : 4 if ! defined( $knots );

# Override arguments if we've been given them as recipe parameters
$order = ( defined( $Frm->uhdr( "RECPAR_BASELINE_ORDER" ) ) ?
           $Frm->uhdr( "RECPAR_BASELINE_ORDER" )            :
           $order );
$knots = ( defined( $Frm->uhdr( "RECPAR_BASELINE_KNOTS" ) ) ?
           $Frm->uhdr( "RECPAR_BASELINE_KNOTS" )            :
           $knots );
$spline_type = ( defined( $Frm->uhdr( "RECPAR_BASELINE_SPLINE_TYPE" ) ) ?
           $Frm->uhdr( "RECPAR_BASELINE_SPLINE_TYPE" )                  :
           $spline_type );
$spline_type = 'interpolating' if ( $spline_type !~ /int/ && $spline_type !~ /smo/ );
my $spline = 1 if $order =~ /spl/;

# Write a heading to structure the commentary.
orac_say "\n\nSubtract masked baselines from unmasked time-series";
my $underline = "-" x 74;
orac_say "$underline\n";

orac_say "Fitting baselines to masked time-series data and subtracting them from unmasked time-series data.";
if ( $spline ) {
  my $spltype = $spline_type =~ /int/ ? "interpolating" : "smoothed";
  orac_print "Baselines are fitted to a ${spltype} spline with $knots knots.\n";
} else {
  orac_print "Baselines are fitted to a polynomial of order $order.\n";
}

my %masked;

foreach my $mFrm ( $Grp->members ) {

  foreach my $i ( 1 .. $mFrm->nfiles ) {

    my ( $in, $out ) = $mFrm->inout( "_bl", $i );

    my $masked_ref = $Grp->uhdr( "MASKED_TIMESERIES" );
    my $masked = $masked_ref->{$in};

    my $temp1 = new ORAC::TempFile( 0 );
    my $temp1file = $temp1->file;
    my $temp2 = new ORAC::TempFile( 0 );
    my $temp2file = $temp2->file;

    # Set up parameters to MFITTREND.
    my $params = "in=$masked out=$temp1file axis=1 auto=false ranges=\! ";
    $params .= "variance subtract=false method=single ";
    if ( $spline ) {
      if ( $spline_type =~ /int/ ) {
        $params .= " fittype=spline interpol knots=$knots";
      } else {
        $params .= " fittype=spline interpol=false knots=$knots";
      }
    } else {
       $params .= "order=$order";
    }

    $Mon{'kappa_mon'}->obeyw( "mfittrend", "$params" );

    # Subtract the mask from the input file.
    $params = "in1=$in in2=$temp1file out=$temp2file";
    $Mon{'kappa_mon'}->obeyw( "sub", "$params" );

    # Copy over.
    $Mon{'ndfpack_mon'}->obeyw( "ndfcopy", "in=$temp2file out=$out" );

    # Report to the user.
    orac_print "$in to $out:\n";
    orac_print " Subtracted baselines determined from $masked.\n";

    # Update the Frm.
    $mFrm->file( $i, $out );

    # Update the key for the masks.
    $masked{$out} = $masked;

    # Request that the output file is removed upon completion of the
    # group processing.
    $Grp->push_intermediates( $ out );
  }
}

# Update the list of files associated with the masks.
$Grp->uhdr( "MASKED_TIMESERIES", \%masked );

orac_print "\n";
