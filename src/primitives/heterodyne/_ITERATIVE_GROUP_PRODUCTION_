# -*-cperl-*-

=head1 NAME

_ITERATIVE_GROUP_PRODUCTION_ - Iteratively create group co-adds for
heterodyne data.

=head1 DESCRIPTION

This primitive creates a group co-added cube from all members of the
current group. It then baselines the cube and creates moments maps.

In the process of baselining the group cube, this primitive also
creates a baseline mask file, masking out emission. It then runs this
mask through UNMAKECUBE to create time-series masks. These masks are
applied to the original time-series data, which are then
baselined. The baselined time-series data are then run through
MAKECUBE to create individual baselined cubes for each
observation. Moments maps are then made from these cubes.

There are a number of ways to define the baseline regions:
  - as a percentage of the spectrum width at either end of the spectrum
  (see EDGES);
  - as a set of velocity ranges expected or known to be free of emission
  lines (see BASELINE_REGIONS); or if both of these arguments or their
  corresponding recipe parameters are undefined,
  - use the whole spectrum smoothing spectrally and spatially (see
  FREQUENCY_SMOOTH and SPATIAL_SMOOTH) with feature detection to mask
  lines.  This can also be selected if BASELINE_REGIONS is defined for
  other purposes, such as the rejection of bad spectra. by setting
  the BASELINE_METHOD recipe parameter to 'auto'.
The first two are suitable for broadline emission.  The second is
desirable in the presence of many lines.  The third is the most common
for a single narrow line.

=head1 ARGUMENTS

=over 4

=item BASELINE_REGIONS = STRING (Given)

A comma-separated list of floating-point velocity ranges each in the
format v1:v2, from where the baseline should be estimated.  It is
countermanded should EDGES be defined and non-negative.  This argument
can be overridden by the RECPAR_BASELINE_REGIONS uhdr via the recipe
parameter system.  [undef]

=item EDGES = REAL (Given)

Percentage of the full range to fit on either edge of the spectrum for
baselining purposes. If set to a non-positive value and
BASELINE_REGIONS is undefined, then the baseline is obtained after
smoothing and automatic emission detection.  This argument can be
overridden by the RECPAR_BASELINE_EDGES uhdr via the recipe parameter
system.  If assigned a negative value, BASELINE_REGIONS, if it is
defined, will be used instead to specify where to determine the
baseline.  [undef]

=item FLATFIELD = LOGICAL (Given)

Whether or not to flat-field the baselined time-series data. This
argument can be overridden by the RECPAR_FLATFIELD uhdr via the recipe
parameter system. [0]

=item FLAT_METHOD = STRING (Given)

When flat-fielding is required (cf. FLATFIELD argument) this selects
the method used to derive the relative gains of the receptors.  The
allowed selection comprises 'ratio' which finds the histogram peaks of
the ratio of voxel values; and 'sum' which finds the integrated flux.
The final option is 'index', which searches a calibration index of
nightly flat-field ratios, and applies it if one is found.  This
argument can be overridden by the RECPAR_FLAT_METHOD uhdr via the
recipe parameter system.  ['sum']

=item FREQUENCY_SMOOTH = INTEGER (Given)

The number of channels to smooth in the frequency axis when smoothing
to determine baselines. This number should be small (~10) for
narrow-line observations and large (~25) for broad-line
observations.  This argument can be overridden by the
RECPAR_FREQUENCY_SMOOTH uhdr via the recipe parameter system.  [25]

=item ITERATIONS = INTEGER (Given)

The number of iterations to perform.  This argument can be
overridden by the RECPAR_ITERATIONS uhdr via the recipe parameter
system.  [1]

=item JSATILE = LOGICAL (Given)

If true JSA tiles will be created. [0]

=item LOWER_VELOCITY = REAL (Given)

The requested lower velocity, in km/s to be applied to products
created after the final iteration.  This argument can be overridden by
the RECPAR_FINAL_LOWER_VELOCITY uhdr via the recipe parameter system.  [undef]

=item MOMENTS = STRING (Given)

The moment maps to create. These are any of the values allowed for the
ESTIMATOR parameter to the COLLAPSE method, but in reality this should
probably be 'integ', 'iwc', and/or 'itd'. Any number of moments can be
given in a comma-separated string.  A value of 'none' prevents creation
of any moment maps.  This argument can be overridden by the RECPAR_MOMENTS
uhdr via the recipe parameter system.  ['integ']

=item METHOD = STRING (given)

Method to use to find emission in the data: 'clumpfind', 'fellwalker',
or 'thresh'.  This argument can be overridden by the RECPAR_CLUMP_METHOD
uhdr via the recipe parameter system.  ['clumpfind']

=item ORDER = INTEGER (Given)

The polynomial order that will be used when estimating baselines.
This argument can be overridden by the RECPAR_BASELINE_ORDER uhdr
via the recipe parameter system.  [1]

=item PARAM1 = REAL (Given)

The first parameter required by the spreading method. This parameter
is required by all methods, and specifies how many arcseconds on
either side of the output position are to receive contributions from
the input pixel. A value of zero indicates that a suitable number
should be calculated automatically.  This argument can be overridden
by the RECPAR_SPREAD_WIDTH uhdr via the recipe parameter system.  [0]

=item PARAM2 = REAL (Given)

The second parameter required by the spreading method, but only for
SombCos, Gauss, SincSinc, SincCos, and SincGauss schemes. For the
SombCos, SincSinc, and SincCos schemes, it specifies the number of
arcseconds at which the envelope of the function goes to zero. The
minimum value corresponds to 1.0 pixels, and the run-time default
value is equivalent to 2.0 pixels.  For the Gauss and SincGauss
scheme, it specifies the full-width at half-maximum (FWHM) of the
Gaussian envelope.  The minimum value is 0.1, and the run-time default
is 1.0.  This argument can be overridden by the
RECPAR_SPREAD_FWHM_OR_ZERO uhdr via the recipe parameter system.  []

=item PER_LINE = LOGICAL (Given)

Whether or not to create a moments map for each detected line. [0]

=item PIXSIZE = REAL (Given)

The output pixel scale. If not defined, this value is automatically
determined from the data.  This argument can be overridden by the
RECPAR_PIXSIZE uhdr via the recipe parameter system.  []

=item QA = LOGICAL (Given)

Whether or not to perform quality assurance tests on output files. [1]

=item REBIN = STRING (Given)

A comma-separated list of requested rebinning resolutions, in km/s. A
rebinned cube will be created for each requested resolution. Only the
group cube will be rebinned. This argument can be overridden by the
RECPAR_REBIN uhdr via the recipe parameter system. []

=item RMS_THRESHOLD = REAL (Given)

The RMS threshold above which emission will be considered to be a
clump. A separate threshold can be specified for each moments map
requested. The last specified number will be applied to any further
moments requested. Typically 'integ' can tolerate a rms of 3-sigma,
but the higher order moments need a higher threshold. Comma-separated
string in the same order as the requested MOMENTS. [3.0,4.0]

=item SPATIAL_SMOOTH = INTEGER (Given)

The number of pixels to smooth in both spatial axes when smoothing to
determine baselines.   This argument can be overridden by the
RECPAR_SPATIAL_SMOOTH uhdr via the recipe parameter system.  [3]

=item SPREAD = STRING (Given)

The method to use when spreading each input pixel value out between a
group of neighbouring output pixels when using MAKECUBE to generate a
cube. This argument can be overridden by the RECPAR_SPREAD_METHOD uhdr
via the recipe parameter system.  ['nearest']

=item SYSTEM = STRING (Given)

The celestial coordinate system for the output cube. Can be any one of
ICRS, GAPPT, FK5, FK4, FK4-NO-E, AZEL, GALACTIC, or ECLIPTIC. A
special value of TRACKING can also be given, in which case the system
used will be that used as the tracking system during the
observation.

If this argument is not defined, then the system is determined from
the data and is typically set to 'tracking'. The only case where this
does not apply is if the observation is a scan and the scanning was
done in AZ/EL, in which case the system will be set to 'AZEL'.

This argument can be overridden by the RECPAR_SYSTEM uhdr via the
recipe parameter system. [undef]

=item TILE = LOGICAL (Given)

Whether or not to break the spectral cubes into tiles.  [1]

=item TILEBORDER = INTEGER (Given)

The size of the border to be added to tiles. This is used when
smoothing the cube in spatial extend in later processing so as to not
add edge effects when merging tiles together. [0]

=item UPPER_VELOCITY = REAL (Given)

The requested upper velocity, in km/s to be applied to products
created after the final iteration.  This argument can be overridden by
the RECPAR_FINAL_UPPER_VELOCITY uhdr via the recipe parameter system.  [undef]

=back

=head1 NOTES

=over 4

=item *

This primitive is suitable for ACSIS data.

=item *

Noise statistics are evaluated and logged for the final cubes.  FOr the
group cubes these include calculating the median Tsys when MAKECUBE has
failed to do it, and to find a mean RMS noise.

=item *

If processing with a line-forest recipe, as determined by the
LINEFOREST_BASELINE recipe parameter being set true, the primitive
validates the presence of a line forest.  If the spectra have but a
few lines that don't occupy most of the spectrum, a warning issued
that that either the recipe is incorrect or that LINEFOREST_BASELINE
should be set to false.

The purpose is to discourage the aggressive filtering formulation that
is needed for line-forest spectra to be applied to regular spectra.
It is inappropriate because, unlike a line forest, the vast majority
of the channels are baseline, not emission. Without a switch of recipe
or LINEFOREST_BASELINE, the baselines are excessively masked leading
to biased or even nonsense baseline fits.

Details of the validation tests may be found
L<_IS_OBSERVATION_A_LINEFOREST_|_IS_OBSERVATION_A_LINEFOREST_>.

=back

=head1 OUTPUT DATA

=over 4

=back

=head1 TASKS

See _CALCULATE_MEDIAN_TSYS_, _CREATE_CUBE_FRAME_, _CREATE_CUBE_GROUP_,
_CREATE_MOMENTS_MAPS_THROUGH_SMOOTHING_, _CREATE_LV_IMAGE_,
_CREATE_NOISE_MAP_, _FIND_MEAN_RMS_, _IS_OBSERVATION_A_LINEFOREST_,
_NORMALIZE_RECEPTOR_RESPONSIVITY_,
_REBIN_VELOCITY_SCALE_, _REMOVE_BASELINE_, _REMOVE_BASELINE_MASKED_TIMESERIES_,
_REMOVE_BASELINE_THROUGH_SMOOTHING_, _REMOVE_FLAT_FIELD_,
_REMOVE_FLAT_FIELD_INDEX_, _REMOVE_FLAT_FIELD_ITERATE_, _RECREATE_MASKED_TIMESERIES_,
_RESTRICT_VELOCITY_RANGE_, _SUBTRACT_REFERENCE_EMISSION_,
_SUBTRACT_REFERENCE_SPECTRUM_, _TAG_AS_REDUCED_PRODUCT_ and primitives therein.

=head1 REQUIRED PERL MODULES

None.

=head1 AUTHORS

Brad Cavanagh E<lt>b.cavanagh@jach.hawaii.eduE<gt>,
Malcolm J. Currie E<lt>mjc@jach.hawaii.eduE<gt>

=head1 COPYRIGHT

Copyright (C) 2008, 2011-2016, 2018-2019 Science and Technology
Facilities Council.  All Rights Reserved.

=head1 LICENCE

This program is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public License as
published by the Free Software Foundation; either Version 3 of
the License, or (at your option) any later version.

This program is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied
warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public
License along with this program; if not, write to the Free
Software Foundation, Inc., 59 Temple Place, Suite 330, Boston,
MA 02111-1307, USA.

=cut

# Obtain arguements and recipe parameter overrides.
# =================================================

# Many arguments get passed to later primitives.
my $b_regions = get_prim_arg( $_PRIM_ARGS_, "BASELINE_REGIONS", undef );
my $edges = get_prim_arg( $_PRIM_ARGS_, "EDGES", undef );
my $flatfield = get_prim_arg( $_PRIM_ARGS_, "FLATFIELD", 0 );
my $flatmethod = get_prim_arg( $_PRIM_ARGS_, "FLAT_METHOD", 'sum' );
my $freqsmooth = get_prim_arg( $_PRIM_ARGS_, "FREQUENCY_SMOOTH", 25 );
my $iterations = get_prim_arg( $_PRIM_ARGS_, "ITERATIONS", 1 );
my $jsatile = get_prim_arg( $_PRIM_ARGS_, "JSATILE", 0 );
my $lower_vel = get_prim_arg( $_PRIM_ARGS_, "LOWER_VELOCITY", undef );
my $moments = get_prim_arg( $_PRIM_ARGS_, "MOMENTS", 'integ' );
my $method =  get_prim_arg( $_PRIM_ARGS_, "METHOD", 'clumpfind' );
my $rms_levels =  get_prim_arg( $_PRIM_ARGS_, "RMS_THRESHOLD", '3.0,4.0' );
my $order = get_prim_arg( $_PRIM_ARGS_, "ORDER", 1 );
my $per_line = get_prim_arg( $_PRIM_ARGS_, "PER_LINE", 0 );
my $pixsize = get_prim_arg( $_PRIM_ARGS_, "PIXSIZE", undef );
my $qa = get_prim_arg( $_PRIM_ARGS_, "QA", 1 );
my $rebin = get_prim_arg( $_PRIM_ARGS_, "REBIN", undef );
my $sp_param1 = get_prim_arg( $_PRIM_ARGS_, "PARAM1", 0 );
my $sp_param2 = get_prim_arg( $_PRIM_ARGS_, "PARAM2", undef );
my $spatsmooth = get_prim_arg( $_PRIM_ARGS_, "SPATIAL_SMOOTH", 3 );
my $spread = get_prim_arg( $_PRIM_ARGS_, "SPREAD", 'nearest' );
my $system = get_prim_arg( $_PRIM_ARGS_, "SYSTEM", undef );
my $tile = get_prim_arg( $_PRIM_ARGS_, "TILE", 1 );
my $tileborder = get_prim_arg( $_PRIM_ARGS_, "TILEBORDER", 0 );
my $upper_vel = get_prim_arg( $_PRIM_ARGS_, "UPPER_VELOCITY", undef );

# Handle recipe parameter overrides.
$b_regions = ( defined( $Frm->uhdr( "RECPAR_BASELINE_REGIONS" ) ) ?
             $Frm->uhdr( "RECPAR_BASELINE_REGIONS" )              :
             $b_regions );
my @regions;
if ( ref( $b_regions ) eq 'ARRAY' ) { @regions = @{ $b_regions } }

$edges = ( defined( $Frm->uhdr( "RECPAR_BASELINE_EDGES" ) ) ?
         $Frm->uhdr( "RECPAR_BASELINE_EDGES" )              :
         $edges );
$flatfield = ( defined( $Frm->uhdr( "RECPAR_FLATFIELD" ) ) ?
             $Frm->uhdr( "RECPAR_FLATFIELD" )              :
             $flatfield );
$flatmethod = ( defined( $Frm->uhdr( "RECPAR_FLAT_METHOD" ) ) ?
              $Frm->uhdr( "RECPAR_FLAT_METHOD" )              :
              $flatmethod );
$freqsmooth = ( defined( $Frm->uhdr( "RECPAR_FREQUENCY_SMOOTH" ) ) ?
              $Frm->uhdr( "RECPAR_FREQUENCY_SMOOTH" )              :
              $freqsmooth );
$lower_vel = ( defined( $Frm->uhdr( "RECPAR_FINAL_LOWER_VELOCITY" ) ) ?
             $Frm->uhdr( "RECPAR_FINAL_LOWER_VELOCITY" )              :
             $lower_vel );
$moments = ( defined( $Frm->uhdr( "RECPAR_MOMENTS" ) ) ?
           $Frm->uhdr( "RECPAR_MOMENTS" )              :
           $moments );
$iterations = ( defined( $Frm->uhdr( "RECPAR_ITERATIONS" ) ) ?
              $Frm->uhdr( "RECPAR_ITERATIONS" )              :
              $iterations );
$order = ( defined( $Frm->uhdr( "RECPAR_BASELINE_ORDER" ) ) ?
         $Frm->uhdr( "RECPAR_BASELINE_ORDER" )              :
         $order );
$pixsize = ( defined( $Frm->uhdr( "RECPAR_PIXSIZE" ) ) ?
           $Frm->uhdr( "RECPAR_PIXSIZE" )              :
           $pixsize );
$rebin = ( defined( $Frm->uhdr( "RECPAR_REBIN" ) ) ?
         $Frm->uhdr( "RECPAR_REBIN" )              :
         $rebin );

# The PARAM1 and PARAM2 recipe parameters are historical internal names
# and match the argument names used by this primitive and others that
# this primitive invokes directly or indirectly.  Users should supply the
# more-descriptive SPREAD_WIDTH and/or SPREAD_FWHM_OR_ZERO, which are
# translated to the historical internal-header names by recipe steering
# primitives.
$sp_param1 = ( defined( $Frm->uhdr( "RECPAR_PARAM1" ) ) ?
             $Frm->uhdr( "RECPAR_PARAM1" )              :
             $sp_param1 );
$sp_param2 = ( defined( $Frm->uhdr( "RECPAR_PARAM2" ) ) ?
             $Frm->uhdr( "RECPAR_PARAM2" )              :
             $sp_param2 );

$spatsmooth = ( defined( $Frm->uhdr( "RECPAR_SPATIAL_SMOOTH" ) ) ?
              $Frm->uhdr( "RECPAR_SPATIAL_SMOOTH" )              :
              $spatsmooth );
$spread = ( defined( $Frm->uhdr( "RECPAR_SPREAD_METHOD" ) ) ?
          $Frm->uhdr( "RECPAR_SPREAD_METHOD" )              :
          $spread );
$system = ( defined( $Frm->uhdr( "RECPAR_SYSTEM" ) ) ?
          $Frm->uhdr( "RECPAR_SYSTEM" )              :
          $system );
$tile = ( defined( $Frm->uhdr( "RECPAR_TILE" ) ) ?
          $Frm->uhdr( "RECPAR_TILE" )            :
          $tile );
$upper_vel = ( defined( $Frm->uhdr( "RECPAR_FINAL_UPPER_VELOCITY" ) ) ?
             $Frm->uhdr( "RECPAR_FINAL_UPPER_VELOCITY" )              :
             $upper_vel );

# Validate values that may have been supplied by recipe parameter.
# ================================================================

$spatsmooth = max( int( $spatsmooth ), 0 );
$freqsmooth = max( int( $freqsmooth ), 0 );

# Validate baseline-region syntax, but only when it is to be used.
my $user_region = 0;
my $base_auto = defined( $Frm->uhdr( "RECPAR_BASELINE_METHOD" ) ) &&
                $Frm->uhdr( "RECPAR_BASELINE_METHOD" ) eq 'auto';

my ( @b_lower, @b_upper );
if ( defined( $b_regions ) && ! $base_auto ) {

   foreach ( @regions ) {
      if ( /^[+-]?(\d+\.\d+|\d+\.|\.\d+|\d+)([eE][+-]?\d+)?:[+-]?(\d+\.\d+|\d+\.|\.\d+|\d+)([eE][+-]?\d+)?$/ ) {
         my ( $lowvel, $uppvel ) = split( ":", $_ );
         $user_region = 1;
         push @b_lower, $lowvel;
         push @b_upper, $uppvel;
      } else {
         $user_region = 0;
         last;
      }
   }

   if ( ! $user_region ) {
      orac_err "Supplied baseline extents have invalid format\n" .
               "Syntax is <lower1>:<upper1>,<lower2>:<upper2>,...\n";
   }
}

# Only process if we're on the last member of a group.
# =====================================================

if ( $Grp->lastallmembers( $Frm ) ) {
   my $copyGrp = $Grp;
   my @members = $Grp->members;
   my ( $good, $bad ) = $Grp->check_membership;

   my $frame_legacy = defined( $Frm->uhdr( "LEGACY" ) ) ? $Frm->uhdr( "LEGACY" ): 0;
   my $group_legacy = defined( $Grp->uhdr( "LEGACY" ) ) ? $Grp->uhdr( "LEGACY" ): 0;
   my $legacy = $frame_legacy || $ group_legacy;

   my $makegroup;
   if ( ! @$good ) {
      orac_warn "No good members in current group. Cannot do group processing.\n";

      # Write the QA information for each member.
      foreach my $Obj ( @members ) {
         _QA_WRITE_REPORT_ OBJ=$Obj
         _WRITE_NOISESTATS_LOG_ OBJ=$Obj
      }
      $makegroup = 0;

      # Create a new group where all the frames are deemed to be good.
      # ==============================================================
      #
      # This particularly includes ones that have failed QA, so that the
      # observation products are created.
      $Grp = new $Grp;
      $Grp->name( $copyGrp->name );
      $Grp->file( $copyGrp->file );
      %{$Grp->hdr} = %{$copyGrp->hdr};
      %{$Grp->uhdr} = %{$copyGrp->uhdr};

      foreach my $Obj ( $copyGrp->allmembers ) {

         # Make a deep copy of the Frame.  The files method copies the
         # hdr for raw data, but not the uhdr.
         my @files = $Obj->files;
         my $tempFrm = $Frm->new( \@files );
         %{$tempFrm->uhdr} = %{$Obj->uhdr};

         # Store all Frames as good.
         $tempFrm->isgood( 1 );
         $Grp->push( $tempFrm );

         # Ensure current Frame matches the final Frame in the new group
         # so that subsequent # $Grp->lastallmembers checks compare like
         # with like.
         $Frm = $tempFrm;
      }

   } else {

      # We need to write QA info for each member that didn't end up in
      # the group (i.e. that failed QA)
      foreach my $badObj ( @$bad ) {
         _QA_WRITE_REPORT_ OBJ=$badObj
         _WRITE_NOISESTATS_LOG_ OBJ=$badObj
      }

      # Write a heading to structure the commentary.
      orac_say "\n\nBeginning iterative group production.";
      my $underline = "-" x 51;
      orac_say "$underline\n";
      $makegroup = 1;
   }

   # Summit versions of recipes may wish to omit slower parts of the
   # processing.
   my $summit = defined( $Frm->uhdr( "SUMMIT" ) ) ? $Frm->uhdr( "SUMMIT" ) : 0;

   # Merge AST Regions for scans only.
   if ( lc( $Frm->uhdr( "ORAC_SAMPLE_MODE" ) ) eq 'scan' ) {
      _MERGE_AST_REGIONS_
   }

   # Iterative baseline removal begins here.
   # =======================================
   my $iter = 1;
   while ( $iter <= $iterations ) {

      orac_say "Iteration $iter of $iterations.\n";
      my $final_iteration = ( $iter == $iterations );

      _CREATE_CUBE_GROUP_ SPREAD=$spread PARAM1=$sp_param1 PARAM2=$sp_param2 TILEBORDER=$tileborder PIXSIZE=$pixsize SYSTEM=$system TILE=$tile

      # This observation group is ostensibly of a line forest based upon
      # the chosen recipe.  However, users sometimes erroneously select
      # this recipe for non line-forest data.  Doing so can cause poor
      # baseline fitting due to excessive aggressive masking in MFITTREND.
      # Therefore validate the Group file if it's the sole one, or the
      # central tile, which is most likely to contain the strongest
      # signal.  Being the largest increases thre odd too.
      if ( defined( $Frm->uhdr( "RECPAR_LINEFOREST_BASELINE" ) ) &&
           $Frm->uhdr( "RECPAR_LINEFOREST_BASELINE" ) ) {
         my ( $centre_index,  $centre_file, $centre_fileg );
         if ( $Grp->nfiles == 1 ) {
            $centre_file = $Grp->file;
         } else {
            my @groupfiles = $Grp->files;
            $centre_index = int( scalar( @groupfiles ) / 2 ) + 1;
            $centre_file = $Grp->file( $centre_index );
         }

         # Test if this is of a line forest.  Disable the flag indicating
         # the line-forest processing in MFITTREBD is needed, should the
         # validation fail.
         _IS_OBSERVATION_A_LINEFOREST_ FILE=$centre_file
         my $is_forest = $_IS_OBSERVATION_A_LINEFOREST_{LINEFOREST};
         if ( ! $is_forest ) {
            orac_warn "The observation is not of a line forest.  Consider " .
                      "using another recipe or setting the LINEFOREST_BASELINE " .
                      "recipe parameter to 0 to prevent erroneous baseline " .
                      "subtraction.\n";
         }
      }

      if ( defined( $edges ) && $edges > 0 ) {
         _REMOVE_BASELINE_ EDGES=$edges ORDER=$order GROUP=1 TAG=1
      } elsif ( $user_region ) {
         _REMOVE_BASELINE_ LOWER=\@b_lower UPPER=\@b_upper ORDER=$order GROUP=1 TAG=1
      } else {
         _REMOVE_BASELINE_THROUGH_SMOOTHING_ FREQUENCY_SMOOTH=$freqsmooth SPATIAL_SMOOTH=$spatsmooth ORDER=$order GROUP=1 TAG=1
      }

      # The Group moment and noise maps, and restricted-velocity cubes need
      # only be made on the final iteration.
      if ( $final_iteration ) {

         if ( lc( $moments ) ne "none" ) {
            _CREATE_MOMENTS_MAPS_THROUGH_SMOOTHING_ GROUP=1 MOMENTS=$moments METHOD=$method RMS_THRESHOLD=$rms_levels FREQUENCY_SMOOTH=$freqsmooth SPATIAL_SMOOTH=$spatsmooth ORDER=$order TAG=integ PER_LINE=$per_line
         }

         if ( $legacy ) {
            _CREATE_NOISE_MAP_ GROUP=1 QA=$qa SURVEY=LEGACY
         } else {
            _CREATE_NOISE_MAP_ GROUP=1 QA=$qa
         }

         if ( defined( $rebin ) ) {
            foreach my $res ( split ",", $rebin ) {
               _REBIN_VELOCITY_SCALE_ GROUP=1 PIXELSCALE=$res
            }
         }
      }

      # Regenerate the post-QA observation timeseries cubes, but with
      # emission masked.
      _RECREATE_MASKED_OBS_FILES_ INTERP=$spread PARAM1=$sp_param1 PARAM2=$sp_param2 PIXSIZE=$pixsize TILEBORDER=$tileborder TILE=$tile

      # Subtract the baselines from the emission-masked timeseries.
      _REMOVE_BASELINE_MASKED_TIMESERIES_

      # Attempt to remove reference-spectrum absorption lines automatically at
      # each reference position and optionally for each receptor.
      my $mask_only = $iter > 1;
      _SUBTRACT_REFERENCE_EMISSION_ ONLY_SOURCE_MASK=$mask_only ITERATION=$iter

      # If the automated method fails, the extents of reference
      # lines (usually determined after automated removal has not
      # removed fully the reference lines) can be supplied, and these
      # lines interpolated across.  This permits a better estimate of
      # the residual reference spectrum, which is then subtracted from
      # the time-series cubes.  There is an option to do this removal
      # by receptor.
      _SUBTRACT_REFERENCE_SPECTRUM_

      if ( $flatfield ) {
         if ( lc( $flatmethod ) eq 'ratio' ) {
            _NORMALIZE_RECEPTOR_RESPONSIVITY_ GROUP=2
         } elsif ( lc( $flatmethod ) eq 'itersum' ||  lc( $flatmethod ) eq 'sum' ) {
            _REMOVE_FLAT_FIELD_ITERATE_ GROUP=2 METHOD=$flatmethod
         } elsif ( lc( $flatmethod ) eq 'index' ) {
            _REMOVE_FLAT_FIELD_INDEX_ GROUP=2
         } else {
            _REMOVE_FLAT_FIELD_ GROUP=2
         }
      }

      _SET_TAG_ TAG=TIMESERIES_MASKED_BASELINED GROUP=2

      # Create the observation products for the next iteration.  This will only produce legacy tiles
      # on the final iteration irrespective of the value of $jsatile.
      my $do_tiling = ( $iter == $iterations ) && $jsatile;
      my $underline = "-" x 41;
      orac_say "Iterations completed.\n\nCreating final Frame products.\n$underline\n" if $do_tiling;

      foreach my $Frm ( $Grp->members ) {

         # Some code looks to see if JSA_TILES is defined rather than being true.
         $Frm->uhdr( "JSA_TILES", 1 ) if $do_tiling;

         # Generate the observation cube.
         _CREATE_CUBE_FRAME_ SPREAD=$spread PARAM1=$sp_param1 PARAM2=$sp_param2 TILEBORDER=$tileborder SUFFIX=_gcube PRODUCT=group_derived_cube PIXSIZE=$pixsize SYSTEM=$system TILE=$tile JSATILE=$do_tiling CHUNK=1

         # Want trimmed velocity range for final product?
         if ( defined( $lower_vel ) || defined( $upper_vel ) ) {
            _RESTRICT_VELOCITY_RANGE_ GROUP=0 AXIS=3 LOWER=$lower_vel UPPER=$upper_vel
         }

         # Tag the observation cube.
         _TAG_AS_REDUCED_PRODUCT_ UPDATE=1 GROUP=0 FLAG_QUAL_BAD=1

         # If the object is a flux standard, calculate and report the peak and
         # integrated intensity.
         _CALCULATE_STANDARD_ QUIET=1
         orac_print "\n";
      }

      if ( lc( $moments ) ne "none" ) {
         _CREATE_MOMENTS_MAPS_THROUGH_SMOOTHING_ GROUP=2 MOMENTS=$moments METHOD=$method RMS_THRESHOLD=$rms_levels FREQUENCY_SMOOTH=$freqsmooth SPATIAL_SMOOTH=$spatsmooth ORDER=$order TAG=integ PER_LINE=$per_line
      }

      my $writeqa = $final_iteration;
      if ( $legacy ) {
         _CREATE_NOISE_MAP_ GROUP=2 QA=$qa STOREQA=$writeqa SURVEY=LEGACY
      } else {
         _CREATE_NOISE_MAP_ GROUP=2 QA=$qa STOREQA=$writeqa
      }

      # Compute and log some noise statistics for the individual Frames'
      # "reduced" products, once the iterative phase is over.
      if ( $final_iteration ) {

         # Obtain the mean RMS and median Tsys of the Frame members.
         _FIND_MEAN_RMS_ GROUP=2
         if ( ! defined( $Frm ->hdr( "MEDTSYS" ) ) && ! $summit ) {
             _CALCULATE_MEDIAN_TSYS_ GROUP=2
         }

         # Log the noise statistics for the reduced Frame files.  Ensure
         # that newly added headers like EXP_TIME are known to ORAC-DR
         # methods.
         foreach my $reducedObj( $Grp->members ) {
           $reducedObj->readhdr();
            _WRITE_NOISESTATS_LOG_ OBJ=$reducedObj
         }
      }

      # Restore the masked and baselined timeseries cubes to the group.
      _RETRIEVE_TAG_ TAG=TIMESERIES_MASKED_BASELINED GROUP=2

      $iter++;
   }

   # Final group processing
   # ======================
   if ( $makegroup ) {
      my $underline = "-" x 41;
      orac_say "Creating final Group products.\n$underline\n";

      # Switch on JSA tiling.
      $Grp->uhdr( "JSA_TILES", $jsatile );

      # Form the group cube.
      _CREATE_CUBE_GROUP_ SPREAD=$spread PARAM1=$sp_param1 PARAM2=$sp_param2 TILEBORDER=$tileborder PIXSIZE=$pixsize SYSTEM=$system TILE=$tile JSATILE=$jsatile

      # Do final baseline subtraction.
      my $tagit = ( ! ( defined( $lower_vel ) || defined( $upper_vel ) ) );
      if ( $edges ) {
         _REMOVE_BASELINE_ EDGES=$edges ORDER=$order GROUP=1 TAG=$tagit
      } elsif ( $user_region ) {
         _REMOVE_BASELINE_ LOWER=\@b_lower UPPER=\@b_upper ORDER=$order GROUP=1 TAG=$tagit
      } else {
         _REMOVE_BASELINE_THROUGH_SMOOTHING_ FREQUENCY_SMOOTH=$freqsmooth SPATIAL_SMOOTH=$spatsmooth ORDER=$order GROUP=1 TAG=$tagit
      }

      # Trim final cube, and hence subsequent products from it.
      if ( !$tagit ) {
         _RESTRICT_VELOCITY_RANGE_ GROUP=1 AXIS=3 LOWER=$lower_vel UPPER=$upper_vel
         _TAG_AS_REDUCED_PRODUCT_ UPDATE=1 GROUP=1 FLAG_QUAL_BAD=1
      }

      if ( lc( $moments ) ne "none" ) {
         _CREATE_MOMENTS_MAPS_THROUGH_SMOOTHING_ GROUP=1 MOMENTS=$moments METHOD=$method RMS_THRESHOLD=$rms_levels FREQUENCY_SMOOTH=$freqsmooth SPATIAL_SMOOTH=$spatsmooth ORDER=$order TAG=integ PER_LINE=$per_line
      }

      _CREATE_LV_IMAGE_

      if ( $legacy ) {
        _CREATE_NOISE_MAP_ GROUP=1 QA=$qa STOREQA=1 SURVEY=LEGACY
      } else {
        _CREATE_NOISE_MAP_ GROUP=1 QA=$qa STOREQA=1
      }

      # Obtain and store two noise statistics for the group spectral cube.
      # First, the mean RMS for individual observations is derived from the
      # time series in _QA_SENSITIVITY_VARIATION_, so attempt to reproduce
      # this calculation from the group cube.  Second, MAKECUBE will only
      # generate a Tsys array, and hence a median Tsys header (MEDTYS),
      # for a limited set of input parameters.
      _FIND_MEAN_RMS_ GROUP=1
      if ( ! defined( $Grp->hdr( "MEDTSYS" ) ) && ! $summit ) {
         _CALCULATE_MEDIAN_TSYS_ GROUP=1
      }

      if ( defined( $rebin ) ) {
         foreach my $res ( split ",", $rebin ) {
            _REBIN_VELOCITY_SCALE_ GROUP=1 PIXELSCALE=$res
         }
      }

   } else {

     # Restore the original group with the bad observations flagged.
     $Grp = $copyGrp;

     # The iteration does generate a first pass at the group objects,
     # but these were based upon entirely bad data, so must be removed.
     orac_print( "Removing group files which were derived from bad observations.\n");
     unlink glob( $Grp->file . "*" );
   }

   # Write QA report for the Frames teimseries cubes.
   foreach my $goodObj( $Grp->members ) {
      _QA_WRITE_REPORT_ OBJ=$goodObj
   }

   # Write reports for the group product.
   if ( $makegroup ) {
      _QA_WRITE_REPORT_ OBJ=$Grp
      _WRITE_NOISESTATS_LOG_ OBJ=$Grp

      # If the object is a flux standard, calculate and report the peak and
      # integrated intensity determined from the Group gridded map.
      _CALCULATE_STANDARD_ OBJ=$Grp
   }

   # Delete the intermediate files needed for group processing or are
   # archive products.
   my $saved = $jsatile ? "raw,_healpix" : "raw,_reduced,_cube,_lv";
   _DELETE_TEMP_FILES_ ALSOGRP=1 KEEP=$saved
}

# Tidy-up output.
orac_print "\n";
