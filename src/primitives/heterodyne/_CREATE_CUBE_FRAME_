# -*-perl-*-

=head1 NAME

_CREATE_CUBE_FRAME_ -- Create a cube from a time-series ACSIS
observation and stuff it in the Frm object.

=head1 DESCRIPTION

This primitive takes a time-series ACSIS cube and, using
SMURF/MAKECUBE, transforms it into a spatial/spectral cube.

=head1 ARGUMENTS

=over 4

=item BYTES_PER_PIXEL = INTEGER (Given)

The number of bytes per pixel. [4]

=item MAXSIZE = INTEGER (Given)

The maximum size, in bytes, of the output cube. This value does not
include extra information such as variance or weight arrays, FITS
headers, or any other NDF extensions. [512000000]

=back

=head1 NOTES

=over 4

=item *

This primitive is suitable for ACSIS.

=back

=head1 OUTPUT DATA

=over 4

=item *

A cube whose filename is of the form aYYYYMMDD_NNNNN_SS_cube.sdf,
where YYYYMMDD is the UT date, NNNN is the zero-padded observation
number, and SS is the zero-padded susbystem number.

=back

=head1 TASKS

SMURF: MAKECUBE.

=head1 REQUIRED PERL MODULES

None.

=head1 AUTHORS

Brad Cavanagh <b.cavanagh@jach.hawaii.edu>

=head1 COPYRIGHT

Copyright (C) 2006 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut

orac_print "Creating cube.\n";

# Deal with parameters.
my $maxsize = ( defined( $_CREATE_CUBE_FRAME_{MAXSIZE} ) ?
                $_CREATE_CUBE_FRAME_{MAXSIZE}            :
                512000000 );

my $bytes_per_pixel = ( defined( $_CREATE_CUBE_FRAME_{BYTES_PER_PIXEL} ) ?
                        $_CREATE_CUBE_FRAME_{BYTES_PER_PIXEL}            :
                        4 );

my $out;
my @files;

# Create a list of input images.
my $inlist = new ORAC::TempFile( OPEN => 1,
                                 SUFFIX => '.lis' );
my $inlist_fh = $inlist->handle;

foreach my $i ( 1..$Frm->nfiles ) {

  my ( $in, $tmp ) = $Frm->inout( "_cube", $i );

  if( $i == 1 ) {
    $out = $tmp;
  }

  print $inlist_fh "$in\n";
  push @files, $in;
}

$inlist->handle->close;

# First we need to run MAKECUBE without output to find out what the
# size of the output cube will be.
my $params = "in='^" . $inlist->file . "' out=! autogrid";

# If we have a raster (SAM_MODE header), then we need to set the pixel
# scale manually.

my $raster = ($Frm->hdr("SAM_MODE") =~ /raster/i);
my $pixsize = '';
if ($raster) {
  $pixsize = $Frm->hdr( "SCAN_VEL" ) * $Frm->hdr( "STEPTIME" );
  $params .= " pixsize=$pixsize";
}

orac_print " Determining size of output cube...";

$Mon{'smurf_mon'}->obeyw( "makecube", "$params" );

my ( $ORAC_STATUS, @lbnd ) = $Mon{'smurf_mon'}->get( "makecube", "lbound" );
( $ORAC_STATUS, my @ubnd ) = $Mon{'smurf_mon'}->get( "makecube", "ubound" );
( $ORAC_STATUS, my @flbnd ) = $Mon{'smurf_mon'}->get( "makecube", "flbnd" );
( $ORAC_STATUS, my @fubnd ) = $Mon{'smurf_mon'}->get( "makecube", "fubnd" );

orac_print "done.\n";

my $x_length = abs( $ubnd[0] - $lbnd[0] + 1 );
my $y_length = abs( $ubnd[1] - $lbnd[1] + 1 );
my $f_length = abs( $ubnd[2] - $lbnd[2] + 1 );
my $f_range = abs( $fubnd[2] - $flbnd[2] );

# Calculate the maximum number of available frequency channels.
my $max_freq_chan = int( $maxsize / ( $x_length * $y_length * $bytes_per_pixel ) );

# If the maximum number of available frequency channels is less than
# the number in the output cube, then we need to tell MAKECUBE to
# restrict the frequency bounds.
if( $max_freq_chan < $f_length ) {

  my $ratio = $max_freq_chan / $f_length;

  # We need to remove ( 1 - $ratio ) * $f_range / 2 from each end.
  my $upper = $fubnd[2] - ( 1 - $ratio ) * $f_range / 2;
  my $lower = $flbnd[2] + ( 1 - $ratio ) * $f_range / 2;

  $params = "specbounds='$lower,$upper' ";

  # Get the units of the frequency axis.
  $Mon{'ndfpack_mon'}->obeyw( "ndftrace", "ndf=$files[0] fullframe" );
  ( $ORAC_STATUS, my $funit ) = $Mon{'ndfpack_mon'}->get( "ndftrace", "funit" );

  # Tell the user what's going on.
  orac_print " Running MAKECUBE to create full cube would result in a file too large\n  for this computer to handle.\n";
  orac_print " Frequency range would have been ";
  orac_print sprintf( "%.3f to %.3f %s.\n", $flbnd[2], $fubnd[2], $funit );
  orac_print " Restricting frequency range to ";
  orac_print sprintf( "%.3f to %.3f %s.\n", $lower, $upper, $funit );

} else {

  $params = "";

}

# Set up the parameters to MAKECUBE.
$params .= "in='^" . $inlist->file . "' out=$out autogrid";

# If we have a raster (SAM_MODE header), then we need to set the pixel
# scale manually.
if( $raster ) {
  $params .= " pixsize=$pixsize";
}

# Tell user what we're doing.
orac_print( " Creating cube from " . join( ", ", @files ) . "..." );

# Run MAKECUBE.
$Mon{'smurf_mon'}->obeyw( "makecube", "$params" );

orac_print( "done.\nCube formed in $out.\n" );

# Update the Frm object.
$Frm->file( $out );

# Tidy-up output.
orac_print "\n";

