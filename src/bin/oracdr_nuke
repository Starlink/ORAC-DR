#!/usr/local/bin/perl

=head1 NAME

oracdr_nuke - kill all ORAC-DR related processes and shared memory

=head1 SYNOPSIS

  oracdr_nuke
  oracdr_nuke -nogaia -nogwm

=head1 DESCRIPTION

Attempt to kill all ORAC-DR related processes and shared memory that
can be found and that are associated with the current user.

=head1 OPTIONS

=over 4

=item B<-nogaia>

Do not kill any GAIA processes.

=item B<-nogwm>

Do not kill any GWM windows.

=item B<-help>

Provide simple help information.

=item B<-man>

Provides the manual page.

=back

=head1 NOTES

=over 4

=item *

All shared memory owned by the current user is removed even if
it is not directly associated with an ORAC-DR process.

=item * 

Will not attempt to remove shared memory owned by another user.

=item *

Will attempt to kill processes owned by other users even though
this will not succeed unless the user has special privilege.

=item *

Does not attempt to clear out ADAM_USER directories. This is not
normally a problem for ORAC-DR since each ORAC-DR process works
in a different ADAM_USER directory.

=back

=head1 AUTHORS

Frossie Economou (frossie@jach.hawaii.edu),
Tim Jenness (t.jenness@jach.hawaii.edu),
Alasdair Allan (aa@astro.ex.ac.uk),
Brad Cavanagh (b.cavanagh@jach.hawaii.edu)

=head1 COPYRIGHT

Copyright (C) 2010 Science and Technology Facilities Council.
Copyright (C) 1996-2006 Particle Physics and Astronomy Research Council.
All Rights Reserved.

=cut

use warnings;
use strict;

# ORAC modules
use lib $ENV{ORAC_PERL5LIB};
use ORAC::Nuke;

# General modules
use Pod::Usage;
use Getopt::Long;
use Sys::Hostname;

$| = 1;

# Argument check
my ($nogaia, $nogwm, $help, $man);

my $opt_status = GetOptions( "nogaia" => \$nogaia,
                            "nogwm" => \$nogwm,
                            "help" => \$help,
                            "man" => \$man );

pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;

# Get date and time
my $date = scalar(localtime);
my $host = hostname;

# Open log in $ORAC_DATA_OUT
if (exists $ENV{ORAC_DATA_OUT}) {

  open ( LOG, ">> $ENV{ORAC_DATA_OUT}/oracdr_nuke.log")
    || warn 'Could not open log file in ORAC_DATA_OUT';

  print LOG "$date by $ENV{'USER'} on $host (perl $]) OS $^O\n";

  close(LOG);
}

# Attempt to kill oracdr processes

print "Killing ORAC-DR processes...";
my $exclude = "oracdr_nuke";
$exclude .= "|gwm" if $nogwm;
orac_proc_kill('oracdr|MessageRelay', $exclude);
print "Done\n";

# Starlink processes

print "Killing Starlink processes...";
$exclude = "oracdr_nuke|start";
$exclude .= "|gaia" if $nogaia;
$exclude .= "|gwm" if $nogwm;

orac_proc_kill('\/star', $exclude);
print "Done\n";

# Shared memory
print "Removing shared memory...";
orac_ipcs_kill();
print "Done\n";

#
print "\n\t\toracdr_nuke- Nuke complete for user $ENV{'USER'}.\n\n";

exit;
